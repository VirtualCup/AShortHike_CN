title: Start
tags: 
colorID: 0
position: 3407,2277
---
dang my dialogue isn't written yet
my bad!
===
title: HomeSignStart
tags: 
colorID: 0
position: 1113,-793
---
<sprite tint=1 name="LeftArrow"> White Coast Trail
Rangers Cabin <sprite name="RightArrow" tint=1>
===
title: GoatStart
tags: 
colorID: 0
position: 5470,-1342
---
<<if $GoatWatchFound is true>>
    [[GoatDone]]
<<endif>>
<<if visited() is false>>
    excuse me do you have the time?
    <<SetSpeaker Player>>
    um yeah, let me chec-
    <<SetSpeaker Original>>
    see... normally i would know the time
    i just lost my watch
    <<SetSpeaker Player>>
    oh, um
    sorry
    <<SetSpeaker Original>>
    it was very expensive
    and priceless when you\nconsider the sentimental value
    <<SetSpeaker Player>>
    i see
    <<SetSpeaker Original>>
    if you find it, can you\nbring it back to me?
    <<SetSpeaker Player>>
    sure
    <<SetSpeaker Original>>
    please don't sell it on\nthe internet or whatever
    it's very valuable to me
    <<SetSpeaker Player>>
    no, don't worry
    i'll bring it right back
    <<SetSpeaker Original>>
    ok
    thanks
<<else>>
    <<if HasItem("Watch", 1) is true>>
        [[GoatGive]]
    <<else>>
        [[GoatExtra]]
    <<endif>>
<<endif>>
===
title: ThoughtsStart
tags: 
colorID: 0
position: 3342,3296
---
I'm very shy and sad :(
Sometimes I see cool people I recognize from the internet but i'm too afraid to say anything so I say nothing.
===
title: ArtistStart
tags: 
colorID: 0
position: 3135,3317
---
Empty Text
===
title: OpeningStart
tags: 
colorID: 0
position: 2345,3348
---
Aunt -
  you've been on ur phone for the past while
player - 
  ahh im just caught up in this thing, and i cant get my mind off of it
aunt - 
  you should for a hike
  dundas peak is really nice from top
player
  maybe I WILL!
===
title: Node7
tags: 
colorID: 0
position: 3018,3069
---
How did u get here if u cant swim??
===
title: BeachHutSign
tags: 
colorID: 0
position: 1541,-795
---
<sprite tint=1 name="UpArrow"> Rangers Cabin
<sprite name="DownArrow" tint=1> White Coast Trail
===
title: WhiteCoastSign
tags: 
colorID: 0
position: 1327,-794
---
<sprite tint=1 name="UpArrow"> Hawk Peak Trail\nWhite Coast Trail <sprite name="RightArrow" tint=1>
<sprite tint=1 name="DownArrow"> Sid Beach\n<sprite tint=1 name="DownArrow"> Visitor Center\n
===
title: Node10
tags: 
colorID: 0
position: 2775,3057
---
my house burnt down!!
my brother did it.
He's a jerk!
===
title: 
tags: 
colorID: 0
position: 3255,3020
---
ughhhh...
oh hayy 
===
title: WriterStart
tags: 
colorID: 0
position: 3496,3067
---

===
title: Node13
tags: 
colorID: 0
position: 2929,3325
---
it all turned out OK - the sentence wasn't so bad

oh.. thats pretty good i guess

i told you it would be fine!

yeah, im still mad tho.

sigh, i know

love you tho
thanks for never giving up on me

i never would!
love you too

hey guess what

what?

im at the top of hawk peak right now
OOOOH no wonder the reception is so good!
Are you going to use the Crystal Feather!?

yeah yeah, i plan to
that's so exciting! you'll be a natural
yeah yeah i know

===
title: Node14
tags: 
colorID: 0
position: 2661,3287
---
i dunno, going up the see the crystal feather or whatever
SAY WHAT!?
thats so awesome!
I wish i was a bird - you're so lucky.
yeah i know...
i dunno.
i feel kinda nervous about it
it's stupid i guess.
i know ill probably be fine but, ive kinda been avoiding it.
aw well, im sure you'll do great.
thanks
===
title: AuntRunOffStart
tags: 
colorID: 0
position: 3925,-2642
---
<<if $AuntIntro is false and $_ClimbedHawkPeak is false>>
    hey! where do you think you're going?
    <<SetSpeaker Player>>
    <<Face Original>>
    oh, um...
    <<SetSpeaker Original>>
    you're not going to just wander\noff without saying hi to old\nAunt May, are you?
    i haven't seen you all day!
    <<SetSpeaker Player>>
    sorry...
    i just, didn't feel like\ntalking i guess
    <<SetSpeaker Original>>
    hmm what's going on?
    <<SetSpeaker Player>>
    it's nothing!
    um
    i've just been waiting\naround for a call
    [[AuntIntroEnd]]
<<endif>>
===
title: AuntIntroStart
tags: 
colorID: 0
position: 4164,-2635
---
hey how's it going?
i haven't seen you all day
you been busy?
<<SetSpeaker Player>>
uh, kinda
well, not really
i've just been waiting\naround for a call
[[AuntIntroEnd]]


===
title: AuntStart
tags: 
colorID: 0
position: 4439,-2714
---
<<if HasItem("ShellNecklace", 1)>>
    [[AuntNecklace]]
<<elseif $_ClimbedHawkPeak is true>>
    [[AuntFinished]]
<<elseif $AuntIntro is false>>
    [[AuntIntroStart]]
<<else>>
    [[AuntChat]]
<<endif>>
===
title: AuntIntroEnd
tags: 
colorID: 0
position: 3923,-2377
---
<<SetSpeaker Original>>
well there's your problem!
there's no reception out here
<<SetSpeaker Player>>
wait...
WHAT!?
<<SetSpeaker Original>>
yeah, i mean, pretty much\nno reception
you might be able to get\nsome at <color=red>Hawk Peak</color>
<<SetSpeaker Player>>
oh... yeah i guess
but that's pretty far\nisn't it?
<<SetSpeaker Original>>
it's not that far!
we've all made the\ntrek before
i figured you would have\ngone already
<<SetSpeaker Player>>
oh... yeah
i've been meaning to go
but
i just
haven't gotten around to it yet
<<SetSpeaker Original>>
well today's as good a day\nas any!
<<SetSpeaker Player>>
...
<<SetSpeaker Original>>
just take <color=red>White Beach Trail</color>\nand head <color=red>north</color> at the fork
then follow the signs\nfor <color=red>Hawk Peak</color>
no problem!
<<set $AuntIntro to true>>
===
title: AuntChat
tags: 
colorID: 0
position: 4683,-2395
---
<<if visitCount() == 0>>
    <color=red>Hawk Peak</color> really is wonderful
    you're going to love it!
    <<SetSpeaker Player>>
    yeah... i've heard a lot about it
    <<SetSpeaker Original>>
    it's quite a hike, sure
    but our family has been going\nthere for generations
    <<SetSpeaker Player>>
    yup
    <<SetSpeaker Original>>
    the hot springs at the peak\ncreate these amazing updrafts
    the feeling of flying up there\nis incredible
    <<SetSpeaker Player>>
    but
    isn't it
    kind of dangerous?
    <<SetSpeaker Original>>
    not really
    it'll be no problem for you!
    <<SetSpeaker Player>>
    hmm
<<elseif visitCount() == 1>>
    so what kind of call\nare you waiting for?
    <<SetSpeaker Player>>
    oh.. um
    you know...
    ...
    the thing
    <<SetSpeaker Original>>
    ...
    ohhh! i'm sorry
    i forgot
    don't worry about it dear
    i'm sure it'll all work\nout fine
    i think
    a nice hike will help you\ntake your mind off it
<<elseif visitCount() == 2>>
    ahh sure is a nice day
    i love it here, you know?
    <<SetSpeaker Player>>
    yeah, it's nice
    the city's pretty good\ntoo though!
    it's got cool events
    great food
    and also
    cellphone reception!
    <<SetSpeaker Original>>
    we'll get reception too\nsoon, ya know!
    they're building a new\nradio tower
    <<SetSpeaker Player>>
    oh
    that'll be nice
<<else>>
    ahh, it's so lovely here
    <<SetEmotion Happy>>
    i could sit here all day
    <<ClearEmotion>>
    and i will
<<endif>>
===
title: KidStart
tags: 
colorID: 0
position: 3719,-868
---
<<if visited() is false>>
    CLAIRE!
    did you know...
    you can <color=red>hold {{JumpButton}} while running</color>\nto glide through the air?!?
    <<SetSpeaker Player>>
    of course!
    everyone knows that
    why do you ask?
    <<SetSpeaker Original>>
    i dunno
    i just had a feeling\nyou might want to know
    <<SetSpeaker Player>>
    um
    ok
<<elseif visitCount() == 1>>
    <<SetSpeaker Player>>
    wait, uh
    how do i glide again?
    <<SetSpeaker Original>>
    you <color=red>hold {{JumpButton}} while running</color> dummy!
    <<SetSpeaker Player>>
    cool just making sure\nyou haven't forgotten
    <<SetSpeaker Original>>
    i never forget anything!
    i'm a genius
<<elseif visitCount() == 2>>
    <<SetSpeaker Player>>
    ok one last time
    what was that thing you said?
    <<SetSpeaker Original>>
    <color=red>HOLD {{JumpButton}} WHILE RUNNING TO GLIDE!!!</color>
    <<SetSpeaker Player>>
    ok thanks
<<elseif visitCount() == 3>>
    <<SetSpeaker Player>>
    seriously just one last time
    <<SetSpeaker Original>>
    HOLD
    {{JumpButton}}
    TO
    GLIDE!!!!
    <<SetSpeaker Player>>
    ok i promise i won't forget
<<else>>
    <<SetSpeaker Player>>
    just one more-
    <<SetSpeaker Original>>
    NO!
    NOOOO!
    NOOOOOOOOOOOOOOOOOOO!
    NO MORE!!!!
<<endif>>
===
title: OtherKidStart2
tags: 
colorID: 0
position: 3066,2802
---
BECAUSE im a random KID OK!?
i want random things
its ur JOB to get the random things

thats not my job

its your JOB
bring me my stuff
===
title: OtherKidStart
tags: 
colorID: 0
position: 3720,-612
---
<<if $KidQuest1 is true>>
    [[OtherKid2]]
<<endif>>
<<if visitCount() is 0>>
    HEY YOU
    FIND ME 15 SHELLS!
    <<SetSpeaker Player>>
    what! why?
    <<SetSpeaker Original>>
    i want em
    <<SetSpeaker Player>>
    yeah, but why
    <<SetSpeaker Original>>
    it's a secret!
    <<SetSpeaker Player>>
    heh, ok
    i'll keep an eye out
    <<SetSpeaker Original>>
    keep BOTH eyes out, please!
<<elseif GetItemCount("Shell") >= 15>>
    <<SetSpeaker Player>>
    hey, i got your shells
    <<SetSpeaker Original>>
    ooooo!
    i'll take em!
    <<GiveItem Shell -15>>
    <<Hide>>
    <<Wait 1>>
    <<SetSpeaker Player>>
    so now what
    <<SetSpeaker Original>>
    what
    <<SetSpeaker Player>>
    i mean
    uh
    do i get anything
    <<SetSpeaker Original>>
    no!
    <<SetSpeaker Player>>
    geez, not even a thank you?
    that's not very nice
    <<SetSpeaker Original>>
    i do what it takes to survive!
    i never promised you anything
    <<SetSpeaker Player>>
    ...
    <<SetSpeaker Original>>
    actually
    i do have something for you
    a new job
    <<SetSpeaker Player>>
    i already have a job
    <<SetSpeaker Original>>
    well make this your side gig
    <<SetSpeaker Player>>
    what if i don't take it
    <<SetSpeaker Original>>
    it's your loss!
    good jobs are hard to find
    ...
    so what will it be?
    <<set $KidQuest1 to true>>
    [[ConsiderKidQuest]]
<<elseif GetItemCount("Shell") < 3 or visitCount() is 1>>
    <<SetSpeaker Player>>
    so what's the deal with the shells?
    <<SetSpeaker Original>>
    it's none of your business!
    except
    for the business of you\ngetting me 15 of them
<<else>>
    <<SetSpeaker Player>>
    i got {{Item_Shell}} shells so far!
    <<SetSpeaker Original>>
    hey can i ask you\na quick question
    <<SetSpeaker Player>>
    sure
    <<SetSpeaker Original>>
    is {{Item_Shell}} more or less than 15
    <<SetSpeaker Player>>
    uh,
    less?
    <<SetSpeaker Original>>
    yeah
    it is
    if you don't have the goods...
    ...then don't waste my time!
    <<SetSpeaker Player>>
    okay geez!
    <<SetSpeaker Original>>
    what can i say
    i run a tight ship
<<endif>>
===
title: Node23
tags: 
colorID: 0
position: 2845,2850
---
i'm a millionaire
oh that's neat
yeah i decided to put all my wealth into chests and leave them across the park
that seems expensive
it was
i think a good treasure hunt is ALWAYS worth it
the joy of discovering a chest full of goodies
===
title: FoxStart
tags: 
colorID: 0
position: 4844,471
---
<<if visited() is false>>
    hmmm
    i was planning on hiking\nto <color=red>Hawk Peak</color>
    but the path suddenly got really steep
    <<SetSpeaker Player>>
    do you think it's climbable?
    <<SetSpeaker Original>>
    well, it would be if i had\na <color=red>golden feather</color>
    apparently they sell them\nat the <color=red>Visitor Center</color>
    <<set $GoldenFeatherAdvice to true>>
<<elseif visitCount() is 1>>
    <<SetSpeaker Player>>
    so, how do <color=red>golden feathers</color> work?
    <<SetSpeaker Original>>
    well, when you've got\none in your pocket
    you'll feel much lighter
    that makes it easier to\nclimb up cliffs!
    <<SetSpeaker Player>>
    woah
    that's pretty cool
    <<SetSpeaker Original>>
    <<SetEmotion Happy>>
    it is indeed!
    <<ClearEmotion>>
<<elseif visitCount() is 2>>
    <<SetSpeaker Player>>
    so why are you going to Hawk Peak?
    <<SetSpeaker Original>>
    i'm going to take some great\nnature shots from the top
    i wouldn't call myself a photographer but...
    it's just a fun hobby
    <<SetSpeaker Player>>
    i'm going there too!
    <<SetSpeaker Original>>
    oh yeah?
    i heard it's a long hike
    <<SetSpeaker Player>>
    it is, eh?
    my aunt's always like
    "it's just a short hike away!"
    but she's a total outdoors geek
    so
    i think she underestimates it
    <<SetSpeaker Original>>
    i'd say so
    <<set $KnowsFoxIsPhotographer to true>>
<<else>>
    <<SetSpeaker Player>>
    <<SetEmotion Happy>>
    if we make it up at the same time
    you'll have to get my picture!
    <<SetSpeaker Original>>
    <<SetEmotion Happy>>
    sounds like a plan!
    <<SetSpeaker Player>>
    see ya there!
    <<ClearEmotion Player>>
    <<ClearEmotion Original>>
    <<set $RequestFoxPhoto to true>> 
<<endif>>

===
title: VisitorCenterStart
tags: 
colorID: 0
position: 4197,-885
---
<<set $TalkedToVisitorCenter to true>>
<<if $KnowsAboutTheStrawberries and GetHour() is 17 and visited() is true>>
    [[Strawberries]]
<<endif>>

<<if $_Artist6Talk is true and $JonEmbarrassed is false>>
    this is a little\nbit embarrassing
    i don't think i'm\nvery photogenic 
    but i'm glad i can\nhelp with his art!
    anyway
    <<set $JonEmbarrassed to true>>
<<elseif "ParkHat" is $_WornHat>>
    lookin' sharp in that hat!
<<else>>
    howdy!
<<endif>>

<<if visited() is true>>
    what's up?
    <<SetSpeaker Player>>
    <<ShowCollection Coin>>
    -> can I buy a <color=red>golden feather?</color>
        <<HideCollection Coin>>
        <<SetSpeaker Original>>
        [[BuyFeatherStart]]
    -> what kind of merch do you sell?
        <<HideCollection Coin>>
        [[BuyMerchStart]]
    -> nothing much
        <<HideCollection Coin>>
        <<SetSpeaker Original>>
        yup
        that's how it goes
<<else>>
    how can i help you?
    <<SetSpeaker Player>>
    <<if $GoldenFeatherAdvice is true>>
        i heard you sell <color=red>\ngolden feathers</color> here
        <<SetSpeaker Original>>
        ah, we do!
        you can buy one\nfor 40 coins
        <<SetSpeaker Player>>
        <<ShowCollection Coin>>
        -> i'll take it!
            <<HideCollection Coin>>
            [[BuyFeatherStart]]
        -> what else do you sell?
            <<HideCollection Coin>>
            <<SetSpeaker Original>>
            we also sell provincial\npark merchandise
            [[BuyMerchStart]]
        -> i'm good for now
            <<HideCollection Coin>>
            <<SetSpeaker Original>>
            alrighty, take care
    <<else>>
        um,
        i dunno
        i guess i'm fine
        <<SetSpeaker Original>>
        alrighty!
        let me know if you need directions\nor want to buy anything
        <<SetSpeaker Player>>
        oh, what are you selling?
        <<SetSpeaker Original>>
        well, we have provincial\npark merch
        and some hiking supplies\nlike <color=red>golden feathers</color>
        <<SetSpeaker Player>>
        what are those?
        they sound cool
        <<SetSpeaker Original>>
        they are cool!
        they make rock climbing easier
        and in your case
        will also help you fly
        <<SetSpeaker Player>>
        !!
        <<SetSpeaker Original>>
        you can buy one for 40 coins
        <<SetSpeaker Player>>
        <<ShowCollection Coin>>
        -> i'll take it!!
            <<HideCollection Coin>>
            [[BuyFeatherStart]]
        -> what kind of merch do you sell?
            <<HideCollection Coin>>
            [[BuyMerchStart]]
        -> i'm good for now
            <<HideCollection Coin>>
            <<SetSpeaker Original>>
            alrighty, take care
    <<endif>>
<<endif>>
===
title: BuyFeatherStart
tags: 
colorID: 0
position: 4445,-606
---
<<if $FeathersSold >= 2>>
    <<SetSpeaker Original>>
    actually, we're all sold out
    <color=red>a boy</color> came by earlier\nand bought most of the\nremaining stock
    he must be going\non some hike!
<<elseif HasItem("Coin", 40) is true>>
    <<SetSpeaker Original>>
    sure, here ya go!
    <<set $FeathersSold to $FeathersSold + 1>>
    <<GiveItem Coin -40>>
    <<PlaySound BuySound>>
    <<Hide>>
    <<Wait 1>>
    <<GiveItem GoldenFeather 1>>
    <<if $FeathersSold is 2>>
        <<set $HeardAboutBoy to true>>
        that's actually all\nthe feathers we have
        <color=red>a boy</color> came by earlier\nand bought most our\nremaining stock
        you'll have to find\nmore elsewhere
        sorry!
    <<endif>>
    <<if $FeathersSold is 1>>
        [[ExplainGoldenFeather]]
    <<endif>>
<<else>>
    <<SetSpeaker Original>>
    sure! that'll be 40 coins
    <<SetSpeaker Player>>
    <<if $NotEnoughYet is false>>
        oh wait
        i don't have enough money
        ...
        yet!
        <<set $NotEnoughYet to true>>
    <<else>>
        oh
        yeah i forgot
        i still need to find more cash
    <<endif>>
<<endif>>
===
title: VistorCenterCrossroadsStart
tags: 
colorID: 0
position: 1756,-801
---
<sprite name="UpArrow" tint=1> Visitor Center\n<sprite tint=1 name="LeftArrow"> Hawk Peak Trail
Sid Beach <sprite tint=1 name="RightArrow">\n<sprite tint=1 name="DownArrow"> Shirley's Point
===
title: Node29
tags: 
colorID: 0
position: 2505,3055
---
every single brushstroke
im applying with precision
every leaf, branch, flower -
its all placed by me
i wonder if people even notice the 2 hours i spent arranging these flowers
===
title: KioskSignStart
tags: 
colorID: 0
position: 1114,-571
---
<sprite name="UpArrow" tint=1> Blackwood Trail\n<sprite name="LeftArrow" tint=1> Hawk Peak Trail
<sprite name="DownArrow" tint=1> Visitor Center\nWhite Beach Trail <sprite name="RightArrow" tint=1>
===
title: HawkPeakRiverSignStart
tags: 
colorID: 0
position: 1341,-571
---
<sprite name="UpArrow" tint=1> Hawk Peak Trail\n<sprite name="UpArrow" tint=1> Good Creek Path
<sprite name="LeftArrow" tint=1> Royal Ridge\nBlackwood Trail <sprite name="RightArrow" tint=1>
===
title: RoyalRidgeSignStart
tags: 
colorID: 0
position: 1556,-561
---
<sprite name="UpArrow" tint=1> Hawk Peak Trail\n<sprite name="UpArrow" tint=1> Good Creek Path
<sprite name="DownArrow" tint=1> Blackwood Trail\n<sprite name="LeftArrow" tint=1> Royal Ridge
===
title: Node33
tags: 
colorID: 0
position: 2625,2782
---
i want to make art
i want to make people happy
but i also want to make money
===
title: FireTowerSnowSignStart
tags: 
colorID: 0
position: 1768,-561
---
<sprite name="UpArrow" tint=1> Hawk Peak Trail\n<sprite name="DownArrow" tint=1> Good Creek Path\nMeteor Lake <sprite name="RightArrow" tint=1>
===
title: HawkPeakSignStart
tags: 
colorID: 0
position: 1116,-350
---
<<set $ReadHawkPeakSign to true>>
<sprite name="UpArrow" tint=1> Hawk Peak Trail
At least <color=yellow>7 Golden Feathers</color> are\nrequired to complete this hike.
Novice hikers should consider using more.
<<if $Climbed1 is false and $Climbed2 is false>>
    <sprite tint=1 name="Warning"> HIKING TIP <sprite tint=1 name="Warning">
    climbing uses less stamina than flying!
    to climb, push against a wall and hold {{JumpButton}}
<<endif>>
===
title: MeteorLakeSignStart
tags: 
colorID: 0
position: 1336,-360
---
<sprite name="LeftArrow" tint=1> Meteor Lake Overlook <sprite name="RightArrow" tint=1>\n<sprite name="LeftArrow" tint=1> Hawk Peak Trail\n<sprite name="DownArrow" tint=1> Blackwood Trail
===
title: MeteorLakeLeftSignStart
tags: 
colorID: 0
position: 1556,-351
---
<sprite name="LeftArrow" tint=1> Meteor Lake Overlook <sprite name="RightArrow" tint=1>\n<sprite name="DownArrow" tint=1> Hawk Peak Trail
===
title: Node38
tags: 
colorID: 0
position: 2256,3074
---
i cant wait to fall asleep at night
so i can wake up and eat breakfast the next morning
===
title: BeachFoxStart
tags: 
colorID: 0
position: 4682,-628
---
<<if HasItem("Compass", 1) is false>>
    <<SetEmotion Happy>>
    hey claire!
    how's the day treatin' ya?
    <<ClearEmotion>>
    <<SetSpeaker Player>>
    well
    i'm just getting out\nof the house now
    <<SetSpeaker Original>>
    ah
    it's one of those kinds of days
    <<SetSpeaker Player>>
    yeah...
    i've been kinda
    caught up in my head all morning
    <<SetSpeaker Original>>
    hmm
    would you say you feel...
    ...lost and directionless?
    <<SetSpeaker Player>>
    uh
    maybe
    <<SetSpeaker Original>>
    welp!
    i've got just the thing for you!
    take this
    <<Hide>>
    <<GiveItem Compass 1>>
    <<Face Original Player>>
    to use it
    <color=red>press {{MenuButton}}</color> to open\nyour inventory
    then select it from the <color=red>items menu</color>
    <<SetSpeaker Player>>
    oh, uh
    thanks
    this should come in handy
    <<SetSpeaker Original>>
    <<SetEmotion Happy>>
    happy hiking!
    <<ClearEmotion>>
<<else>>
    <<if $_ShowCompass is false>>
        <<SetSpeaker Player>>
        how do i use my compass again?
        <<SetSpeaker Original>>
        just <color=red>press {{MenuButton}}</color> to\n<color=red>open your inventory</color>
        then select it from\nthe <color=red>items menu</color>
        <<SetSpeaker Player>>
        ah right
        thanks!
    <<else>>
        <<SetSpeaker Player>>
        thanks for lending me the compass
        i'll get it back to you\nwhen i'm done my hike
        <<SetSpeaker Original>>
        oh, nah
        just keep it
        you know what they say...
        it's good luck to know\nwhich way is north!
        <<SetSpeaker Player>>
        uh
        do they say that?
        <<SetSpeaker Original>>
        actually, i'm not sure
        but
        it certainly can't be bad luck!
    <<endif>>
<<endif>>
===
title: VisitorsCenterSignStart
tags: 
colorID: 0
position: 1597,-123
---
Welcome to Hawk Peak Provincial Park!
Enjoy a scenic trek to Outlook Point\nor follow <color=yellow>Hawk Peak Trail</color> up to the\nhighest point in the province!
Although this island is known for\nits titular mountain peak,
It is also home to a number of\nbeautiful beaches and forests!
Did you know?
The infamous local dish\n'Strawberries and Toast'\nwas invented HERE at Hawk Peak!
Please join us at 5PM to\nenjoy a delicious serving\nof the historical dish!
<<set $KnowsAboutTheStrawberries to true>>

===
title: BuyMerchStart
tags: 
colorID: 0
position: 3946,-608
---
<<SetSpeaker Original>>
<<if visited() is false>>
    we're kinda low on stock
    but that's good, actually
    it's been a busy season!
    provincial parks must be\ntrending or something
    <<SetSpeaker Player>>
    it's possible!
    <<SetSpeaker Original>>
    anyway
<<endif>>
<<SetSpeaker Original>>
<<if $SoldHat is false>>
    <<set $KnowsAboutMerchHat to true>> 
    we only have one hat left
    it costs 100 coins
    <<SetSpeaker Player>>
    <<ShowCollection Coin>>
    -> i'll take it!
        <<HideCollection Coin>>
        <<if HasItem("Coin", 100) is true>>
            <<GiveItem Coin -100>>
            <<PlaySound BuySound>>
            <<Hide>>
            <<Wait 1>>
            <<GiveItem ParkHat 1>>
            <<set $SoldHat to true>>
            <<SetSpeaker Original>>
            why don't you try it on?
            <<Hide>>
            <<Wait 1>>
            <<if HasItem("GoldenFeather", 1) is false>>
                hmm, you're all out of cash now...
                how about I throw in a\nfree golden feather!
                <<set $FeathersSold to $FeathersSold + 1>>
                <<Hide>>
                <<GiveItem GoldenFeather 1>>
                [[ExplainGoldenFeather]]
            <<endif>>
        <<else>>
            <<SetSpeaker Original>>
            oh uhhh,
            i don't think...
            you can afford it
            <<SetSpeaker Player>>
            oh
            right
        <<endif>>
    -> nah, i'll pass
        <<HideCollection Coin>>
        <<SetSpeaker Original>>
        okay!
<<else>>
    we're all out!
    you bought the last hat
    <<SetSpeaker Player>>
    are you guys gonna order more?
    <<SetSpeaker Original>>
    not until next season
    each season is a limited run
    <<SetSpeaker Player>>
    i guess i'm lucky i\ngot the last one!
    <<SetSpeaker Original>>
    haha, i suppose so!
<<endif>>
===
title: ExplainGoldenFeather
tags: 
colorID: 0
position: 4214,-359
---
let me explain how they work!
<<Face Original Player>>
if you press <color=red>{{JumpButton}}</color> while\nin midair, you can flap your\nwings for a double jump!
but that's not all!
to climb a wall, push against it and <color=red>hold {{JumpButton}}</color>
the more golden feathers you\nhave, the more you'll be able\nto climb and fly
have fun out there!
===
title: HikingDalmationStart
tags: 
colorID: 0
position: 5705,-930
---
<<set $HikerTalkCount to $HikerTalkCount + 1>>
<<if GetSpeakerY() > 250.5>>
    [[AtTopOfHike]]
<<else>>
    <<if GetNPCSpeakerNextNode() < 18>>
        [[HikingUp]]
    <<else>>
        [[HikingDown]]
    <<endif>>
<<endif>>
===
title: HikingUp
tags: 
colorID: 0
position: 5449,-634
---
<<if $HikerTalkCount is 1>>
    heyo!
    how's the hike treating you?
    <<SetSpeaker Player>>
    oh, uh
    it's been nice
    it's really beautiful here
    <<SetSpeaker Original>>
    i know right!
    <<SetEmotion Happy>>
    the view from the top will be...
    SO
    GOOD!!
    <<SetSpeaker Player>>
    you're going to <color=red>Hawk Peak</color> too!?
    <<SetSpeaker Original>>
    <<ClearEmotion>>
    oh, no
    that's too intense for me
    <<SetSpeaker Player>>
    ...
    <<SetSpeaker Original>>
    i'm just going to Outlook Point
    will i see ya there?
    <<SetSpeaker Player>>
    -> yeah, i'll meet you at the top!
        <<SetSpeaker Original>>
        <<SetEmotion Happy>>
        awesome!
        catch ya later!
    -> uh, maybe
        well, see ya around!
<<else>>
    <<if $MetHikerAtTop is true>>
        i'm heading back up to the tower
        i gotta get that exercise in
        see ya around!
    <<else>>
        <<if $DumbLore is false>>
            Good Creek Path is really pretty
            a long time ago, it used to\nbranch off to Harbord Ridge
            but the path was damaged in\na rainstorm and they took\nthe signs down
            <<set $DumbLore to true>>
        <<else>>
            <<SetEmotion Happy>>
            what a pretty day!
            <<ClearEmotion>>
        <<endif>>
    <<endif>>
<<endif>>
===
title: HikingDown
tags: 
colorID: 0
position: 5701,-639
---
<<if $HikerTalkCount is 1>>
    heyo!
    how's the hike treating you?
    <<SetSpeaker Player>>
    oh, uh
    it's been nice
    it's really beautiful here
    <<SetSpeaker Original>>
    i know right!
    <<SetEmotion Happy>>
    the view from the top was...
    SO
    GOOD!!
    <<SetSpeaker Player>>
    you made it up to <color=red>Hawk Peak</color>!?
    <<SetSpeaker Original>>
    <<ClearEmotion>>
    oh, no
    that's too intense for me
    <<SetSpeaker Player>>
    ...
    <<SetSpeaker Original>>
    i've just come from Outlook Point
    i'm gonna do a few rounds\nto get my exercise in
    not to brag, but
    i'm pretty fast
    maybe i'll see you\nup there in a few!
    <<SetSpeaker Player>>
    -> yeah, i'll meet you at the top!
        <<SetSpeaker Original>>
        <<SetEmotion Happy>>
        awesome!
        catch ya later!
    -> uh, maybe
        well, see ya around!
<<else>>
    <<if $MetHikerAtTop is true>>
        <<if $HikeDownChitChat is false>>
            man
            i wished i lived here
            i would do this hike every single day
            and i'd never get tired of it
            <<SetSpeaker Player>>
            you could get a job at the park!
            my aunt works here, and she\ngets to live here all summer
            <<SetSpeaker Original>>
            *sigh*
            i wish
            i can't just up and\nleave the city
            <<SetSpeaker Player>>
            -> well, you could!
                i mean, it wouldn't be easy
                might be worth it though
                <<SetSpeaker Original>>
                ha, yeah...
                maybe someday!
            -> yeah, i get it
                you'd have to leave behind, like
                your friends
                your apartment
                your job...
                <<SetSpeaker Original>>
                yeah, it would tough
            <<set $HikeDownChitChat to true>>
        <<else>>
            well, i gotta get my hike on!
            catch ya later!
        <<endif>>
    <<else>>
        heyyooo
        i was just up at Outlook Point
        i didn't see ya there though
        <<SetSpeaker Player>>
        whoops!
        <<SetSpeaker Original>>
        you gotta check it out though
        the view is fantastic.
        i'm planning on making a few rounds
        gotta get that exercise in!
    <<endif>>
<<endif>>
===
title: AtTopOfHike
tags: 
colorID: 0
position: 5974,-646
---
<<if $HikerTalkCount is 1>>
    man
    you can really feel the wind up here!
    i love this
    it makes me feel so...
    happy
    <<SetSpeaker Player>>
    yeah
    it's good
    [[GetMapFromHiker]]
<<else>>
    <<if $MetHikerAtTop is false>>
        hey!
        good to see ya up here
        is it just me
        or is this kinda epic
        <<SetSpeaker Player>>
        -> yeah, it's epic
            <<SetSpeaker Original>>
            yes!
            the wind in my face
            it makes me feel so good
        -> it's OK
            <<SetSpeaker Original>>
            it's OK?!
            look at all this!
            it's EPIC
            you can see like,
            the whole island!
        [[GetMapFromHiker]]
    <<else>>
        i love it up here!
        ...
        i should have brought\nsome snacks though
        when you get to the\ntop of a mountain...
        you gotta have a snack!
        it just seems right.
    <<endif>>
<<endif>>
===
title: TreasureMapsStart
tags: 
colorID: 0
position: 2413,-657
---
<<if GetItemCount("TreasureMap") == 1>>
    <<if $_TMap1 is true>>
        [[TMap1]]
    <<endif>>
    <<if $_TMap2 is true>>
        [[TMap2]]
    <<endif>>
    <<if $_TMap3 is true>>
        [[TMap3]]
    <<endif>>
    <<if $_TMap4 is true>>
        [[TMap4]]
    <<endif>>
<<else>>
    Which map should I read?
    <<if $_TMap1 is true>>
        [[Fallen Star|TMap1]]
    <<endif>>
    <<if $_TMap2 is true>>
        [[In Her Shadow|TMap2]]
    <<endif>>
    <<if $_TMap3 is true>>
        [[The King's Throne|TMap3]]
    <<endif>>
    <<if $_TMap4 is true>>
        [[The Treasure of Sid Beach|TMap4]]
    <<endif>>
<<endif>>
===
title: TMap1
tags: 
colorID: 0
position: 2405,89
---

===
title: TMap2
tags: 
colorID: 0
position: 2786,-313
---
it says...
<<SetStyle MapTextBoxProfile>>
His vessel was lost on the water
But the beacon found and caught her
So he buried his trade
At the end of its shade
To keep safe for the lightkeeper's daughter
<<SetSpeaker Player>>
<<if $_Unearthed_LighthouseShadow>>
    i think i've already found this one!
<<endif>>
===
title: GetMapFromHiker
tags: 
colorID: 0
position: 5975,-416
---
<<SetSpeaker Original>>
the visitor center is\nso tiny from here!
heh
oh, that reminds me!
take this
<<Hide>>
<<GiveItem TreasureMap 1>>
<<set $_TMap4 to true>>
apparently there's some sort of\ntreasure buried at Sid Beach
i couldn't find anything, maybe\nyou'll have better luck!
<<set $MetHikerAtTop to true>>
===
title: Node54
tags: 
colorID: 0
position: 3340,2765
---
i don't know how to end my novel!
i usually write action stuff, but im trying to write a peaceful book
its just about life...
but im worried it's boring!
===
title: OtherKid2
tags: 
colorID: 0
position: 3850,-304
---
<<if $AcceptedKidQuest2 is false>>
    so
    have you come CRAWLING\nback to reconsider?
    [[ConsiderKidQuest]]    
<<elseif HasItem("ShellNecklace", 1)>>
    how's the delivery going?
    <<SetSpeaker Player>>
    i'm working on it
    <<SetSpeaker Original>>
    not hard enough apparently
<<elseif $KidDone is false>>
    so my sources say
    you've delivered the goods
    <<SetSpeaker Player>>
    your sources are correct
    ...
    so what about that pay
    <<SetSpeaker Original>>
    ok ok
    here ya go
    <<Hide>>
    <<GiveItem Shell 10>>
    <<Wait 1>>
    <<SetSpeaker Player>>
    ...
    <<SetSpeaker Original>>
    enjoy your shells
    <<set $KidDone to true>>
<<else>>
    <<SetSpeaker Player>>
    so what's your deal
    <<SetSpeaker Original>>
    i deal in shells
    <<SetSpeaker Player>>
    hmm
<<endif>>
===
title: ToughBirdStart
tags: 
colorID: 0
position: 4450,-1328
---
<<if $ToughBirdSellIntro is true>>
    [[ToughBirdSell]]
<<endif>>

hey kid
you wanna buy some golden feathers?
<<SetSpeaker Player>>
-> yeah ok
    <<SetSpeaker Original>>
    heh, cool
    it'll be 100 coins per feather
    <<SetSpeaker Player>>
    ..but
    that's more than twice as\nmuch as the visitor center!
    <<SetSpeaker Original>>
    go buy feathers from them then
    <<if $HeardAboutBoy is true>>
        <<SetSpeaker Player>>
        but
        they're all sold out!
        <<SetSpeaker Original>>
        heh
        i guess you're out of luck
        ...
    <<else>>
        <<SetSpeaker Player>>
        maybe i will!
        <<SetSpeaker Original>>
        ...
    <<endif>>
    <<set $ToughBirdSellIntro to true>>
    [[ToughBirdSell]]
-> nah i'm ok
    <<SetSpeaker Original>>
    whatever, suit yourself
    
===
title: ToughBirdSell
tags: 
colorID: 0
position: 4760,-1329
---
<<if $ToughBirdFriend is true>>
    hey
    what's up?
    <<SetSpeaker Player>>
    <<ShowCollection Coin>>
    [[can i buy a feather?|ToughBirdBuyFeatherNice]]
    [[see ya|ToughBirdExitNeutral]]
<<else>>
    <<if $ToughBirdSales >= 4>>
        dang, you bought all my feathers...
        <<SetSpeaker Player>>
        <<ShowCollection Coin>>
        <<if $ToughBirdsProblem is false>>
            [[what's your deal?|ToughBirdWhatDeal]]
            [[see ya|ToughBirdExitNeutral]]
        <<else>>
            <<if HasItem("Coin", 400) is true>>
                [[do you want money for tuition?|ToughBirdTuition]]
                [[see ya|ToughBirdExitNeutral]]
            <<else>>
                [[what's up?|ToughBirdWhatsUp]]
                [[see ya|ToughBirdExitNeutral]]
            <<endif>>
        <<endif>>
    <<else>>
        <<SetSpeaker Original>>
        ...so you gonna buy\nmy feathers or what?
        <<SetSpeaker Player>>
        <<ShowCollection Coin>>
        [[fine|ToughBirdBuyFeather]]
        [[nope!|ToughBirdExit]]
        <<if $ToughBirdSales > 0>>
            <<if $ToughBirdsProblem is false>>
                [[what's your deal?|ToughBirdWhatDeal]]
            <<else>>
                <<if HasItem("Coin", 400) is true>>
                    [[do you want money for tuition?|ToughBirdTuition]]
                <<else>>
                    [[what's up?|ToughBirdWhatsUp]]
                <<endif>>
            <<endif>>
        <<endif>>
    <<endif>>
<<endif>>
===
title: ToughBirdBuyFeather
tags: 
colorID: 0
position: 4755,-1714
---
<<HideCollection Coin>>
<<SetSpeaker Original>>
<<if HasItem("Coin", 100) is true>>
    heh
    alrighty then, pay up
    <<set $ToughBirdSales to $ToughBirdSales + 1>>
    <<GiveItem Coin -100>>
    <<PlaySound BuySound>>
    <<Hide>>
    <<Wait 1>>
    <<GiveItem GoldenFeather 1>>
    smell ya later
<<else>>
    um
    you don't even have 100 coins
    <<SetSpeaker Player>>
    ...
    <<SetSpeaker Original>>
    i'm not gonna lower my prices
    i provide a service!
<<endif>>
===
title: CheckWatch
tags: 
colorID: 0
position: 2193,-655
---
it's {{Time}}
===
title: TMap3
tags: 
colorID: 0
position: 2212,-314
---
it says...
<<SetStyle MapTextBoxProfile>>
South of the stone tower\nAcross the river bridge
A lonely seat sits crumbling\nUpon the Royal Ridge
<<SetSpeaker Player>>
<<if $_Unearthed_KingsThrone>>
    i think i've already found this one!    
<<endif>>
===
title: TMap4
tags: 
colorID: 0
position: 2543,-317
---
it says...
<<SetStyle MapTextBoxProfile>>
I hid my treasure from the world\nBut never far from reach
It rests between the mossy stones\nThat lie upon Sid Beach
<<SetSpeaker Player>>
<<if $_Unearthed_SidBeachTreasure>>
    i think i've already found this one!    
<<endif>>
===
title: RunRabbitStart
tags: 
colorID: 0
position: 3043,431
---
it's dangerous to go slow!
that's why
i go fast
===
title: IceHikerStart
tags: 
colorID: 0
position: 4846,700
---
<<set $_MetIceHiker to true>>
    
<<if $_FoxClimbedToTop is true>> 
    [[IceHikerPeak]] 
<<elseif visited() is false>>
    <<Face Player>>
    <<if $GoldenFeatherAdvice is true>>
        hey it's you again!
    <<else>> 
        hey!
    <<endif>> 
    we're almost at the top...
    but
    it looks like the\nbridge collapsed?
    ...
    there might be <color=red>another way</color> up...
    or
    i could just go collect\nmore golden feathers?
<<elseif $_ClimbedHawkPeak is true>>
    <<Face Player>>
    [[IceHikerAfterPeak]]
<<elseif visitCount() is 1>>
    <<Face Player>>
    dang
    i really wanted to get\na photo from the top
    i'm not sure what\ni'll do now
    ...
    i heard
    there used to be a route\nthat went along the dark\nside of the mountain
    but
    they closed it a long time ago
    i wonder if it's still usable?
    <<set $KnowsFoxIsPhotographer to true>> 
<<else>>
    <<Face Player>>
    if you manage to find a way up
    can you let me know?
    <<SetSpeaker Player>>
    sure!
    i'll come back for you
<<endif>>
===
title: BeachHikerStart
tags: 
colorID: 0
position: 4570,93
---
<<if visited() is false>>
    there's gonna be a marathon\nrace at Meteor Lake today!
    the competition this year is fierce!
    <<set $KnowsAboutRace to true>>
<<elseif visitCount() is 1>>
    my nephew is running in the marathon
    he's been out training all morning
    if you see him out there,
    make sure he's taking breaks!
    <<set $NephewHi to true>>
<<else>>
    also make sure he's\ndrinking lots of water!
    never overlook hydration!
    <<set $Hydration to true>>
<<endif>>
===
title: LowerHikerStart
tags: 
colorID: 0
position: 4323,96
---
<<if visited() is false>>
    i've heard some rumors
    do you like rumors?
    <<SetSpeaker Player>>
    i dabble
    <<SetSpeaker Original>>
    ok hear me out
    jim just bought a metal detector
    he was running around yesterday
    looking for stuff around the island
    it was going nutso!
    seems like there's stuff\nburied all over the island?!
    <<SetSpeaker Player>>
    what kind of stuff?
    <<SetSpeaker Original>>
    i dunno
    metal stuff?
<<elseif visitCount() is 1>>
    jim doesn't actually own <color=red>a shovel</color>
    so...
    he wasn't able to dig any of it up
<<elseif visitCount() is 2>>
    ok so you wanna hear another rumor
    <<SetSpeaker Player>>
    sure why not
    <<SetSpeaker Original>>
    so deborah was up at\nOutlook Point the other day
    she said, that when she\nlooked through the binoculars
    she could see a...
    MYSTERIOUS ISLAND
    <<SetSpeaker Player>>
    how mysterious!!
<<else>>
    so you want to hear another rumor?
    <<SetSpeaker Player>>
    alright
    <<SetSpeaker Original>>
    well i'm all out
    <<SetSpeaker Player>>
    that's ok
<<endif>>
===
title: FrogMiniStart
tags: 
colorID: 0
position: 5086,-397
---
<<if $_GotShovel is true>>
    <<if $_BuiltMoreSandcastles is true>>
        [[FrogElections]]
    <<else>>
        aww heck
        my productivity is\ngonna skyrocket with\na shovel like this
        i'll catch ya later,
        i've got a lot\nof work to do
    <<endif>>
<<else>>
    <<if HasItem("ToyShovel", 1) is true and $AskedForShovel is true>>
        <<SetSpeaker Player>>
        hey check out this\nshovel i found
        it might be a little more your size
        [[FrogTrade]]
    <<else>>
        <<SetSpeaker Player>>
        hey dude
        <<if visited() is false>>
            what's happening
            <<SetSpeaker Original>>
            oh, you know!
            just jammin' out on the beach
            building sandcastles\nand what not
            <<SetSpeaker Player>>
            sounds like a good time
            <<SetSpeaker Original>>
            you know it
        <<endif>>
        <<SetSpeaker Player>>
        -> can i borrow your shovel?
            <<if $AskedForShovel is false>>
                <<SetSpeaker Original>>
                sorry bud
                i need it for sandcastle construction
                <<SetSpeaker Player>>
                isn't it a little big for that?
                <<SetSpeaker Original>>
                yeah but
                that's just how shovels are
                <<SetSpeaker Player>>
                you can get smaller ones
                ones made for sandcastles
                <<SetSpeaker Original>>
                whaat?
                sounds fake
                <<if HasItem("ToyShovel", 1) is true>>
                    <<SetSpeaker Player>>
                    i've got one on me!
                    here, take a look at this
                    <<Hide>>
                    <<Wait 1>>
                    [[FrogTrade]]
                <<else>>
                    but if you can show me one
                    i'd be glad to trade
                    <<set $AskedForShovel to true>>
                <<endif>>
            <<else>>
                <<SetSpeaker Original>>
                if you can find me\na better suited shovel
                i'd be totes down to trade
            <<endif>>
        -> see ya later
            <<SetSpeaker Original>>
            keep it fresh!
    <<endif>>
<<endif>>
===
title: RunnerStart
tags: 
colorID: 0
position: 3559,2558
---
Do you want to know the secret of speed?
<<SetSpeaker Player>>
-> Yes
    <<SetSpeaker Original>>    
    i knew it!
    well, first you have to get me 
    a cool toy
    <<SetSpeaker Player>>
    geez, people sure want me to get a lot of junk for them today
-> No
    <<SetSpeaker Original>>
    oh, ok
===
title: FrogElections
tags: 
colorID: 0
position: 5336,-399
---
<<if $_BuiltSandProvince is true>>
    [[FrogProvincialElections]]
<<endif>>
<<if visited() is false>>
    my sandcastle is now\npart of a sandcity!
    and also
    i'm running in a HOTLY\ncontested election for mayor
    oh hold on...
    the election results\nare JUST coming in!
    <<SetSpeaker Player>>
    !!
    <<SetSpeaker Original>>
    okay okay
    im tallying them up
    ...
    THIRD PLACE!?
    whaaat!
    i built this city!
    <<set $_CityElection to true>>
<<else>>
    <<SetSpeaker Player>>
    sorry about the loss
    <<SetSpeaker Original>>
    it was a tough race
    but it's ok
    i managed to secure a seat\non the city council
<<endif>>
===
title: DebugFoxStart
tags: 
colorID: 0
position: 4408,2786
---
<<if visited() is false>>
    Hey Hand Eye Society!
    this is a note from the developer
    im still working out the\ngame progression and dialog
    until i have stuff better sorted out
    take this free stuff
    <<Hide>>
    <<GiveItem GoldenFeather 1>>
    <<GiveItem RunningShoes 1>>
    you can hold <color=red>{{RunButton}}</color> to\nrun with the running shoes
<<else>>
    thanks for playing!
<<endif>>
===
title: MeetToughStart
tags: 
colorID: 0
position: 3914,2882
---
hey

uh..
hi?
what do you want

oh uh
nothing
i'm just hiking up to Hawk Peak

-> nothing
-> i dunno

------------------
hey, you

huh, me?

yeah
where are you going?

what does it matter to you

well, if you're going to <color=red>Hawk Peak,</color>
you're gonna have a hard time

what

just 
are you going up to <color=red>Hawk Peak?</color>


===
title: Nice Kid Start
tags: 
colorID: 0
position: 4160,2876
---
Empty Text
===
title: Plot
tags: 
colorID: 0
position: 3758,3182
---
plot 1
- the player is waiting for a call
  > shown at the beginning of the game
- her mother has surgery, and she's anxious about it
  > talking to the friend will reveal this
- she's going on a walk to distract herself
  > talking to the friend reveals this
- as she continues, she appreciates the world around her and gets out of her mind
  > when she gets to the top of the mountain
- when she gets near the top, she's worried she won't make it
  > her friend is waiting for her when she gets to the ledge, 

plot 2
- the player is avoiding doing something she's afraid of
  > talking to the tough bird she'll admit that
- she's pressured to climb the mountain, but she's scared she won't be able to do it
  > talking to the tough bird will reveal this
- it seems scary, but it turns out it's possible, and even fun
  > she talks to the tough bird 
- she makes it to the top, and is proud of herself
  > the tough bird makes it to the top
===
title: RaceRabbitStart
tags: 
colorID: 0
position: 6297,-843
---
<<if "Headband" is $_WornHat and $BunnyQuestDone is false>>
    hmm
    uh
    <<SetSpeaker Player>>
    yeah?
    <<SetSpeaker Original>>
    that headband you're wearing
    <<SetSpeaker Player>>
    oh uh!
    right
    <<Hide>>
    <<Wait 1>>
    <<set $_WornHat to "">>
    here, i found this
    [[GiveRabbitHeadband]]
<<elseif $RabbitQuest is true and HasItem("Headband", 1) is true and $BunnyQuestDone is false>>
    any luck finding\nmy headband yet?
    <<SetSpeaker Player>>
    well
    i found this...
    <<Hide>>
    <<Wait 1>>
    [[GiveRabbitHeadband]]
<<elseif $BunnyQuestDone is true>>
    [[RabbitWithHeadband]]
<<elseif visited() is false>>
    uh..
    hmm!
    <<SetSpeaker Player>>
    hi!
    <<SetSpeaker Original>>
    HII!!!
    <<SetSpeaker Player>>
    uh, are you ok?
    <<SetSpeaker Original>>
    uhh!
    not really...
    i'm supposed to run\na marathon race today
    <<set $KnowsAboutRace to true>>
    but i've lost my lucky\nheadband!!
    i've worn it to EVERY\nrace i've ever run!
    i...
    i don't know if i could\nrace without it
    <<SetSpeaker Player>>
    where'd you lose it?
    <<SetSpeaker Original>>
    somewhere in Blackwood Forest
    ...
    i think...
    <<SetSpeaker Player>>
    i could take a look for you
    <<SetSpeaker Original>>
    really!?
    you'd do that for me?
    <<SetSpeaker Player>>
    sure!
    <<SetSpeaker Original>>
    !!
    it's a <color=red>red headband</color>
    ...i can give you a reward!
    i could give you a\n<color=red>pair of running shoes</color>
    <<SetSpeaker Player>>
    don't you need those yourself?
    <<SetSpeaker Original>>
    well
    i just got sponsored\nby this shoe company
    they gave me like 10 pairs
    <<SetSpeaker Player>>
    woah!
    congrats 
    <<SetSpeaker Original>>
    oh!!
    haha... thanks
    but uhh
    let me know if you see\na <color=red>red headband!</color>
    <<set $RabbitQuest to true>>
<<else>>
    [[RaceRabbitExtra]]
<<endif>>
===
title: CellReceptionStart
tags: 
colorID: 0
position: 2354,2788
---
<<SetSpeaker Player>>
it doesn't seem like i\ncan get a signal anywhere
<<SetSpeaker Original>>
ah, yeah... it's pretty bad out here
i hear that you might be\nable to get some at <color=red>Hawk Peak</color>
<<SetSpeaker Player>>
yeah, i heard that too
it just seems kinda hard to get to
<<SetSpeaker Original>>
===
title: RaceTurtleStart
tags: 
colorID: 0
position: 6767,-851
---
<<if "Headband" is $_WornHat>>
    [[WearingTurtleHat]]
<<endif>>

<<if $BunnyQuestDone is true>>
    <<if $ToldTurtle is false>>
        <<SetSpeaker Player>>
        i gave it to her
        <<SetSpeaker Original>>
        so she's back in the race?
        <<SetSpeaker Player>>
        yes!
        <<if $BunnyGiftHeadband is false>>
            <<SetSpeaker Original>>
            awesome
            <<SetEmotion Happy>>
            i'm glad i could help!
            <<ClearEmotion>>
        <<else>>
            <<SetSpeaker Player>>
            she returned the\nheadband, though
            she seemed inspired
            i think she wants to\ntry and win luck-free
            <<SetSpeaker Original>>
            i don't think she needs it!
            but you know what
            why don't you keep the headband
            that way we'll be equal!
            <<SetSpeaker Player>>
            uh
            do really think it matters?
            <<SetSpeaker Original>>
            well...
            <<SetEmotion Happy>>
            probably not
            but
            just to be safe
            <<ClearEmotion>>
            <<set $ExplainedHeadband to true>>
        <<endif>>
        <<set $ToldTurtle to true>>
    <<else>>
        [[RaceTurtleAfter]]
    <<endif>>
<<elseif HasItem("Headband", 1) is true>>
    let me know how it goes!
<<elseif $RabbitQuest is true and $_BunnyGotHeadband is false>>
    <<SetSpeaker Player>>
    oh uh...
    weird question, but
    where'd you get that headband?
    <<SetSpeaker Original>>
    uh...
    i don't know
    i've had it for ages
    why?
    <<SetSpeaker Player>>
    ah well
    i was just talking to this rabbit
    she said she lost her headband\nsomewhere in the forest
    <<SetSpeaker Original>>
    oh! you mean Sue?
    <<SetSpeaker Player>>
    maybe?
    i didn't get her name
    she was really worried\nabout it though
    she said it was lucky and\nshe needs it to race
    <<SetSpeaker Original>>
    i'm supposed to race\nher later today!
    she's so talented, i've been\nreally looking forward to it!
    hmm
    what if i gave you mine?
    i'm pretty sure it's the same kind
    maybe she wouldn't notice?
    <<SetSpeaker Player>>
    oh, hmm...
    but
    then it wouldn't be lucky?
    <<SetSpeaker Original>>
    ah, i dunno
    just take it
    see if she wants it
    <<Hide>>
    <<set $_TurtleGivenHeadband to true>>
    <<GiveItem Headband 1>>
<<else>>
    <<if $TurtleTalk1 is false>>
        <<SetSpeaker Player>>
        what's up?
        <<SetSpeaker Original>>
        i'm just jogging
        warming up for a race later today
        i'm a little nervous
        there's gonna be a lot of\nskilled runners there!
        <<set $KnowsAboutRace to true>>
        <<set $TurtleTalk1 to true>>
    <<else>>
        gotta go speedy!
        just me and my <color=red>red headband!</color>
    <<endif>>
<<endif>>
===
title: ToMainIslandStart
tags: 
colorID: 0
position: 1829,-122
---
<sprite name="LeftArrow" tint=1> Back To Hawk Peak Island
===
title: Node78
tags: 
colorID: 0
position: 4387,2519
---
why doesn't the water level rise when the ice melts!?

theories:
aliens!
===
title: FrogProvincialElections
tags: 
colorID: 0
position: 5592,-403
---
<<if visited() is false>>
    <<SetSpeaker Player>>
    woah
    the city has really\nbeen growing huh?
    <<SetSpeaker Original>>
    oh this isn't a sand city any more
    this is a sand province!
    this time
    i'm running for premier\nin the provincial elections!
    sure, i wasn't elected as mayor
    but this time
    i'm really doubling\ndown on outreach
    and i'm promoting policies\ni really think will resonate\nwith my key districts
    once i'm elected
    i'll be able to use\nmy provincial influence
    to finally exert control over\nthe city that shunned me!
    AHAHAHAHAA!
    <<SetSpeaker Player>>
    ...
    woah
    <<SetSpeaker Original>>
    and the first legislation i'll pass
    will exist to consolidate\nmy allies in parliament
    the second legislation
    FREE CANDY FOR ALL!!
    AHAHAHAH!!
    <<set $ListenedToFrogSpeech to true>>
<<else>>
    <<SetSpeaker Player>>
    so...
    how's the campaigning going?
    <<SetSpeaker Original>>
    super good!!
<<endif>>
===
title: RabbitWithHeadband
tags: 
colorID: 0
position: 6543,-599
---
<<if visited() is false>>
    <<SetSpeaker Player>>
    so do you really think it's luck?
    <<SetSpeaker Original>>
    well
    i don't know
    i've gotten this far
    and
    it's just me
    like...
    ...it's got to be luck
    <<SetSpeaker Player>>
    well maybe a little
    but
    you're so fast!
    and you've trained so much!
    you got this far\ncuz you're GOOD!
    <<SetSpeaker Original>>
    aw
    ...
    ...yeah
    i know
    i think i've known all along
    but
    i don't know
    i get so nervous
    sometimes i wonder if\nthis is all a fluke
    ...
    but you're right!
    i don't need luck
    here, you can have\nthe headband back
    i'm gonna do this\nall on my own!
    <<Hide>>
    <<GiveItem Headband 1>>
    <<set $_BunnyGotHeadband to false>>
    <<set $BunnyGiftHeadband to true>>
    <<SetSpeaker Player>>
    woah
    thanks!
    good luck in the race
    you've got this!!
<<else>>
    sorry, i can't chat right now
    i've gotta prepare mentally and\nphysically for the race!
    see ya around!
<<endif>>
===
title: HawkPeakAltSignStart
tags: 
colorID: 0
position: 1127,-132
---
<sprite tint=1 name="UpArrow"> Hawk Peak Trail
<sprite tint=1 name="Warning"> NOTICE <sprite tint=1 name="Warning">
<color=yellow>Hawk Peak Trail</color> is a strenuous hike!
You might need <color=yellow>golden\nfeathers</color> to complete it.
<color=yellow>Golden feathers</color> are for sale\nat the <color=yellow>visitor center.</color>
===
title: RaceTurtleAfter
tags: 
colorID: 0
position: 7018,-599
---
speed's my name and\nracing is my game!
actually my name is Taylor
but yeah
the important thing\nto remember is that
i go fast
===
title: WearingTurtleHat
tags: 
colorID: 0
position: 6773,-601
---
<<if $BunnyGiftHeadband is true>>
    <<if $ExplainedHeadband is false>>
        so uh
        are you just gonna keep\nthe headband yourself?
        <<SetSpeaker Player>>
        no!
        um, well
        Sue actually gave it back
        she said she doesn't wanna\nrely on luck anymore
        <<SetSpeaker Original>>
        that's great!
        it's NOT luck
        she's very talented
        <<SetSpeaker Player>>
        so do you want the headband back?
        <<SetSpeaker Original>>
        eh
        you seem to like it
        you can have it
        <<set $ExplainedHeadband to true>>
    <<else>>
        [[RaceTurtleAfter]]
    <<endif>>
<<else>>
    <<if visited() is false>>
        hmm
        that headband looks familiar
        <<SetSpeaker Player>>
        oh um
        i'm just...
        seeing out how fits
        it looked comfy 
        <<SetSpeaker Original>>
        yeah it's pretty good
    <<else>>
        so...
        you're still giving my\nheadband a trial run?
        <<SetSpeaker Player>>
        uhh
        it's just
        my pockets are pretty full
        this is the easiest way to carry it
        <<SetSpeaker Original>>
        fair enough
    <<endif>>
<<endif>>
===
title: GiveRabbitHeadband
tags: 
colorID: 0
position: 6300,-592
---
<<SetSpeaker Original>>
hmm...
that's not it
the sweat stains aren't\nin the right place
<<SetSpeaker Player>>
ew
<<SetSpeaker Original>>
where did you find this?
<<SetSpeaker Player>>
actually, the turtle gave it to me
they said you could have it
they're looking forward to\nthe race and didn't\nwant you to miss it!
<<SetSpeaker Original>>
what?!
REALLY??
TAYLOR THE TURTLE!?
they're like, the regional champion!?
this headband has to be like
at least TWICE as lucky 
...possibly even...
QUADRUPLE lucky
<<SetSpeaker Player>>
!!!
<<SetSpeaker Original>>
ok
i'll take it
i'm so pumped!
take these running shoes
<<GiveItem Headband -1>>
<<set $_BunnyGotHeadband to true>>
<<set $BunnyQuestDone to true>>
<<set $_BunnyHappy to true>>
<<Hide>>
<<GiveItem RunningShoes 1>>
you can hold <color=red>{{RunButton}}</color> to run with them!
however
just keep in mind that running\nrequires <color=red>golden feathers</color>
===
title: Node86
tags: 
colorID: 0
position: 3637,2812
---
Empty Text
===
title: OutlookPointSignStart
tags: 
colorID: 0
position: 1373,-128
---
<sprite tint=1 name="UpArrow"> Outlook Point
===
title: Strawberries
tags: 
colorID: 0
position: 4196,-615
---
<<if visited() is true>>
    <<SetSpeaker Player>>
    still out of strawberries?
    <<SetSpeaker Original>>
    yup
<<else>>
    <<SetSpeaker Player>>
    so i heard about the strawberries
    ...and it's 5PM
    <<SetSpeaker Original>>
    aw crap
    we're all out
    <<SetSpeaker Player>>
    what!?
    but
    that's false advertising!
    <<SetSpeaker Original>>
    sorry
    we usually pick them locally
    the yield was weirdly low this year
    i mean
    we still have lots of toast
    you want some toast?
    <<SetSpeaker Player>>
    hook me up with some toast!
    <<SetSpeaker Original>>
    alrighty then...
    <<Hide>>
    <<GiveItem Toast 1>>
    <<SetSpeaker Original>>
    so, is there anything else you need?
<<endif>>
<<SetSpeaker Player>>
<<ShowCollection Coin>>
-> can I buy a <color=red>golden feather?</color>
    <<HideCollection Coin>>
    <<SetSpeaker Original>>
    [[BuyFeatherStart]]
-> what kind of merch do you sell?
    <<HideCollection Coin>>
    [[BuyMerchStart]]
-> i gotta jet
    <<HideCollection Coin>>
    <<SetSpeaker Original>>
    see ya
===
title: EatToastStart
tags: 
colorID: 0
position: 4209,-138
---
<<SetSpeaker Player>>
sure, i'll eat this toast
why not
<<Hide>>
<<GiveItem Toast -1>>
<<PlaySound EatToast>>
<<Wait 1.5>>
mmm
i feel....
the same
<<set $AteToast to true>>
<<GiveAchievement AteToast>> 
===
title: FoxNearTopStart
tags: 
colorID: 0
position: 4701,2694
---
FoxNearTopStart
===
title: TucanStart
tags: 
colorID: 0
position: 4582,-162
---
<<if visited() is false>>
    yo
    this updraft is ON POINT!
    you can get some sick dives on it!
<<else>>
    heyo!
    <sprite name="Music" tint=1> yeah i'm just chillin' here
    <sprite name="Music" tint=1> soakin' up those sweet sweet rays
    <sprite name="Music" tint=1> as long as i got my SPF fifty
    <sprite name="Music" tint=1> i could lie here for sweet sweet days
<<endif>>
<<SetSpeaker Player>>
-> how do you dive?
    <<SetSpeaker Original>>
    oh dang, it's easy
    but it takes some practice
    you gotta hear me out
    first
    you gotta be gliding...
    as you know
    you glide by <color=red>holding {{JumpButton}}</color>
    then, if you're high enough\noff the ground
    <<if IsUsingJoystick() is true>>
        <color=red>release your joystick</color>\nto dive downward
    <<else>>
        <color=red>release all arrow keys</color>\nto dive downward
    <<endif>>
    but you must still remember\nto <color=red>hold {{JumpButton}}</color>
    that's the tricky part!
    with a good dive, you can\n<color=red>build up a lot of speed</color>
    you can practice by gliding\nover this updraft
    that'll give you a lot of\nheight to work with!
-> sounds nice
    <<SetSpeaker Original>>
    dang, you KNOW it
    it's nice like some rice with spice
===
title: Cellphone2
tags: 
colorID: 0
position: 7343,-517
---
<<Wait 1>>
<<SetSpeaker Player>>
*huff*
phew
<<Hide>>
<<Wait 1>>
i...
<<Hide>>
<<Wait 0.6>>
...i did it!
<<Hide>>
<<SendMessage MountainTopCutscene ShowSitCamera>>
<<Wait 1.5>>
<<SetPose Sit>>
<<Wait 5>>
<<PlaySound Whippoorwill>>
<<Wait 10>>
<<PlaySound LoonCall>>
<<Wait 15>>
<<PlaySound PhoneRing>>
<<Wait 0.5>>
<<SetPose RaiseArms>>
!!!
CELLPHONE RECEPTION!!
[[Cellphone3]]
===
title: CellphoneStart
tags: 
colorID: 0
position: 7336,-763
---
<<set $_ClimbedHawkPeak to true>>
<<if $ReachedTopCutscene is false>>
    <<set $ReachedTopCutscene to true>>
    [[Cellphone2]]
<<endif>>
===
title: Node93
tags: 
colorID: 0
position: 4136,2543
---
all i want to do is climb up a hill to slide down it again
===
title: YellAtMayStart
tags: 
colorID: 0
position: 4825,-160
---
<<if visited() is false>>
    <<SetSpeaker Player>>
    i can see my house from here!
    ...
    ahem
    <<SetPose RaiseArms>>
    HEYYYYYYYYY!!!
    AUNT MAYY!!!!!
    <<SetSpeaker AuntMayNPC>>
    yes, claire?
    <<SetSpeaker Player>>
    YEAAAH!
    IT'S ME!!!!
    CAN YOU HEAR ME UP HERE!!??!?
    <<SetSpeaker AuntMayNPC>>
    sure can
    <<SetSpeaker Player>>
    OHHH
    OKAY
    <<SetSpeaker AuntMayNPC>>
    have fun up there!
    <<SetSpeaker Player>>
    YEAAAHH
    I WILL!!
    <<ClearPose Player>>
<<elseif visitCount() is 1>>
    <<SetSpeaker Player>>
    <<SetPose RaiseArms>>
    SO
    WHATCHA DOING!?
    <<ClearPose Player>>
    <<SetSpeaker AuntMayNPC>>
    oh, you know
    the usual
    <<SetSpeaker Player>>
    OOKAAY
    COOL
<<elseif visitCount() is 2 and $TalkedAboutDinner is false>>
    <<SetSpeaker Player>>
    <<SetPose RaiseArms>>
    WHAT'S FOR DINNER TONIGHT!?
    <<SetSpeaker AuntMayNPC>>
    uhh
    i think i'm gonna\nmake curry tonight
    <<set $CurryDinner to true>>
    <<SetSpeaker Player>>
    SOUNDS TASTYY!!
    <<ClearPose Player>>
<<else>>
    <<SetSpeaker Player>>
    <<SetPose RaiseArms>>
    HEY AUNT MAY
    <<SetSpeaker AuntMayNPC>>
    yeah?
    <<SetSpeaker Player>>
    I DUNNOOO!
    I GUESS I'M BORED!!!
    <<SetSpeaker AuntMayNPC>>
    i can tell!
    <<ClearPose Player>>
<<endif>>
===
title: TheStory
tags: 
colorID: 0
position: 5092,2774
---
Claire is visiting her aunt for a few weeks in the summer.
She is a university student who has moved away from home. She's in-between her first and second year.
Aunt May is a ranger at Hawk Peak Provincial Park.
May drove to pick up Claire from the bus stop, and took the ferry to the park.
During the drive with May, Claire learns from May that her mom has a surgery coming up.
She calls her Mom during this car ride and has a short argument with her.
It's too far for her to travel home to Mom at this point, and the bus doesn't come for another few days anyway.
It's for a surgery that will cut into her lungs to get a tissue sample.
The sample will determine if the issue is malignant or benign.
Claire is upset that her mother didn't tell her it was coming up, and is worried the surgery will go poorly, and that it could be a serious issue.
Claire worries about things a lot, and might be struggling slightly with anxiety.
The game starts on the morning that the surgery is taking place.
She is waiting for a call or message from her mother, about the surgery results (there is no internet her Aunt's cabin).
Upon learning about the lack of reception, she sets out to go to Hawk Peak.
Hawk Peak is the highest point in the province. It's a common hiking / flying spot for birds.
She's nervous about hiking up there, because it's sort of a family tradition to ride the updrafts.
Claire can fly fine, but she never gets a lot of practice in the city and is nervous about it.
The game will focus on Claire hiking to Hawk Peak.
She will meet cute or funny characters dealing with their own problems.
She might talk about her argument with her Mom, or about her worries about the surgery, or her worries about not living up to family expectations.
Nearing the top, she talks to a stranger about the issues and feels worried about them again.
Upon reaching the top, she gets a call from her mom.
She learns that the surgery went fine.
She wants to learn to cope with stress better, and her mom will try and stay in touch better.
The end!
===
title: ExplainDayToMom
tags: 
colorID: 0
position: 6979,372
---
[[Cellphone4]]
===
title: Cellphone3
tags: 
colorID: 0
position: 7369,-253
---
<<Hide>>
<<SendMessage MountainTopCutscene TakeOutCellphone>>
<<SendMessage MountainTopCutscene ShowCellCamera>>
<<PlaySound PhonePickUp>>
<<SetPose OnPhone>>
<<Wait 0.5>>
hello?
<<SetSpeaker CutsceneCellphone>>
hi, claire
<<SetSpeaker Player>>
<<PlayMusic MomMusicSet>>
mom!
i'm so glad to hear from you
i've been waiting all day to hear from you
i've been worried
<<SetSpeaker CutsceneCellphone>>
aw honey
i told you i'd be fine!
<<SetSpeaker Player>>
yeah
but you always say that
<<SetSpeaker CutsceneCellphone>>
and i always am!
<<SetSpeaker Player>>
...
<<SetSpeaker CutsceneCellphone>>
anyway
how have you been?
<<SetSpeaker Player>>
well
i'm at the top of Hawk Peak right now!
<<SetSpeaker CutsceneCellphone>>
ooh!
wow
i love it up there
May and i used to go up\nevery summer together
it's about time you made it up!
<<SetSpeaker Player>>
yeah... i know
<<SetSpeaker CutsceneCellphone>>
you're so big now
my little girl's leaving the nest
you won't have any need\nfor little ol' mom
<<SetSpeaker Player>>
but i DO need you
i'll never not need you!
when i heard about the surgery...
...how come you never told\nme this was a thing!?
<<SetSpeaker CutsceneCellphone>>
i didn't want you to worry
<<SetSpeaker Player>>
but
<<SetSpeaker CutsceneCellphone>>
i know you can get worked up about things
<<SetSpeaker Player>>
i could have been there for you
you're all alone there, now
<<SetSpeaker CutsceneCellphone>>
really, it was nothing!
everything was fine
<<SetSpeaker Player>>
<<Hide>>
<<Wait 1>>
i'm just glad you're ok
i love you,
mom
<<SetSpeaker CutsceneCellphone>>
i love you too
<<Hide>>
<<Wait 1>>
[[ExplainDayToMom]]
===
title: Cellphone4
tags: 
colorID: 0
position: 6978,620
---
<<Hide>>
<<Wait 1>>
<<SendMessage MountainTopCutscene StartRumble>>
<<Wait 1>>
<<SetSpeaker Player>>
wait
something's happening?!
<<Hide>>
<<Wait 1>>
<<SendMessage MountainTopCutscene ShowLakeCamera>>
<<Wait 1>>
<<SendMessage MountainTopCutscene StartBubbles>>
<<Wait 2>>
<<SendMessage MountainTopCutscene ActivateHotSpring>>
<<SendMessage MountainTopCutscene FinishRumble>>
<<Wait 2>>
<<SendMessage MountainTopCutscene HideLakeCamera>>
<<Wait 1>>
<<SetSpeaker CutsceneCellphone>>
was that the famous Hawk Peak updraft!?
<<SetSpeaker Player>>
uh
looks like it
<<SetSpeaker CutsceneCellphone>>
you should catch a ride on it now!
before it fizzles out
<<SetSpeaker Player>>
i wanna
but i'm kinda nervous
<<SetSpeaker CutsceneCellphone>>
claire
you can do it!
i'll come visit you soon, ok?
also
don't forget to say hi to <color=red>Aunt May</color> for me!
she's probably waiting for\nyou back at the cottage
bye, love you!
<<SetSpeaker Player>>
love you too, mom
<<SendMessage MountainTopCutscene PutAwayCellphone>>
<<SendMessage MountainTopCutscene HideCellCamera>>
<<ClearPose>>
===
title: AuntFinished
tags: 
colorID: 0
position: 4445,-2399
---
<<if $HadFinalAuntDialogue is false>>
    <<SetSpeaker Original>>
    hey
    [[AuntFinishedBody]]
<<else>>
    [[AuntFinalDialogue]]
<<endif>>
===
title: EndGameStart
tags: 
colorID: 0
position: 4943,-2122
---
i think it'd be nice to take a break
-> sleep
    alrighty
    it's nap time!
    <<set $_CanEndGame to true>>
-> not yet
===
title: Node101
tags: 
colorID: 0
position: 7212,379
---
so what has it been like there?
<<SetSpeaker Player>>
well
===
title: Node102
tags: 
colorID: 0
position: 7215,618
---
i need to talk about how to dive, if u havent been given the tutorial yet
===
title: TitleScreenIntroStart
tags: 
colorID: 0
position: 2728,-960
---
<<Wait 1.2>>
<<SetSpeaker AuntSpeaker>>
i never get tired of this view
claire,
you're gonna have a great\ntime here this summer!
i think
it'll be good to get\naway from the city
and take a break from\neverything else going on
<<Hide>>
<<Wait 1.5>>
are you still awake?
<<SetSpeaker Original>>
mmmmm
maybe
<<SetSpeaker AuntSpeaker>>
hold on a little longer!
we're almost at the ferry
<<Hide>>
<<Wait 1>>
===
title: ConsiderKidQuest
tags: 
colorID: 0
position: 3588,-303
---
<<SetSpeaker Player>>
-> i'll take your "side gig"
    <<SetSpeaker Original>>    
    good
    take this...
    <<set $AcceptedKidQuest2 to true>>
    <<Hide>>
    <<GiveItem ShellNecklace 1>>
    ...and deliver it to Ranger May
    that is all
-> nope
    <<SetSpeaker Original>>    
    suit yourself
===
title: AuntNecklace
tags: 
colorID: 0
position: 4162,-2374
---
<<set $DeliveredNecklace to true>>
<<SetSpeaker Player>>
i've got a delivery!
it's from Jen
<<GiveItem ShellNecklace -1>>
<<Hide>>
<<Wait 1>>
<<SetSpeaker Original>>
oh wow!
it's so pretty
she's such a sweet girl
collecting all those shells\nmust have been a lot of work
<<SetSpeaker Player>>
well actually...
<<SetSpeaker Original>>
hmm?
<<SetSpeaker Player>>
eh never mind
<<SetSpeaker Original>>
oh!
i remembered something
i found this golden feather in the cabin
i figured you could use it
<<Hide>>
<<GiveItem GoldenFeather 1>>
<<SetSpeaker Player>>
woah, thanks
this is
weird timing
is this like, a reward for\ndelivering the necklace?
<<SetSpeaker Original>>
uh, no?
<<SetSpeaker Player>>
huh
<<if $_ClimbedHawkPeak is true and $HadFinalAuntDialogue is false>>
    <<Hide>>
    <<Wait 1>>
    <<SetSpeaker Original>>
    oh, by the way
    [[AuntFinishedBody]]
<<endif>>
===
title: FrogTrade
tags: 
colorID: 0
position: 5083,-166
---
<<SetSpeaker Original>>
ohh that's perfect
let's trade!
<<GiveItem ToyShovel -1>>
<<Hide>>
<<Wait 1>>
<<set $_GotShovel to true>>
<<GiveItem Shovel 1>>
when you want to use my shovel...
<color=red>press {{MenuButton}}</color> to open your inventory\nand <color=red>equip it</color>
keep it fresh!
===
title: Node105
tags: 
colorID: 0
position: 3184,2505
---
<<SetSpeaker Player>>
hey
whatcha painting?
<<SetSpeaker Original>>
oh uhh
this coastline i think
i'm working on a series of paintings 
i'm supposed to exhibit them at a\nshow in the city next month
<<SetSpeaker Player>>
wow!
that's really cool
<<SetSpeaker Original>>
yeah, it should be
i pitched it as...
"Hawk Peak: Emotions in Color! A new perspective of famous island!
<<SetSpeaker Player>>
what's the new perspective?
<<SetSpeaker Original>>
it's uhhh
i'm trying to find it right now
<<SetSpeaker Player>>
oh
<<SetSpeaker Original>>
but i have some ideas
i'm gonna finish up this piece and then head over to meteor lake

noone is going to care about a bunch of landscape paintings at this
it seems like the exhibit is going to be mostly contemporary art

but it's beautiful!
you're capturing the EMOTIONS in COLOR!

well, i certainly hope so
i can't tell these days
does it sound dumb?
i really want people to like them


i've been working at a company for a while
doing graphic design
it's fine but
i really want to.. make art
i love painting
usually...
but i'm all stressed out
this is my first entry into the city's artist scene



but if i'm being completely honest




i can't compete with contemporary artist Ian Artworth

i think i'm going to paint this coastline

i'm not sure yet
i guess i'm trying to get inspired





<<SetSpeaker Player>>
===
title: PolarBearStart
tags: 
colorID: 0
position: 5861,935
---
<<if visited() is false>>
    hold on there,\nyoung friend
    i hope you don't mind me saying
    but you seem a\nlittle unprepared
    it's mighty cold up here for\nfolks who aren't accustomed
    <<SetSpeaker Player>>
    i didn't expect it to get this cold!
    my feathers are freezing up
    <<SetSpeaker Original>>
    Hawk Peak is unique in the\nprovince for geothermal activity
    if you get too chilly, stop\nat one of the hotsprings
    that should give you\nback your strength!
    <<if $ReadHawkPeakSign is false and GetItemCount("GoldenFeather") < 7>>
        <<SetSpeaker Player>>
        thanks for the tip!
        uh
        am i almost at the top?
        <<SetSpeaker Original>>
        well, yes...
        but it's a tricky climb
        you might need more <color=red>golden\nfeathers</color> than you have now
    <<endif>>
    <<set $_TalkedToPolarBear to true>> 
<<else>>
    <<if $PunMade is false and $_ClimbedHawkPeak is true>>
        hey there
        did you get up safely?
        <<SetSpeaker Player>>
        yup!
        well
        <<SetEmotion Happy>>
        you could say i...
        ...BEAR-ly made it!
        haha
        <<SetSpeaker Original>>
        ...
        <<SetSpeaker Player>>
        <<ClearEmotion>>
        do you get it?
        <<SetSpeaker Original>>
        oh
        i get it
        <<set $PunMade to true>>
    <<elseif $_CampfireFriendsBegin is true>> 
        [[PolarBearSitStart]] 
    <<else>>
        be safe out there!
        i'm the one who has to\nradio your aunt if\nyou get into trouble!    
    <<endif>>
<<endif>>
===
title: FlowerSignStart
tags: 
colorID: 0
position: 4663,3012
---
The 

Hawk Peak Provincial Park is home to this rare species of flower.
Your a bucket of water on it to make it sprout!
Then, you'll get high :)
===
title: Runner2Start
tags: 
colorID: 0
position: 3266,435
---
<<if visited() is false>>
    running is my life
    i run marathons all around the world
<<else>>
    what if
    running wasn't my life
    but i was actually using running
    to run FROM life!?
    ...
    that's a troubling thought
<<endif>>
===
title: Runner3Start
tags: 
colorID: 0
position: 3487,443
---
<<if $AskedRunnerAboutRace is true>>
    SPEED FIRST!!
    TALK LATER!!
<<else>>
    <<if visited() is false>>
        <<if $KnowsAboutRace is true>>
            <<SetSpeaker Player>>
            hey!
            are you doing the race today?
            <<SetSpeaker Original>>
            yeah
            i'm kinda busy right now
            what do you want?
        <<else>>
            <<SetSpeaker Player>>
            what are you jogging for?
            <<SetSpeaker Original>>
            excuse me?
            i'm training for the race!
            what do you want?
        <<endif>>
    <<else>>
        you again?
        what do you want?
    <<endif>>
    <<set $KnowsAboutRace to true>>
    <<SetSpeaker Player>>
    -> can i join the race?
        <<SetSpeaker Original>>
        <<set $AskedRunnerAboutRace to true>>
        *sigh*
        registration ended\nlike, last month
        why does no one\nread the rules!?
        you're <color=red>the second person</color>\ni've had to turn away today!
        anyway
        i gotta go
    -> nothing
        <<SetSpeaker Original>>
        welp
        i gotta go
<<endif>>
===
title: Runner4Start
tags: 
colorID: 0
position: 3706,452
---
<<if $NephewHi is true>>
    <<if $NephewHiDone is false>>
        ugh...
        i hate leg days
        <<SetSpeaker Player>>
        oh uh
        your uncle wanted me to remind you
        to take breaks
        <<if $Hydration is true>>
            and to stay hydrated
        <<endif>>
        <<SetSpeaker Original>>
        that's good advice
        just one more lap first...
        <<set $NephewHiDone to true>>
    <<else>>
        <<SetSpeaker Player>>
            good luck!
        <<SetSpeaker Original>>
            thanks
    <<endif>>
<<else>>
    ugh...
    i hate leg days
<<endif>>
===
title: BucketSignStart
tags: 
colorID: 0
position: 1571,101
---
The Helianthus Dissultantis "Rubber Flower"
Hawk Peak Provincial Park's Official Flower
This unique species <color=yellow>blooms instantly</color>\nin flash flooding conditions, inflating\nlike a water balloon!
Experience it for yourself with one of\nthe park's communal <color=yellow>water buckets!</color>
<color=#D7876F>*All buckets are property of the park.</color>
<color=#D7876F>Please do not remove buckets\nfrom the park!!</color>
<<SetSpeaker Player>>
<<if visited() is false>>
    hmm
    a bucket full of water seems heavy...
    i don't think i could fly\nor climb carrying a bucket\nfull of water...
    but i could probably still glide!
<<else>>
    i don't think i could fly\nor climb carrying a bucket\nfull of water
    but i could probably still glide!
<<endif>>
===
title: ToughBirdExit
tags: 
colorID: 0
position: 5166,-1284
---
<<HideCollection Coin>>
<<SetSpeaker Original>>
whatever, suit yourself
===
title: ToughBirdWhatDeal
tags: 
colorID: 0
position: 5078,-1012
---
<<HideCollection Coin>>
<<SetSpeaker Original>>
what's MY deal?
HMPH!
what's YOUR deal!?
<<SetSpeaker Player>>
well...
i'm hiking up to hawk peak
<<SetSpeaker Original>>
wow
sounds kinda basic
<<SetSpeaker Player>>
hey!
<<SetSpeaker Original>>
i'm just saying
tourists come from all over
they're like
"i'm goonnaa hike to hawwk peaak"
then they get up there
and see that it actually\ntakes real effort
and then they give up
<<SetSpeaker Player>>
geez
you don't have to be
...like that
<<SetSpeaker Original>>
i'm just saying
it's a little basic
<<SetSpeaker Player>>
so
what IS your deal?
i gave you mine
<<SetSpeaker Original>>
i need money
<<SetSpeaker Player>>
...
<<SetSpeaker Original>>
*sigh*
i'm gonna be short on\ntuition next semester
i'm nearly there though
just trying to make some extra cash
<<SetSpeaker Player>>
huh
how much do you need
<<SetSpeaker Original>>
...
well after i sell\nall my junk
i'll still be short by about 400 coins...
<<SetSpeaker Player>>
oh
<<SetSpeaker Original>>
yeah...
but whatever
that's life i guess
<<set $ToughBirdsProblem to true>>
===
title: ToughBirdTuition
tags: 
colorID: 0
position: 4843,-1007
---
<<HideCollection Coin>>
<<SetSpeaker Original>>
wait what?
really?
<<SetSpeaker Player>>
yeah sure
<<SetSpeaker Original>>
but why?
<<SetSpeaker Player>>
i dunno
i found most of this just by\nlooking around the island
i'm pretty good at finding stuff
<<SetSpeaker Original>>
wow
uh
i don't know what to say
<<SetSpeaker Player>>
it's 400 coins, right?
here ya go!
<<PlaySound CoinGet>>
<<GiveItem Coin -400>>
<<Hide>>
<<Wait 1>>
<<SetSpeaker Original>>
...
thanks
um
you can have this
<<Hide>>
<<GiveItem Watch 1>>
<<set $ToughBirdFriend to true>>
i uh
found it
i was gonna sell it on\nthe internet or whatever
but i don't need it anymore
<<SetSpeaker Player>>
huh
okay
===
title: ToughBirdExitNeutral
tags: 
colorID: 0
position: 5156,-1495
---
<<HideCollection Coin>>
<<SetSpeaker Original>>
later
===
title: ToughBirdBuyFeatherNice
tags: 
colorID: 0
position: 4979,-1716
---
<<HideCollection Coin>>
<<if $ToughBirdSales < 4>>
    <<SetSpeaker Original>>
    <<if visited() is false>>
        oh yeah
        no problem
        you can have it for the\nregular price of 40 coins
    <<else>>
        sure!
        you can have it for 40 coins
    <<endif>>
    <<if HasItem("Coin", 40) is true>>
        <<set $ToughBirdSales to $ToughBirdSales + 1>>
        <<GiveItem Coin -40>>
        <<PlaySound BuySound>>
        <<Hide>>
        <<Wait 1>>
        <<GiveItem GoldenFeather 1>>
        see ya later
    <<else>>
        <<SetSpeaker Player>>
        oh wait
        i'm short on cash
    <<endif>>
<<else>>
    <<SetSpeaker Original>>
    actually...
    you bought all the\nfeathers i had left
    sorry
    <<SetSpeaker Player>>
    that's OK
<<endif>>
===
title: GoatDone
tags: 
colorID: 0
position: 5737,-1397
---
<<if visited() is false>>
    <<SetSpeaker Player>>
    the watch is really nice
    <<SetSpeaker Original>>
    <<SetEmotion Happy>>
    why thank you
    <<ClearEmotion>>
    it was my grandfather's
    it's been all around the world
<<else>>
    i've actually lost this watch before
    luckily
    it's always seemed to find its way home
<<endif>>



===
title: GoatGive
tags: 
colorID: 0
position: 5743,-1170
---
<<SetSpeaker Player>>
hey, i think i found your watch
<<Hide>>
<<GiveItem Watch -1>>
<<Wait 1>>
<<SetSpeaker Original>>
yes!
that's it!!
thank you! thank you!!
<<SetSpeaker Player>>
you're welcome!
<<SetSpeaker Original>>
i ought to return the favour
please
take this
<<Hide>>
<<GiveItem SilverFeather>>
silver feathers are very rare
with one in hand,
you can climb faster
and fly higher!
<<set $GoatWatchFound to true>>
===
title: Node119
tags: 
colorID: 0
position: 4942,2533
---
ILL NEVER BE A GOOD CLIMBER LIKE DEBRA
===
title: RhinoStart
tags: 
colorID: 0
position: 5619,-57
---
<<if $JoinedClimbClub is true>>
    [[RhinoInClub]]
<<endif>>
<<if visited() is true>>
    hey again!
    [[RhinoAsk]]
<<endif>>
hey there!
what are you up to today?
<<SetSpeaker Player>>
uh
i'm hiking up to Hawk Peak
<<SetSpeaker Original>>
that's a tough hike!
you know
you'll need to learn to <color=red>rock climb</color>\nto make it to the top!
why not join our\nrock climbing club?
<<SetSpeaker Player>>
oh uh
is it just the two of you?
<<SendMessage SquirrelCutsceneCamera Enable>>
<<SetSpeaker ClimbSquirrel>>
<<Face ClimbSquirrel Player>>
HEY!
<<LookAt ClimbSquirrel Player>>
THE ROCK CLIMBERS ASSOCIATION\nHAS HUNDREDS OF MEMBERS SPANNING\nOVER 35 COUNTRIES WORLDWIDE!
HUNDREDS OF MEMBERS GROWING AND\nREFINING THE ART OF ROCK CLIMBING
PUSHING THE CRAFT TO ITS\nABSOLUTE LIMIT AND BEYOND
<<SetSpeaker Player>>
uh
i meant
if it's just the two of you...
like,
here?
<<SetSpeaker ClimbSquirrel>>
...
WHAT OF IT!?
LISTEN KID
CLIMBING AIN'T FOR THE WEAK OF HEART
MOST PEOPLE
DON'T HAVE WHAT IT TAKES
<<SetSpeaker Original>>
<<SendMessage SquirrelCutsceneCamera Disable>>
<<Face Original Player>>
<<ClearLookAt Player>>
um
it's not really that bad!
so anyway
[[RhinoAsk]]
===
title: RhinoAsk
tags: 
colorID: 0
position: 5868,-64
---
you want to join the climbers club?
<<SetSpeaker Player>>
-> sure
    <<SetSpeaker Original>>
    <<SetEmotion Happy>>
    great!!
    we've got our third member!
    <<SetSpeaker ClimbSquirrel>>
    NOT THIRD
    JUST ANOTHER DROP
    IN THE HUNDREDS
    WORLDWIDE
    <<SetSpeaker Original>>
    alright alright
    anyway
    <<ClearEmotion>>
    <<set $JoinedClimbClub to true>>
    [[RhinoInClub]]
-> nah
    <<SetSpeaker Original>>
    aww well
    let me know if you\nchange your mind
===
title: RhinoInClub
tags: 
colorID: 0
position: 5765,195
---
<<if $RhinoTut is true>>
    [[RhinoTut]]
<<endif>>
<<if HasItem("GoldenFeather", 1) is false and $RhinoSaidGetFeather is true and $TalkedToVisitorCenter is true and HasItem("Coin", 40) is false>>
    <<SetSpeaker Original>>
    so you got that feather?
    <<SetSpeaker Player>>
    not yet...
    they cost 40 coins
    and i only have {{Item_Coin}}
    <<SetSpeaker Original>>
    oh!
    hmm
    i can spot you the rest!
    <<Hide>>
    <<PlaySound CoinGet>>
    <<FillItem Coin 40>> 
    <<Wait 1>>
    <<SetSpeaker Player>>
    woah, thanks!
    <<SetSpeaker Original>>
    <<SetEmotion Happy>>
    no problem!
    <<ClearEmotion>>
<<else>>
    <<SetSpeaker Original>>
    <<if visited() is false>>
        so first things first
    <<endif>>
    you got a <color=red>golden feather?</color>
    <<if HasItem("GoldenFeather", 1) is true>>
        <<SetSpeaker Player>>
        yup!
        <<SetSpeaker Original>>
        great!
        [[RhinoTut]]
    <<else>>
        <<SetSpeaker Player>>
        oh, not yet
        <<SetSpeaker Original>>
        ah
        you can't climb without one
        but you can buy them at the <color=red>visitor center</color>
        they should be pretty cheap
        <<set $GoldenFeatherAdvice to true>>
        <<set $RhinoSaidGetFeather to true>>
        come back when you've bought one!
    <<endif>>
<<endif>>
===
title: RhinoTut
tags: 
colorID: 0
position: 6014,194
---
<<SetSpeaker Original>>
<<if visited() is false>>
    so, to get started
    you can try climbing this <color=red>short wall</color> here
    just <color=red>push against the wall</color> and <color=red>hold {{JumpButton}}
    <<set $RhinoTut to true>>
<<elseif $Climbed2 is true>>
    [[RhinoTalk]]
<<elseif $Climbed1 is true>>
    try climbing up the big one next!
    <<if HasItem("GoldenFeather", 3) is false>>
        you might need to jump\nfrom the other platform
    <<endif>>
<<else>>
    try climbing the <color=red>short wall</color> here!
    just <color=red>push against the wall</color> and <color=red>hold {{JumpButton}}
<<endif>>
===
title: ClimbWall1Start
tags: 
colorID: 0
position: 5777,423
---
<<if $RhinoTut is true and $Climbed1 is false and $Climbed2 is false>>
    <<Face Original Player>>
    nice one!
    <<SetSpeaker ClimbSquirrel>>
    WHATEVER
    <<SetSpeaker Original>>
    ...
    <<SetSpeaker ClimbSquirrel>>
    that climb is easy
    real pros use the big wall
    <<set $Climbed1 to true>>
<<endif>>
===
title: ClimbWall2Start
tags: 
colorID: 0
position: 5781,648
---
<<if $RhinoTut is true and $Climbed2 is false>>
    <<Face Original Player>>
    woah, good climbing!
    <<SetSpeaker ClimbSquirrel>>
    eh it was alright
    <<SetSpeaker Original>>
    so
    i know you can fly and all
    but your <color=red>stamina will last longer</color>\nif you climb places instead
    just something to keep in mind
    <<SetSpeaker Player>>
    cool
    so what's next?
    <<SetSpeaker ClimbSquirrel>>
    TO HAWK PEAK!
    <<SetSpeaker Player>>
    oh ok
    <<SetSpeaker Original>>
    i still need to warm up myself
    but i'll see ya up there?
    <<SetSpeaker Player>>
    alright!
    <<set $Climbed2 to true>>
    <<set $_ClimbersBefriended to true>>
<<endif>>
===
title: FlyWall1Start
tags: 
colorID: 0
position: 6008,421
---
<<if $RhinoTut is true and $BadFly1 is false and $Climbed1 is false and $Climbed2 is false>>
    <<Face Original Player>> 
    uhh
    i know you can fly and all
    but this is the climbing club
    <<SetSpeaker ClimbSquirrel>>
    yeah!
    the CLIMBING club!
    <<SetSpeaker Original>>
    ...
    anyway
    could you try climbing\nup there instead?
    <<set $BadFly1 to true>>
<<endif>>
===
title: FlyWall2Start
tags: 
colorID: 0
position: 6013,652
---
<<if $RhinoTut is true and $BadFly2 is false and $Climbed2 is false>>
    <<Face Original Player>> 
    flying up there is cool
    but
    this is a club about climbing...
    <<SetSpeaker ClimbSquirrel>>
    yeah!
    it kinda seems like\nyou're just showing off
    <<SetSpeaker Player>>
    no, i'm not!
    i just
    like to fly places i guess
    <<SetSpeaker Original>>
    give climbing a try!
    it's fun too
    <<set $BadFly2 to true>>
<<endif>>
===
title: RhinoTalk
tags: 
colorID: 0
position: 6274,191
---
<<if visited() is false>>
    uh
    sorry about tim
    it's not personal
    he's just cranky today
    <<SetSpeaker ClimbSquirrel>>
    I'M FINE!
    <<SetSpeaker Original>>
    we're really glad you joined!
    <<SetSpeaker Player>>
    uh
    thanks!
<<else>>
    <<SetSpeaker Player>>
    so why is he cranky?
    <<SetSpeaker Original>>
    uh
    actually
    he's always a little bit cranky
    <<SetSpeaker ClimbSquirrel>>
    I CAN HEAR YOU, YOU KNOW!?
<<endif>>
===
title: SquirrelStart
tags: 
colorID: 0
position: 6236,-73
---
<<if $JoinedClimbClub is false>>
    i'm busy
<<elseif $RhinoTut to false>>
    want know the secret to climbing?
    <<SetSpeaker Player>>
    sure
    <<SetSpeaker Original>>
    climb climb all the time
    <<set $HeardClimbingSecret to true>>
    <<SetSpeaker Player>>
    neat
<<elseif $Climbed1 is false and $Climbed2 is false>>
    you can try out the big wall
    once you master the short wall
<<elseif $Climbed2 is false>>
    if you're having trouble\nwith the big wall,
    just watch my technique
    it's highly refined from\nYEARS of climbing
<<else>>
    i guess i'll see\nyou at Hawk Peak
    <<SetSpeaker Player>>
    i guess so!
    <<if $HeardClimbingSecret is true and $SquirrelJoke1 is false>>
        <<SetSpeaker Original>>
        i'm not gonna lie
        it'll be a tough climb
        <<SetSpeaker Player>>
        well, you know what they say
        <<SetSpeaker Original>>
        what?
        <<SetSpeaker Player>>
        climb climb all the time
        <<SetSpeaker Original>>
        heh
        i guess so
        <<set $SquirrelJoke1 to true>>
    <<endif>>
<<endif>>
===
title: ToSecretIslandStart
tags: 
colorID: 0
position: 1835,102
---
<sprite name="LeftArrow" tint=1> Back To Hawk Peak Island
===
title: Node131
tags: 
colorID: 0
position: 5199,2518
---
sometimes i wake up and im like "i'm GREAT"
and other days i look into the mirror like "you're a mistake"
usually life is somewhere in between that tho

im dramatic 
===
title: RhinoClimbStart
tags: 
colorID: 0
position: 6241,645
---
<<if visited() is false>>
    oh hey!
    uh
    it seems like you've really\ngotten the hang of climbing!
    <<SetSpeaker Player>>
    yeah!
    <<SetSpeaker TimScared>>
    <<LookAt TimScared Player>>
    <<LookAt TimScared Original>>
    HEY UHH
    UMM
    I THINK
    I'M STUCK
    <<SetSpeaker Original>>
    tim!
    <<SetSpeaker TimScared>>
    IT'S TOO HIGH
    WHAT HAVE I DONE
    I CAN'T DO THIS
    <<SetSpeaker Original>>
    <<ClearLookAt Player>>
    TIM!
    take a deep breath
    try and remain calm
    you're doing great
    and you KNOW you can handle this
    <<SetSpeaker TimScared>>
    CAN I?
    WHAT AM I DOING!?
    <<SetSpeaker Original>>
    just focus on the next step!
    you've GOT this!
    <<SetSpeaker TimScared>>
    I'VE GOT THIS?
    I'VE GOT THIS
    <<ClearLookAt Original>>
    <<set $_DiscussedSquirrelStruggle to true>>
<<else>>
    uh
    this might take a while
    we'll catch up with you later
    i'll make sure he's OK!
<<endif>>
===
title: AuntFinishedBody
tags: 
colorID: 0
position: 4108,-2116
---
<<SetSpeaker Original>>
did you make it to the top?
<<SetSpeaker Player>>
yeah!
it was really nice
i can see why you like it so much
though
it wasn't exactly a "short hike"
i'm really tired
<<SetSpeaker Original>>
<<SetEmotion Happy>>
haha
well
maybe that was a bit\nof an understatement!
<<ClearEmotion>>
<<SetSpeaker Player>>
<<SetEmotion Happy>>
only a bit!!
<<ClearEmotion>>
<<SetSpeaker Original>>
[[AuntDynamic]]

===
title: Node132
tags: 
colorID: 6
position: 2956,2505
---
raccon is trying to paint a series for the exhibit in the city
they are painting some landscape paintings but trying to put a contemporary spin on them
trying to impress local artist scene to get that sweet sweet external validation we all need
maybe discovers that the landscape paintings done need to be more

encounter 1 
Hey, what are you painting?

Oh... you can't tell?
it's supposed to be the coastline

Ah, sorry!
Guess it's... abstract art?

Sigh
Yeah, I don't think I really like it... I think I'm going to head over to meteor lake and try again there.

encounter 2
Hey, sorry about earlier, your painting is looking really good so far!

Oh, hi again
Thanks, I guess
This park is special to me, but...
I don't think that critics will like my paintings
Landscapes are kind of passe these days

Really?
I like them! They're pretty!

Haha, thanks... 
But pretty isn't good enough
I have to show people that I'm achieving something worthwhile, academically

*hint for next location

encounter 3
Hey, this one's my favourite so far! 
You're so talented!

Thanks, I appreciate it!
Although, I don't think my work is that special...
But I have worked really hard to get better!

It shows!

I'm suppossed to be showing these at an event in the city
But all the other artists there are really prominent in contemporary galleries!
I think my work will look really out of place...

Whaaat? Nah.
You'll blow 'em out of the water!
This one reminds me of the group of seventeen!

...ah.

...is that bad?

Well, the group of seventeen have kinda got a bad rap in the contemporary scene.
People think they are really overrated.

Oh, huh.
That doesn't mean they'll think your paintings are bad, though!

They will if I don't submit a passable 5-page artist statement alongside my submission

*hint at final location

encounter 4
WOW!

Oh hey!
I think I got it this time.

IT'S AMAZING!!!

Thanks!
I think I just needed to paint what I wanted to paint

...Instead of what you felt like you were suppossed to paint?

Yeah.
I'm not even sure I'm going to go to that showing.

SCREW 'EM! I'd buy this!!

Haha, thanks for being so nice!

I'M NOT BEING NICE IT'S ACTUALLY REALLY GOOD

...Yeah, it is.
===
title: Node133
tags: 
colorID: 0
position: 2719,2508
---
encounter 1 
Hey, what are you painting?

Oh... you can't tell?
it's supposed to be the coastline

Ah, sorry!
Guess it's... abstract art?

Sigh
Yeah, I don't think I really like it... I think I'm going to head over to meteor lake and try again there.

encounter 2
Hey, sorry about earlier, your painting is looking really good so far!

Oh, hi again
Thanks, I guess
This park is special to me, but...
I don't think that critics will like my paintings
Landscapes are kind of passe these days

Really?
I like them! They're pretty!

Haha, thanks... 
But pretty isn't good enough
I have to show people that I'm achieving something worthwhile, academically

*hint for next location

encounter 2-2
So do you have a lot of critics?

Well, no... not yet!

encounter 3
Hey, this one's my favourite so far! 
You're so talented!

Thanks, I appreciate it!
Although, I don't think my work is that special...
But I have worked really hard to get better!

It shows!

I'm suppossed to be showing these at an event in the city
But all the other artists there are really prominent in contemporary galleries!
I like contemporary art, but it's not really my style
I think my work will look really out of place...

Whaaat? Nah.
You'll blow 'em out of the water!
This one reminds me of the group of seventeen!

...ah.

...is that bad?

Well, the group of seventeen have kinda got a bad rap in the contemporary scene.
People think they are really overrated.

Oh, huh.
That doesn't mean they'll think your paintings are bad, though!

They will if I don't submit a passable 5-page artist statement alongside my submission

*hint at final location

encounter 4
WOW!

Oh hey!
I think I got it this time.

IT'S AMAZING!!!

Thanks!
I think I just needed to paint what I wanted to paint

...Instead of what you felt like you were suppossed to paint?

Yeah.
I think I'm not as worried about the show anymore.
Haha, thanks for being so nice!

I'M NOT BEING NICE IT'S ACTUALLY REALLY GOOD

...Yeah, it is.
===
title: AuntFinalDialogue
tags: 
colorID: 0
position: 4700,-2122
---
<<if visitCount() is 0>>
    <<SetSpeaker Player>>
    oh
    i almost forgot
    mom says hi
    and...
    everything was ok
    <<SetSpeaker Original>>
    <<SetEmotion Happy>>
    that's great to hear
    your mom is a tough bird
    i knew she'd be alright
    <<ClearEmotion>>
<<else>>
    <<SetSpeaker Player>>
    i'm really glad i came
    it's been nice
    <<SetSpeaker Original>>
    <<SetEmotion Happy>>
    i'm glad you're here
    you can visit again anytime!
    <<ClearEmotion>>
<<endif>>
===
title: Node135
tags: 
colorID: 0
position: 2456,2510
---



---
is ranger jon gonna be in the painting?

yup!
he agreed to be in this one
cuz he's the best

- ranger jon
aw
you're gonna make me blush

it's ok
i can paint the not-blushing version
if you want

woah
this is looking really good!

i think i'm just gonna paint\nthe things i want to paint
instead of
painting the things i think\nother people want me to paint
at the very least
it's making things easier

- ranger jon
and
you want to paint me!?

uhh! well!
you're a part of the park too
i just want to capture all aspects the park

that sounds cool!

oh also
on my way down from the mountain, i found this
<<Hide>>
<<GiveItem GoldenFeather 1>>
jon told me you were collecting them
so i thought you should have it

---
ALT

i'm still not so sure\nwhat i'll put in the exhibit
but
i don't feel as worried about it

i think you should put it all in!

yeah?
even the watered-down rainy painting?

yeah, it's all part of the experience!
plus it looked pretty cool

hmm
yeah, that might work...

---
ALT2

i wanna spend some time on this one
really capture the moment, you know?
i'll talk to you later!
===
title: Artist1Start
tags: 
colorID: 0
position: 2632,740
---
<<if visitCount() is 0>>
    <<SetSpeaker Player>>
    hey!
    whatcha painting?
    <<SetSpeaker Original>>
    oh...
    you can't tell?
    um
    it's supposed to be the coastline
    though
    i'm trying to go for a more...
    ...abstract look
    <<SetSpeaker Player>>
    ah
    i think i can see it now
    <<SetSpeaker Original>>
    <<Face Player>>
    it's still a work-in-progress!
    but i don't know if i\nlike where it's going
    i think i'm gonna head over to the\nblackwood forest lighthouse soon
    i'm still...
    ...trying to find the\nright inspiration
    <<FaceDefault>>
    <<set $_Artist1Talk to true>>
<<elseif visitCount() is 1>>
    <<SetSpeaker Player>>
    so
    the beach is that big\nyellow smudge there?
    <<SetSpeaker Original>>
    i...
    think so?
    it's more about the\nmood of the beach
    than the beach itself
    <<SetSpeaker Player>>
    ooh
    ok
<<else>>
    i think i'm gonna put\na pin in this one
    hopefully i'll have better\nluck at the lighthouse...
<<endif>>
===
title: Artist2Start
tags: 
colorID: 0
position: 2864,737
---
<<if visited() is false>>
    hey, it's you again!
    <<SetSpeaker Player>>
    yup!
    classic me again!!
    <<SetSpeaker Original>>
    so
    what do you think\nabout this so far?
    <<SetSpeaker Player>>
    ...
    woahh
    this is REALLY good!
    it's like...
    better than real life!
    <<SetSpeaker Original>>
    haha well
    i don't think i can\ncompete with real life
    but i am happy with it so far
    ...
    i don't know if it will\nfit into my exhibit, though
    i'm trying to come up with\nsomething more interesting...
    i'm gonna follow one of the\nrivers up to its source
    i have an idea for a painting\nfrom that perspective
    <<set $_Artist2Talk to true>>
<<else>>
    <<SetSpeaker Player>>
    what river are you gonna follow?
    <<SetSpeaker Original>>
    probably the one on\nthe west side of the mountain
    it's higher up
<<endif>>
===
title: Artist3Start
tags: 
colorID: 0
position: 3085,725
---
<<if visited() is false>>
    hmm
    this painting isn't working
    <<SetSpeaker Player>>
    what?
    i like it!
    <<SetSpeaker Original>>
    thanks
    this park is special to me
    but
    i don't think i'm going to\nimpress anyone with these
    <<SetSpeaker Player>>
    hey!
    I'M impressed!
    <<SetSpeaker Original>>
    i mean, like
    leave an impression on anyone
    ...
    i have to show people that i'm...
    achieving something worthwhile
    like,
    academically
    <<SetSpeaker Player>>
    hmm
    <<SetSpeaker Original>>
    i heard
    there's a graveyard in\nthe shadow of mountain
    i'm gonna try that next
    <<set $_Artist3Talk to true>>
<<else>>
    <<SetSpeaker Player>>
    why the graveyard?
    <<SetSpeaker Original>>
    i'm not sure
    i just wanna try\nsomething different
<<endif>>
===
title: Artist4Start
tags: 
colorID: 0
position: 2636,970
---
<<if visited() is false>>
    <<SetSpeaker Player>>
    are you...
    painting in the rain?
    <<SetSpeaker Original>>
    yup
    <<SetSpeaker Player>>
    is that possible?
    <<SetSpeaker Original>>
    i'm trying!
    i don't have a lot of time
    the exhibition is next week!
    <<SetSpeaker Player>>
    oh
    okay
    <<SetSpeaker Original>>
    nothing has been\nworking so far...
    i finally got invited to\nshow my work in the city
    but everyone there is doing\namazing contemporary work
    i don't know if i can keep up
    <<SetSpeaker Player>>
    but
    your stuff is beautiful
    <<SetSpeaker Original>>
    you know who is curating the event?
    ian artsworth!
    famous contemporary artist,\nian artsworth!!
    he PERSONALLY invited\nme to participate
    and
    and
    i'm gonna show up with\nmy boring landscapes
    and he's gonna take\none look at 'em
    and whisper
    "this fails to resonate with me"
    and probably karate\nchop them in half!!
    <<SetSpeaker Player>>
    what!?
    he won't do that!
    <<SetSpeaker Original>>
    he COULD
    he's ALSO a blackbelt
    so it's definitely possible
    *sigh*
    but you're right
    trying to paint here is useless
    maybe i'd have better\nluck at outlook point
    <<set $_Artist4Talk to true>>
<<else>>
    <<SetSpeaker Player>>
    why don't you take a break?
    you've already done so much today
    <<SetSpeaker Original>>
    i can't
    i need to show people\nthat i'm worth something
    i mean
    my art is worth something
<<endif>>
===
title: Artist5Start
tags: 
colorID: 0
position: 2859,959
---
<<if visited() is false>>
    <<SetSpeaker Player>>
    WOAH!
    this is really good!!
    i love it!!!
    <<SetSpeaker Original>>
    oh hey
    i like it too but
    i can't use this...
    outlook point has been\npainted to death
    <<SetSpeaker Player>>
    has it?
    you've got such a cool style
    <<SetSpeaker Original>>
    thanks but
    it's just a landscape painting
    this isn't bringing anything\nnew to the table
    i want...
    i want people to FEEL something\nwhen they look at my work
    <<SetSpeaker Player>>
    but
    i feel something!
    when i look at it
    it...
    makes me happy
    if i had that on my wall
    i'd look at\nit everyday
    and
    it would make\neveryday better
    <<SetSpeaker Original>>
    really?
    <<SetSpeaker Player>>
    yes!
    i live out in the city
    i love the city but...
    it's got none of this
    <<SetSpeaker Original>>
    hmm
    <<SetSpeaker Player>>
    you were invited to\nthis show right?
    they invited you cuz they\nknow your work is good!
    i'm sure they wanna\nsee this too!
    <<SetSpeaker Original>>
    i don't know
    i need to think\nabout this
    i'm gonna find something\nnew to paint
    maybe
    i should head back\nto where i started...
    <<set $_Artist5Talk to true>>
<<else>>
    <<SetSpeaker Player>>
    are you ok?
    <<SetSpeaker Original>>
    yeah
    i'm good
    i'll see you around
<<endif>>
===
title: Artist6Start
tags: 
colorID: 0
position: 3082,972
---
<<if visited() is false>>
    <<SetSpeaker Player>>
    is ranger jon gonna\nbe in this painting?
    <<SetSpeaker Original>>
    yup!
    he agreed to be in this one
    cuz he's the best
    <<LookAt CampRangerNPC>>
    <<LookAt CampRangerNPC Player>>
    <<SetSpeaker CampRangerNPC>>
    aw
    <<SetEmotion Happy>>
    you're gonna make me blush
    <<ClearEmotion>>
    <<SetSpeaker Original>>
    it's ok!
    i can paint the\nnot-blushing version
    if you want
    <<ClearLookAt>>
    <<ClearLookAt Player>>
    <<SetSpeaker Player>>
    woah
    this is looking really good!
    <<SetSpeaker Original>>
    i think i'm just gonna paint\nthe things i want to paint
    instead of
    painting the things i think\nother people want me to paint
    at the very least
    it's making things easier
    <<LookAt CampRangerNPC>>
    <<SetSpeaker CampRangerNPC>>
    <<ClearEmotion>>
    and
    you want to paint me!?
    <<SetSpeaker Original>>
    uhh! well!
    <<SetEmotion Happy>>
    you're a part of the park too
    i just want to capture\nall aspects the park
    <<ClearEmotion>>
    <<ClearLookAt>>
    <<SetSpeaker Player>>
    that sounds cool!
    <<SetSpeaker Original>>
    oh also
    on my way down from the\nmountain, i found this
    <<Hide>>
    <<GiveItem GoldenFeather 1>>
    jon told me you\nwere collecting them
    so i thought you\nshould have it
    <<set $_Artist6Talk to true>>
<<elseif visitCount() is 1>>
    i'm still not so sure\nwhat i'll put in the exhibit
    but
    i don't feel as\nworried about it
    <<SetSpeaker Player>>
    i think you should\nput it all in!
    <<SetSpeaker Original>>
    yeah?
    even the watered-down\nrainy painting?
    <<SetSpeaker Player>>
    yeah!
    it's all part of the experience!
    plus it looked pretty cool
    <<SetSpeaker Original>>
    hmm
    you know
    that's not a bad idea!
<<else>>
    i wanna spend some\ntime on this one
    really capture the\nmoment, you know?
    i'll talk to you later!
<<endif>>
===
title: GoatExtra
tags: 
colorID: 0
position: 5458,-1072
---
<<if visited() is false>>
    oh also what is the\ntime by the way
    <<SetSpeaker Player>>
    it's {{Time}}
<<elseif visitCount() is 1>>
    <<SetSpeaker Player>>
    so
    do you think someone else\nmight have found it already?
    <<SetSpeaker Original>>
    well, no
    my phone number is\non the back
    so if someone found\nit, they'd call me!
    unless...
    they really needed\nmoney or something!?!
    ...AND SOLD IT ON THE INTERNET!?
    <<SetSpeaker Player>>
    oh uh
    don't worry!
    i'm sure it's still out there
    <<SetSpeaker Original>>
    i just don't get the internet
    don't trust it
    never will
<<else>>
    please find my watch...
    BEFORE INTERNET THIEVES\nFIND IT FIRST!!
<<endif>>
===
title: RaceRabbitExtra
tags: 
colorID: 0
position: 6539,-835
---
<<if visited() is false>>
    <<SetSpeaker Player>>
    do you think someone\nmight have picked it up?
    <<SetSpeaker Original>>
    i don't think so..?
    unless
    someone thought it...
    ...it was trash...
<<else>>
    any luck finding\nmy headband yet?
    <<SetSpeaker Player>>
    sorry
    no luck yet
    <<SetSpeaker Original>>
    ok... thanks
<<endif>>
===
title: RelaxedFrogStart
tags: 
colorID: 0
position: 5078,96
---
<<if visited() is false>>
    i'm so stoked\nto eat breakfast!
    <<SetSpeaker Player>>
    but
    it's already the afternoon?
    <<SetSpeaker Original>>
    tomorrow's breakfast!
    it's the best meal\nof the day.
    sometimes when i'm\nabout to go to bed...
    ...i get so excited by the thought\nof it that i have trouble sleeping!
<<elseif visitCount() is 1>>
    <<SetSpeaker Player>>
    so what makes breakfast so great?
    <<SetSpeaker Original>>
    dang
    you ever had a pancake before?
    <<SetSpeaker Player>>
    yeah
    <<SetSpeaker Original>>
    bacon? waffles?
    eggs? french toast?
    A FRUIT BOWL!?!
    <<SetSpeaker Player>>
    <<SetEmotion Happy>>
    yes to all!
    <<ClearEmotion>>
    <<SetSpeaker Original>>
    so you know what i'm talking about!
<<else>>
    don't mind me
    just daydreaming...
    ...about cinnamon waffles
<<endif>>
===
title: Node146
tags: 
colorID: 0
position: 4522,1838
---
hey, could you do me a favor?
===
title: ToughBirdWhatsUp
tags: 
colorID: 0
position: 4605,-1008
---
<<HideCollection Coin>>
<<SetSpeaker Original>>
<<if visited() is false>>
    still trying to get my act\ntogether for next semester
    <<SetSpeaker Player>>
    you still need to find 400 coins?
    <<SetSpeaker Original>>
    yeah...
<<else>>
    the balance on my student loan
    <<SetSpeaker Player>>
    oh
    <<SetSpeaker Original>>
    but yeah
    other than that
    pretty much nothing
    <<SetSpeaker Player>>    
<<endif>>


===
title: VolleyKidStart
tags: 
colorID: 0
position: 1329,655
---
<<if visited() is false>>
    <<SetSpeaker Player>>
    hey!
    looks like you got\nthe net set up!
    are you two gonna\nplay volleyball?
    <<SetSpeaker Original>>
    volleyball?
    nah
    we invented our OWN game
    i think it's gonna be\nthe next big thing!
    <<SetSpeaker Player>>
    alright!
    <<SetSpeaker Original>>
    <<SetPose RaiseArms>>
    CLAIRE!
    <<ClearPose>>
    <<SetSpeaker Player>>
    ??
    <<SetSpeaker Original>>
    i FORMALLY challenge YOU\nto a game of...
    <<SetPose RaiseArms>>
    BEACHSTICKBALL!!!
    <<ClearPose Original>>
    <<SetSpeaker RefereeKid>>
    <<LookAt Player>>
    <<LookAt RefereeKid Player>>
    ooooOOOOoooo
    <<SetSpeaker Original>>
    <<ClearLookAt Player>>
    that means you HAVE to play
    <<SetSpeaker RefereeKid>>
    <<LookAt RefereeKid Player>>
    yeah!!
    you HAVE to!!
    it's the LAW!!
    <<ClearLookAt Player>>
    <<SetSpeaker Original>>
    you can't fight the law!
    <<SetSpeaker Player>>
    ok ok
    i'll play
    <<SetSpeaker RefereeKid>>
    <<SetEmotion Happy>>
    <<SetEmotion Happy Original>>
    <<SetPose RaiseArms Original>>
    yaay!!
    <<ClearEmotion>>
    <<ClearEmotion Original>>
    <<SetSpeaker Player>>
    so...
    how do you play?
    <<SetSpeaker Original>>
    <<ClearPose Original>>
    step over to the\nother side of court
    and i'll teach you\neverything i know
    <<ClearLookAt RefereeKid>>
    <<set $_VolleyGameActive to true>>
<<elseif $_BallHitsBest is 0>>
    you gotta go to the\nother side to play!
<<else>>
    [[VolleyKidChat]]
<<endif>>
===
title: RefereeKidStart
tags: 
colorID: 0
position: 1076,1137
---
<<if visited() is false>>
    <<SetSpeaker Player>>
    hey julie!
    <<SetSpeaker Original>>
    hi!!
    <<SetSpeaker Player>>
    how's the day going?
    <<SetSpeaker Original>>
    we're gonna play games!!
    ALL DAY!!
    <<SetSpeaker Player>>
    sounds fun!
<<elseif visitCount() == 1>>
    <<SetSpeaker Player>>
    how do you like the big chair?
    <<SetSpeaker Original>>
    it's great!!
<<elseif visitCount() == 2>>
    <<SetSpeaker Player>>
    hmm
    where'd you get that\nchair from anyway?
    <<SetSpeaker Original>>
    we stole it!!
    <<SetSpeaker Player>>
    oh
<<elseif visitCount() == 3>>
    <<SetSpeaker Player>>
    are you gonna return\nit when you're done?
    <<SetSpeaker Original>>
    nope!!
<<else>>
    <<SetSpeaker Player>>
    you two should probably\nreturn that chair
    <<SetSpeaker Original>>
    hmmm!!
    maybe!!
<<endif>>
===
title: VolleyballGameStart
tags: 
colorID: 0
position: 1323,910
---
<<Face Player Original>>
<<Face Original Player>>
<<if visited() is false>>
    <<LookAt Player RefereeKid>>
    <<LookAt Original Player>>
    so
    beachstickball has only\nthree simple rules
    RULE NUMBER ONE!
    hit the ball with your stick
    <<SetSpeaker RefereeKid>>
    <<LookAt RefereeKid Player>>
    yeah hit it real hard!!
    <<SetSpeaker Original>>
    <<LookAt Original Player>>
    RULE NUMBER TWO!
    don't let it hit the ground
    i mean, for the most part...
    julie is the ref\nso it's all up to her!
    <<SetSpeaker RefereeKid>>
    <<LookAt RefereeKid Player>>
    it's all up to me!!
    <<SetSpeaker Original>>
    <<LookAt Original Player>>
    RULE NUMBER THREE!
    believe in yourself
    that's the most important rule
    <<SetSpeaker RefereeKid>>
    <<LookAt RefereeKid Player>>
    don't EVER forget it!!
    <<SetSpeaker Original>>
    <<LookAt Original Player>>
    that's pretty much it
    are you ready?
    <<SetSpeaker Player>>
    i guess so?
    <<SetSpeaker Original>>
    <<if IsEquipped("Stick") is false>>
        the answer is NO!
        you need to equip your stick first!
        <<set $_AllowGameStart to false>>
    <<else>>
        OK READY SET GO!!!
        <<set $_AllowGameStart to true>>
    <<endif>>
    <<ClearLookAt Player>>
    <<ClearLookAt RefereeKid>>
<<else>>
    [[VolleyballGameMain]]
<<endif>>
===
title: VolleyballGameMain
tags: 
colorID: 0
position: 1325,1150
---
<<if $_AllowGameStart is false and IsEquipped("Stick") is false>>
    you need to equip the STICK!!
<<else>>
    <<if IsEquipped("Pickaxe") and $PoppedBall is true>>
        ...
        i'm not gonna play if you're\ngonna use the pickaxe again...
        <<set $_AllowGameStart to false>>
    <<else>>
        <<set $PlayedBeachStickBall to true>>
        <<if $VolleyStartTalk is 0>>
            OK READY SET GO!!!
        <<elseif $VolleyStartTalk is 1>>
            OK HERE WE GO!!!
        <<elseif $VolleyStartTalk is 2>>
            OK LET'S DO THIS!!!
        <<elseif $VolleyStartTalk is 3>>
            OK OK OK GOOO!!!
        <<else>>
            TIME TO DUEL!!!
        <<endif>>

        <<set $VolleyStartTalk to $VolleyStartTalk + 1>>
        <<if $VolleyStartTalk >= 5>>
            <<set $VolleyStartTalk to 0>>
        <<endif>>
        <<set $_AllowGameStart to true>>
    <<endif>>
<<endif>>
===
title: VolleyballGameEnd
tags: 
colorID: 0
position: 1760,718
---
<<set $_BallHits to $_BallHits * 2>>
<<if $FirstBallCounts is true>>
    <<set $_BallHits to $_BallHits + 1>>
<<endif>>


<<if $_BallHits >= $_BallHitsBest>>
    <<set $_BallHitsBest to $_BallHits>>
    <<set $NewRecord to true>>
<<else>>
    <<set $NewRecord to false>>
<<endif>>


<<Wait 1>>
<<Face Player Original>>
<<Face Original Player>>
<<if visited() is false>>
    [[FirstVolleyLost]]
<<else>>
    [[RegularVolleyLost]]
<<endif>>
===
title: FirstVolleyLost
tags: 
colorID: 0
position: 1578,978
---
<<SetSpeaker Player>>
oops
did i lose?
<<SetSpeaker RefereeKid>>
<<LookAt RefereeKid Player>>
NOPE!!
you can never lose!!
<<ClearLookAt Player>>
<<SetSpeaker Original>>
<<SetEmotion Happy>>
when you play beachstickball...
<<SetPose RaiseArms>>
EVERYONE wins!
<<ClearEmotion>>
...
<<ClearPose>>
except if you pop the ball
then i guess you lose
otherwise
<<SetSpeaker Original>>
beachstickball is a\npurely coop game!
<<SetSpeaker Player>>
you mean co-op game?
<<SetSpeaker Original>>
yeah, a coop game!
it means we work together\nto get a high score!
<<SetSpeaker Player>>
that sounds nice
<<SetSpeaker Original>>
we get a point for\nevery pass we make
if we can get to 10 points\ni'll give you a prize!
<<if $_BallHits >= 10>>
    <<SetSpeaker RefereeKid>>
    you already got 10 points!!
    your score is {{BallHits}}
    <<SetSpeaker Original>>
    <<if $_BallHits < 20>>
        oh!
        well
        i guess i owe you a prize!
        [[GiveVolleyPrize]]
    <<else>>
        <<if $_BallHits >= 30>>
            we ALREADY hit 30 POINTS?!?!
        <<else>>
            we ALREADY hit 20 points?!
        <<endif>>
        that's more than\nwe've ever done before!!
        claire...
        you're a PRODIGY!
        take this golden feather
        <<Hide>>
        <<GiveItem GoldenFeather 1>>
        <<set $GotVolleyPrize to true>>
        and also
        [[GiveVolleyPrize2]]
    <<endif>>
<<endif>>
===
title: GiveVolleyPrize
tags: 
colorID: 0
position: 1573,1254
---
<<set $GotVolleyPrize to true>>
take this
<<Hide>>
<<GiveItem GoldenFeather 1>>
you're really good!
<<if $_BallHits >= 20>> 
    hmm
    i also wanted to give you a\nsecond prize for making it to 20!
    ...
    [[GiveVolleyPrize2]] 
<<else>> 
    with your skill, we might even\nbe able to make it to 20!
    but that would still\nbe pretty hard...
<<endif>>
===
title: PoppedBallEnd
tags: 
colorID: 0
position: 2004,719
---
<<if visited() is false>>
    <<Wait 1>>
    NOOOOOOOOOOOOOOOO
    ...how?
    ...why??
    <<SetSpeaker Player>>
    uh!
    sorry
    maybe that was a bad idea
    <<SetSpeaker Original>>
    just don't do it again, ok?
    <<set $PoppedBall to true>>
<<elseif visitCount() is 1>>
    <<Wait 1>>
    HEY!
    i said not to do\nthat anymore!
    <<SetSpeaker RefereeKid>>
    do it again!!
    <<SetSpeaker Original>>
    <<LookAt RefereeKid>>
    julie!
    <<SetSpeaker RefereeKid>>
    i like it when it pops!!
    <<SetSpeaker Original>>
    <<ClearLookAt>>
    but i'm trying to play the game!
<<else>>
    <<SetSpeaker RefereeKid>>
    HAHAHA!!
    so fun!!
    <<SetSpeaker Original>>
    ugh
<<endif>>
===
title: GiveVolleyPrize2
tags: 
colorID: 0
position: 1855,1250
---
<<set $GotVolleyPrize2 to true>>
i'm gonna give ya ALL the\nmoney in my pocket!
<<Hide>>
<<PlaySound BuySound>>
<<GiveItem Coin 50>>
<<Wait 1>>
<<SetSpeaker Player>>
<<Face Original Player>>
woah, that's a lot
i don't wanna take\nall your money
<<SetSpeaker Original>>
it's ok!
my family's rich
<<SetSpeaker Player>>
oh
well
ok i guess
<<if $_BallHits >= 30>>
    <<SetSpeaker Original>>
    honestly
    i never even thought\n30 was possible...
    i wanna give you\none more thing
    [[GiveVolleyPrize3_2]]
<<endif>>
===
title: RaceOpponentStart
tags: 
colorID: 0
position: 1262,-2217
---
<<set $_BeginRace to false>>
<<set $_ResetRace to false>>
<<set $JustMetAvery to false>>
    
<<if $_FinishedRace is false>>
    [[BeforeRaceIntro]]
<<else>>
    [[AfterRaceIntro]]
<<endif>>
===
title: RaceSelectMenu
tags: 
colorID: 0
position: 2418,-2403
---
<<SetSpeaker Original>>
totally!
what race you wanna do?
<<SetSpeaker Player>>
<<if $LighthouseRaceFound is true>>
    [[the lighthouse race|LighthouseRaceRetry]]
<<endif>>
<<if $BuildingRaceFound is true>>
    [[the abandoned building race|BuildingRaceRetry]]
<<endif>>
<<if $MountainTopRaceFound is true>>
    [[the mountaintop race|MountainTopRaceRetry]]
<<endif>>
[[nevermind|NothingRace]]
===
title: BeforeRaceMenu
tags: 
colorID: 0
position: 2073,-2750
---
<<SetSpeaker Player>>
[[let's race!|BeginRace]]
[[not yet|RefuseRace]]
<<if $RacesFound > 1>>
    [[let's do a rematch!|RaceSelectMenu]]
<<endif>>
===
title: DiscussRaceResult
tags: 
colorID: 0
position: 1906,-1357
---
<<if $_WonRace is true>>
    <<if $_RaceVictories < $_HardcodedReplays>>
        [[WinRegular]]
    <<elseif $_RaceVictories == $_HardcodedReplays>>
        [[WinMedal]] 
    <<else>>
        [[WinGhost]]
    <<endif>>
<<else>>
    <<if $_RaceVictories < $_HardcodedReplays>>
        [[LoseRegular]]
    <<else>>
        [[LoseGhost]]
    <<endif>>
<<endif>>
===
title: LighthouseRaceRetry
tags: 
colorID: 0
position: 2703,-2409
---
<<SetSpeaker Original>>
yes!
let's head over there
<<set $_RaceId to "LighthouseRace">>
<<set $_ResetRace to true>>
===
title: MountainTopRaceRetry
tags: 
colorID: 0
position: 2705,-2192
---
<<SetSpeaker Original>>
okey dokey! 
let's go to the\nstarting line!
<<set $_RaceId to "MountainTopRace">>
<<set $_ResetRace to true>>
===
title: BeforeRaceIntro
tags: 
colorID: 0
position: 1520,-2760
---
<<if $AgreedToAvery is false>>
    [[AveryIntroduction]]
<<endif>>

<<if $_RaceId is "LighthouseRace">>
    [[LighthouseRaceIntro]]
<<elseif $_RaceId is "MountainTopRace">>
    [[MountainTopRaceIntro]]
<<elseif $_RaceId is "OldBuildingRace">>
    [[BuildingRaceIntro]]
<<else>>
    [[BeforeRaceMenu]]
<<endif>>
===
title: AfterRaceIntro
tags: 
colorID: 0
position: 1244,-1697
---
<<if $_RaceResultDiscussed is false>>
    <<set $_RaceResultDiscussed to true>>
    [[DiscussRaceResult]]
<<else>>
    <<if $_IsSwimming is true>>
        helloooo
        just treading water here
        <<SetEmotion Happy>>
        it feels nice to take a dip!
        <<ClearEmotion>>
    <<else>>
        hey!
        what's up?
    <<endif>>
    [[AfterRaceMenu]]
<<endif>>
===
title: MountainTopRaceIntro
tags: 
colorID: 0
position: 1804,-2633
---
<<set $MountainTopRaceFound to true>>

<<if visited() is false>>
    <<set $RacesFound to $RacesFound + 1>>
    <<if $JustMetAvery is false>>
        hello again!
        UH
        i've got a bit of a crazy idea!
        but it's like...
        a good crazy idea
        will ya hear me out?
        <<SetSpeaker Player>>
        sure!
        <<SetSpeaker Original>>
        <<set $_CrazyIdea to true>> 
    <<endif>>
    ok so
    what if...
    we raced...
    ALL THE WAY TO THE SUMMIT!?
    <<SetSpeaker Player>>
    woah
    <<if $_CrazyIdea is true>> 
        that is a crazy idea
        <<SetSpeaker Original>>
        but a GOOD one!
        the rules are simple
    <<endif>> 
    <<SetSpeaker Original>>
    first one to the top WINS!
    i only have <color=red>9 feathers,</color> so
    don't use more than that, k?
    <<if HasItem("WalkieTalkie", 1) is false>>
        OH
        and one more thing
        take this!
        <<Hide>>
        <<GiveItem WalkieTalkie 1>>
        <<Face Original Player>>
        it's a long race so
        you can call me if you want to restart
        all good?
    <<endif>>
<<elseif $_LighthouseRace_Attempted is false>>
    so you wanna race to THE TOP?!
<<else>>
    hello again!
    you wanna rematch to the top?
<<endif>>

[[BeforeRaceMenu]]
===
title: LighthouseRaceIntro
tags: 
colorID: 0
position: 1804,-2882
---
<<set $LighthouseRaceFound to true>>

<<if visited() is false>>
    <<set $RacesFound to $RacesFound + 1>>
    <<if $JustMetAvery is false>>
        hello again!
        i've got another route\ni want to race!
        <<SetSpeaker Player>>
        sure!
        i'm interested
        <<SetSpeaker Original>>
    <<endif>>
    ok so
    here's the rules
    first one to touch the\nlighthouse door WINS!
    ALSO
    let's only use <color=red>3 golden\nfeathers</color> during the race!
    ...cuz that's all i\nhave on me right now
    sound good?
<<elseif $_LighthouseRace_Attempted is false>>
    so how about that lighthouse race?
<<else>>
    heyo!
    you wanna rematch to the lighthouse?
<<endif>>

[[BeforeRaceMenu]]
===
title: BeginRace
tags: 
colorID: 0
position: 2341,-2883
---
<<SetSpeaker Original>>

<<if HasItem("RunningShoes", 1) is false>>
    oh actually
    you might want to find\na pair of <color=red>running shoes</color>
    they're pretty crucial to\ngood running technique!
    <<SetSpeaker Player>>
    -> i'll race anyway!
        <<SetSpeaker Original>>
        haha ok
        lets do this!
    -> wait, nevermind
        <<SetSpeaker Original>>
        [[CancelRaceShoes]]
<<elseif $_RaceId is "LighthouseRace" and HasItem("GoldenFeather", 3) is false>>
    [[ConsiderFeathersRace]]
<<elseif $_RaceId is "OldBuildingRace" and HasItem("GoldenFeather", 4) is false>>
    [[ConsiderFeathersRace]]
<<elseif $_RaceId is "MountainTopRace" and HasItem("GoldenFeather", 7) is false>>
    [[ConsiderFeathersRace]]
<<else>>
    AWESOME
    let's do this!
<<endif>>

<<set $_BeginRace to true>>
===
title: RefuseRace
tags: 
colorID: 0
position: 2067,-3384
---
<<SetSpeaker Original>>
ok
see you later then?
===
title: AfterRaceMenu
tags: 
colorID: 0
position: 2109,-1958
---
<<SetSpeaker Player>>
[[bye!|NothingRace]]
[[want to do a rematch?|RestartRace]]
<<if $AskedAboutAvery is false and $KnowsAboutRace is true>>
    [[why not join the marathon?|AveryStory]]
<<endif>>
<<if $RacesFound > 1>>
    [[let's do a different race!|RaceSelectMenu]]
<<endif>>
===
title: RestartRace
tags: 
colorID: 0
position: 2407,-2053
---
<<SetSpeaker Original>>
<<if $_WonRace is false>> 
    i'm down!!
    let's try it again!
<<else>> 
    totally!!
    i'll do better this time!
<<endif>> 
<<set $_ResetRace to true>>
===
title: NothingRace
tags: 
colorID: 0
position: 2104,-2236
---
<<SetSpeaker Original>>
see ya!
===
title: RegularVolleyLost
tags: 
colorID: 0
position: 1841,976
---
<<if $_BallHits >= 4>>
    <<Wait 0.5>>
    <<SetSpeaker RefereeKid>>
    <<if $FirstCountResult is false>>
        i counted {{BallHits}} hits!!
    <<else>>
        i counted {{BallHits}} hits!!
        <<set $FirstCountResult to true>>
    <<endif>>
    <<SetSpeaker Original>>

    <<if $GotVolleyPrize is false and $_BallHits >= 10>>
        <<SetEmotion Happy>>
        YES!
        we did it!
        as promised, i've got\na prize for you!
        <<ClearEmotion>>
        [[GiveVolleyPrize]]
    <<elseif $GotVolleyPrize2 is false and $_BallHits >= 20>>
        <<SetEmotion Happy>>
        <<if $_BallHits >= 30>>
            HOLY MOLY WE HIT 30!!
        <<else>>
            WE HIT 20!!
        <<endif>>
        THAT'S MY NEW RECORD!!
        <<ClearEmotion>>
        uh uh
        i gotta come up with a reward!!
        [[GiveVolleyPrize2]]
    <<elseif $GotVolleyPrize3 is false and $_BallHits >= 30>>
        [[GiveVolleyPrize3]]
    <<else>>
        [[NoVolleyPrize]]
    <<endif>>
<<endif>>
===
title: NoVolleyPrize
tags: 
colorID: 0
position: 2331,1250
---
<<if visited() is false>>
    <<set $_BallHitsMore to $_BallHits + 1>>
    but i counted {{BallHitsMore}}!
    <<SetSpeaker RefereeKid>>
    the serve doesn't count!!
    <<SetSpeaker Original>>
    what?
    it totally counts!
    what do you think, claire?
    <<SetSpeaker Player>>
    -> it counts!
        <<SetSpeaker Original>>
        yeah see!
        <<SetSpeaker RefereeKid>>
        fine!!
        <<set $FirstBallCounts to true>>
        <<set $_BallHits to $_BallHits + 1>>
        you got {{BallHits}} points then
    -> it doesn't really count...
        <<SetSpeaker Original>>
        aw come on
        <<SetSpeaker Player>>
        sorry..
        i'm with julie on this one
        <<SetSpeaker RefereeKid>>
        yeah!!
        <<SetSpeaker Original>>
        fine fine
<<else>>
    <<if $_BallHits >= 20>>
        <<if $NewRecord is true>>
            that's a new record!!
        <<else>>
            <<SetEmotion Happy>>
            we make a pretty great team!
            <<ClearEmotion>>
        <<endif>>
    <<elseif $_BallHits >= 10>>
        <<if $BallBrag is false>>
            you're doing great!
            <<SetSpeaker Player>>
            well
            you're a lot better
            you never drop the ball!
            <<SetSpeaker Original>>
            trueee
            <<set $BallBrag to true>>
        <<else>>
            solid!
        <<endif>>
    <<elseif $_BallHits >= 8>>
        <<if $GotVolleyPrize is false>>
            oh, almost there!
        <<else>>
            oh, almost 10 again!
        <<endif>>
    <<elseif $_BallHits >= 6>>
        not bad!
    <<elseif $_BallHits >= 4>>
        that's a good start!
    <<endif>>
<<endif>>
===
title: GiveVolleyPrize3
tags: 
colorID: 0
position: 2098,1252
---
wait what?
<<SetSpeaker RefereeKid>>
i said you got {{BallHits}} points!
<<SetSpeaker Original>>
WE GOT PAST 30 POINTS!?
i didn't even know\nthat was possible!
hmm
that deserves the most specialest prize
but i already gave you all my money...
<<SetSpeaker Player>>
it's ok!
i don't need anything
<<SetSpeaker Original>>
yes you do!!
maybe you should have this
[[GiveVolleyPrize3_2]]
===
title: CampCatStart
tags: 
colorID: 0
position: 4136,483
---
<<if $ReturnedPermit>> 
    <<if $CampCatSorry is false>> 
        [[CampCatFreakout]] 
    <<else>> 
        <<SetEmotion Happy>> 
        it feels great to\nbe 100% legal again!
        <<ClearEmotion>> 
    <<endif>> 
<<elseif HasItem("CampingPermit", 1)>> 
    <<SetSpeaker Player>>
    you won't believe what i found!
    <<Hide>> 
    <<GiveItem CampingPermit -1>> 
    <<Wait 1>> 
    <<SetSpeaker Original>>
    <<SetPose RaiseArms>> 
    YOU FOUND MY PERMIT!?
    I THOUGHT IT WAS\nGONE FOREVER!
    <<ClearPose>>
    <<SetSpeaker Player>>
    you weren't kidding!
    a fish really did have it
    <<SetSpeaker Original>> 
    <<SetEmotion Happy>> 
    thank you so much!
    <<ClearEmotion>> 
    you should have the\nrest of my bait
    i'm feeling a little\ndone with fishing
    <<Hide>> 
    <<PlaySound PickUp>>
    <<GiveItem Bait 5>>
    <<Wait 1>> 
    thanks again!
    you're a LIFESAVER!
    <<set $ReturnedPermit to true>>
<<elseif visited() is false>>
    <<SetSpeaker Player>>
    hey
    <<SetSpeaker Original>>
    <<SetPose RaiseArms>> 
    AAHHH!
    <<Face Player>> 
    YES THIS SETUP\nIS 100% LEGAL!!!
    <<SetSpeaker Player>>
    uh-
    <<SetSpeaker Original>>
    YES I HAVE A VALID\nCAMPING PERMIT!!!
    <<SetSpeaker Player>>
    wait-
    <<SetSpeaker Original>>
    <<ClearPose>> 
    <<SetPose Shaking>> 
    FINE FINE! YOU GOT ME
    <<SetSpeaker Player>>
    hold on-
    <<SetSpeaker Original>>
    I LOST IT
    I'M A TERRIBLE LIAR
    I'M A MESS
    <<SetSpeaker Player>>
    uhhhhhhhhhh-
    <<SetSpeaker Original>>
    PLEASE DON'T WRITE ME UP
    I CAN BRIBE YOU
    <<Hide>>
    <<PlaySound PickUp>>
    <<GiveItem Bait 5>>
    <<Wait 1>> 
    <<SetSpeaker Player>>
    <<Face Original>>
    ...
    did you really just hand\nme a fistful of worms?
    <<SetSpeaker Original>>
    IT'S FISHING BAIT!!
    I'M DESPERATE!!
    WHAT WOULD YOU DO?!
    <<SetSpeaker Player>>
    well, uh
    it doesn't matter
    i'm not gonna tell anyone!
    <<SetSpeaker Original>>
    <<ClearPose>> 
    wait, really?
    aren't you a junior ranger?
    <<SetSpeaker Player>>
    nope
    my aunt's a ranger
    but i don't work for her
    <<SetSpeaker Original>>
    oh well
    you won't tell right?
    <<SetSpeaker Player>>
    nah
    <<SetSpeaker Original>>
    good!
    i guess
    you can keep the bait
    <<SetSpeaker Player>>
    ...thanks
<<elseif visitCount() is 1>>
    <<SetSpeaker Player>> 
    where'd you lose the permit?
    <<SetSpeaker Original>>
    well...
    you might not believe me
    <<SetSpeaker Player>> 
    try me
    <<SetSpeaker Original>>
    a fish ate it
    <<SetSpeaker Player>> 
    what?
    come on
    <<SetSpeaker Original>>
    really!
    <<SetSpeaker Player>>
    it does sound a little...
    fishy!
    heh
    <<SetSpeaker Original>>
    ...
    <<SetPose Shaking>> 
    this is serious!
    <<ClearPose>> 
    <<set $_MissingPermit to true>> 
<<else>> 
    [[CampCatFreakout]] 
<<endif>>
===
title: VolleyKidChat
tags: 
colorID: 0
position: 1076,897
---
<<if visited() is false>>
    so
    do you love it?
    <<SetSpeaker Player>>
    beachstickball?
    <<SetSpeaker Original>>
    yeah!!
    <<SetSpeaker Player>>
    -> i like it!
        thanks for showing me the game!
        <<SetSpeaker Original>>
        <<SetEmotion Happy>>
        thanks for playing!
        you're the best, claire!
        <<ClearEmotion>>
    -> i dunno
        i think i still prefer volleyball
        <<SetSpeaker Original>>
        WHAT
        but beachstickball is like\nvolleyball with super powers!
        the super power of WHACKING\nTHINGS WITH A STICK!!
<<else>>
    our best score so far is {{BallHitsBest}}!
    <<if $_BallHitsBest >= 20 and $BallChat1 is false>>
        <<SetSpeaker Player>>
        that's pretty good!
        <<SetSpeaker Original>>
        yeah!
        <<SetEmotion Happy>>
        we make a good team!
        <<ClearEmotion>>
        <<set $BallChat1 to true>>
    <<elseif $_BallHitsBest >= 30 and $BallChat2 is false>>
        <<SetSpeaker Original>>
        i think this is history in the making
        <<SetSpeaker Player>>
        i'd say so
        <<set $BallChat2 to true>>
    <<endif>>
<<endif>>
===
title: GiveVolleyPrize3_2
tags: 
colorID: 0
position: 1994,1506
---
<<Hide>>
<<set $_GotKidHat to true>>
<<set $GotVolleyPrize3 to true>>
<<GiveItem KidHat 1>>
<<Wait 1>>
<<SetSpeaker Player>>
<<Face Original Player>>
woah
<<SetSpeaker Original>>
please keep it!
so you can remember\nthis moment forever!!
<<SetSpeaker Player>>
i'll try
<<GiveAchievement BeachstickballChamp>> 
===
title: Node178
tags: 
colorID: 0
position: 4430,2164
---
ugh
i'm such an idiot
i forgot to sign up for the race!
nah, i can't join day of..
i feel crappy and stupid
===
title: Node179
tags: 
colorID: 0
position: 4692,2097
---
i'm racing in the marathon today
it's gonna be my first race!
i'm so pumped!
===
title: Node180
tags: 
colorID: 0
position: 4752,1838
---
this is boring
i'm bored and scared 
===
title: Node181
tags: 
colorID: 0
position: 4972,1838
---
hey
hi
...
uh i'll talk to you
===
title: WalkieTalkieStart
tags: 
colorID: 0
position: 3233,-2407
---
<<PlaySound RadioOn>>
<<SetPose OnPhone>>
<<Wait 0.5>>

<<if GetSceneName() == "GameScene">>
    <<if $_StartedRace is true and $_RaceResultDiscussed is false>>
        <<if IsVisible("RaceOpponent") is true and $_FinishedRace is true>>
            [[WalkieOnScreen]]
        <<else>>
            [[WalkieMidRace]]
        <<endif>>
    <<else>>
        <<if IsVisible("RaceOpponent") is true>>
            [[WalkieOnScreen]]
        <<else>>
            [[WalkieNotRacing]]
        <<endif>>
    <<endif>>
<<else>>
    [[WalkieNobody]]
<<endif>>
===
title: WalkieMidRace
tags: 
colorID: 0
position: 2975,-2152
---
<<SetStyle AveryRadioTextBoxProfile>>

<<if $_FinishedRace is false>>
    uhh
    i'm running and talking here
    but, what's up?
<<else>>
    hey!
    i'm waiting for you
    what's up?
<<endif>>

<<SetSpeaker Player>>
-> let's start over!
    <<SetStyle AveryRadioTextBoxProfile>>        
    oh, uh
    sure, we can start over
    <<set $_RaceResultDiscussed to true>>
    <<SendMessage RaceCoordinator CallRestartRace>>
-> i give up
    <<SetStyle AveryRadioTextBoxProfile>>
    aww, well
    ok i guess
    <<set $_RaceResultDiscussed to true>>
    <<SendMessage RaceCoordinator CallAbandonRace>>
-> nothing much
    <<if $_FinishedRace is false>>
        there's no time to chit chat
        a race is underway!
    <<else>>
        oh ok
        see you soon?
    <<endif>>

<<PlaySound RadioOff>>
<<ClearPose Player>>
===
title: WalkieNotRacing
tags: 
colorID: 0
position: 3215,-2148
---
hey, where are you?
<<SetStyle AveryRadioTextBoxProfile>>
<<if $_RaceResultDiscussed is true>>
    <<if $RaceAbandoned is true>>
        uh...
        just wandering around somewhere...
    <<elseif $_RaceId is "LighthouseRace">>
        i'm still at the lighthouse!
    <<elseif $_RaceId is "OldBuildingRace">>
        i'm still at that old building!
    <<elseif $_RaceId is "MountainTopRace">>
        i'm still at the top of the mountain!
    <<else>>
        um...
        i don't know!
    <<endif>>
<<elseif $_RaceId is "LighthouseRace">>
    i'm hanging out at the royal ridge!
<<elseif $_RaceId is "OldBuildingRace">>
    i'm around the graveyard!
<<elseif $_RaceId is "MountainTopRace">>
    i'm near the visitor center!
<<else>>
    uhh...
    i don't know!
<<endif>>

<<PlaySound RadioOff>>
<<ClearPose Player>>
===
title: WalkieNobody
tags: 
colorID: 0
position: 3443,-2148
---
hello?
is anyone there?
...

<<PlaySound RadioOff>>
<<ClearPose Player>>
===
title: WalkieOnScreen
tags: 
colorID: 0
position: 2971,-2404
---
<<SetSpeaker RaceOpponent>>
<<Face Player>>
<<SetPose RaiseArms>>
hey, i'm right here!
<<ClearPose>>

<<PlaySound RadioOff>>
<<ClearPose Player>>
===
title: FishingTutorialStart
tags: 
colorID: 0
position: 704,-1384
---
<<if $DidFishTutorial is true>>
    [[FisherTalk]]
<<endif>>

<<if visited() is true>>
    <<SetSpeaker Player>>
    hey
    <<SetSpeaker Original>>
    hello again
    <<SetSpeaker Player>>  
    -> can you teach me how to fish?
        <<SetSpeaker Original>>
        sure
        [[FishingTutorialBegin]]
    -> uh gotta go
        <<SetSpeaker Original>>
        alrighty
        you get going then
<<else>> 
    <<SetSpeaker Player>>
    how's it going?
    <<SetSpeaker Original>>
    good
    the fish are biting today
    <<SetSpeaker Player>>
    cool cool
    so
    uh
    is fishing hard?
    <<SetSpeaker Original>>
    nope
    it's easy
    i could show you,\nif you'd like
    <<SetSpeaker Player>>
    -> yeah!
        [[FishingTutorialBegin]]
    -> not now...
        ...i've got somewhere to be
        maybe later!
<<endif>> 
===
title: FishTutCastStart
tags: 
colorID: 0
position: 707,-1157
---
casting yer line is pretty easy
just press <color=red>{{UseItemButton}}</color>
===
title: FishTutChatStart
tags: 
colorID: 0
position: 699,-923
---
<<Wait 4>>
<<SetSpeaker Player>>
so
uh
what do i do now?
<<SetSpeaker Original>>
ya wait
<<SetSpeaker Player>>
oh
ok
<<Hide>>
<<Wait 4>>
<<SetSpeaker Player>>
uhh
am i doing this right?
<<SetSpeaker Original>>
yup
<<Hide>>
<<Wait 5>>
<<SetSpeaker Player>>
maybe
i should cast it again?
you know
find a better spot?
<<SetSpeaker Original>>
this spot is good
i saw some fish\naround here earlier
that's a good sign
sometimes
you just gotta wait
patience is key
<<SetSpeaker Player>>
hmm
not my strong suit
===
title: FishTutNibbleStart
tags: 
colorID: 0
position: 699,-694
---
look alive!
a fish is nibbling
<<SetSpeaker Player>>
should i pull it out?
<<SetSpeaker Original>>
not yet...
ya gotta wait\nfor it to bite

===
title: FishTutBiteStart
tags: 
colorID: 0
position: 688,-473
---
now!
press <color=red>{{UseItemButton}}</color> to yank it out!
===
title: FishTutPullStart
tags: 
colorID: 0
position: 690,-239
---
<<SetSpeaker Player>>
UHHH
WHAT DO I DO NOW!?
<<SetSpeaker Original>>
<color=red>pull back</color> and <color=red>tap {{UseItemButton}}</color> to reel it in!
===
title: FishTutDoneStart
tags: 
colorID: 0
position: 692,-9
---
see?
easy as pie
<<SetSpeaker Player>>
<<LookAt Original>> 
i dunno if i'd say that
i suck at baking
<<SetSpeaker Original>>
i guess i mean
as easy as...
...eating pie?
<<SetSpeaker Player>>
i can get behind that
<<ClearLookAt>>
===
title: FisherTalk
tags: 
colorID: 0
position: 466,-1378
---
<<if visited() is false>>
    <<SetSpeaker Player>>
    so what do i do with the fish?
    <<SetSpeaker Original>>
    whatever you want, kid
<<else>>
    <<SetSpeaker Player>>
    well
    thanks for showing me how to fish
    <<SetSpeaker Original>>
    no problem
    yer a natural
    <<SetSpeaker Player>>
    <<SetEmotion Happy>>
    i don't feel like one!
    <<ClearEmotion>> 
<<endif>>
===
title: Node388
tags: 
colorID: 0
position: 2711,2240
---
i don't know if i like fishing...
it gives me too much time to think

can you give me advice?
you seem wise

that's an odd thing to say
honeslty i have no idea what im doing
i dont talk much tho
so i think people assume i'm very thoughtful
truth is
there's not much going on in here
i don't mind tho
you'd think i'd get a little smarter each year
hard to say if it's actually happening

that's not very helpful

no i suppose not
but that's how things are
honestly it's hard to give advice
when i still don't know what works for me



just focus on right now


i don't really love just sitting around
if i'm not busy
my mind finds ways to be busy
sometimes it's hard to relax
===
title: RacerIntroStart
tags: 
colorID: 0
position: 3822,2301
---
<<if visited() is true>>
    <<if $AcceptedRaceChallenge is true>>
        i'll meet you at the Royal Ridge
    <<else>>
        so, wanna join MY race?
        -> sure
            [[JoinRaceIntro]]
        -> not yet
            alright
    <<endif>>
<<else>>
    <<if $KnowsAboutRace is true>>
        <<SetSpeaker Player>>
        hey
        is this where\nthe race starts?
        <<SetSpeaker Original>>
        yeah!
    <<else>>
        <<SetSpeaker Player>>
        hey
        what's going on here?
        <<SetSpeaker Original>>
        there's a big race here today!
    <<endif>>
    i was SUPPOSED to be racing in it
    but...
    ugh
    turns out you had\nto sign up ONLINE
    <<SetPose RaiseArms>>
    like, LAST MONTH!!
    <<ClearPose>>
    i'm like
    COME ON
    how is anyone supposed\nto know that?!
    <<SetSpeaker Player>>
    beats me!
    <<SetSpeaker Original>>
    *sigh*
    it's a burn alright
    but you know what
    i came here\nfor a good time
    and i'm NOT gonna\nlet this stop me!
    I'M GONNA RUN MY OWN RACE
    <<SetSpeaker Player>>
    woah!
    <<SetSpeaker Original>>
    i just need some contenders
    so...
    ARE YOU GAME?!?
    -> i'm game!
        [[JoinRaceIntro]]
    -> i'm not game
        that's OK!
        but if you change your mind
        you know where to find me!
<<endif>>
===
title: JoinRaceIntro
tags: 
colorID: 0
position: 4081,2296
---
AWW YEAH!!
ok ok
i'm gonna see who\nelse i can recruit
let's meet on Royal Ridge\non the west side of\nthe island
HOPEFULLY i'll have thought of\na finish line by the time\nwe get there!
<<set $AcceptedRaceChallenge to true>>
===
title: FishBuyerStart
tags: 
colorID: 0
position: 129,-2068
---
<<if visited() is true>>
    howdy!
    got any fish?
    <<if HasItem("Fish", 1) is true>>
        [[FishBuyerMenu]]
    <<else>>
        <<SetSpeaker Player>>
        not right now!
    <<endif>>
<<else>>
    <<SetSpeaker Original>>
    howdy
    <<SetSpeaker Player>>
    hi!
    whatcha doing?
    <<SetSpeaker Original>>
    takin' a break!
    heh
    don't tell anyone, alright?
    <<SetSpeaker Player>>
    i won't
    i don't even know\nwho i'd tell!
    <<SetSpeaker Original>>
    heh, good
    i can't wait to\nbe done today
    when my shift's over
    i'll be out catchin'\nme some rare fish!
    it's my hobby, ya see
    if you happen to net\nsome fish yourself...
    ...bring them to me and\ni'll trade ya coin!
    <<SetSpeaker Player>>
    <<if HasItem("Fish", 1) is true>>
        i caught some fish already!
        <<SetSpeaker Original>>
        oh?
        can i take a look at 'em?
        [[FishBuyerMenu]]
    <<else>>
        <<if HasItem("FishingRod", 1) is false>>
            i would if i had\na fishing rod!
            <<SetSpeaker Original>>
            ya know
            my friend bill might be\nable to lend you one
            he's out fishing all the time
        <<else>>
            sounds good to me!
        <<endif>>
    <<endif>>
<<endif>>
===
title: FishBuyerMenu
tags: 
colorID: 0
position: 124,-1826
---
<<SetSpeaker Player>>
-> take a look at this...
    <<SendMessage FishBuyer SellFish>>
-> not right now
    <<SetSpeaker Original>>
    alrighty
===
title: BuyFish
tags: 
colorID: 0
position: -163,-1765
---
<<if $_FishAlreadySold is true>>
    [[BuyFishAgain]]
<<endif>>

<<if $_FishRare is false>>
    [[BuyFishQuip]]
<<else>>
    [[BuyRareFishQuip]]
<<endif>>


===
title: BuyFishEnd
tags: 
colorID: 0
position: -156,-956
---
is that all?
<<SetSpeaker Player>>
<<if HasItem("Fish", 1) is true>>
    -> take a look at this...
        <<SendMessage FishBuyer SellFish>>
    -> that's all!
        <<SetSpeaker Original>>
        pleasure doin'\nbusiness with ya
<<else>>
    that's all for now!
<<endif>>
===
title: BuyFishAgain
tags: 
colorID: 0
position: -403,-1495
---
<<set $_BaitSold to true>>
<<if visited() is false>>
    sorry pal
    i've already got that fish
    but...
    i can trade you <color=red>bait</color> for fish\nyou've already given me!
    sure, you can catch\nfish without bait
    but <color=red>it's a lot faster</color> with it!
<<else>>
    i'll trade you bait for that
<<endif>>

<<Hide>>
<<GiveItem Bait 1>>
<<Face Original Player>>
<<Wait 1>>
[[BuyFishEnd]]
===
title: BuyFishCancel
tags: 
colorID: 0
position: 81,-959
---
'till next time!
===
title: BuyFishAction
tags: 
colorID: 0
position: -25,-1227
---
<<SendMessage FishBuyer PayCoins>>
<<PlaySound BuySound>>
<<Hide>>
<<Wait 1>>

<<if visitCount() is 2>>
    you're quite good at this!
    you should take my journal
    it'll help ya keep track\nof the fish you've found
    <<Hide>>
    <<GiveItem FishEncyclopedia 1>>
    <<Wait 1>>
    <<Face Original Player>>
    so
<<endif>>

<<if HasSoldAllFish() is true and $SoldAllFishReward is false>> 
    well now!
    if i'm not mistaken
    it seems like you've gotten me\nall the fish from my journal!
    claire!
    you've REALLY got\na knack for this!
    <<SetSpeaker Player>>
    thanks!
    <<SetSpeaker Original>> 
    hmm
    i've got something i think\nyou might find useful
    i think i can trust you with this
    <<Hide>>
    <<GiveItem FishingRod -1>> 
    <<GiveItem GoldenFishingRod 1>>
    <<Wait 1>>
    <<Face Original Player>>
    <<SetSpeaker Player>>
    wow!
    it's very pretty
    <<SetSpeaker Original>> 
    rare fish are drawn to it
    ya might find some\ninteresting fish with it!
    but don't lose it
    it's very precious to me
    it's been passed down for\ngenerations within my family
    <<SetSpeaker Player>>
    woah
    <<SetSpeaker Original>> 
    also
    it's also made of gold
    so
    yeah
    don't lose it!
    <<SetSpeaker Player>>
    i won't!
    <<set $SoldAllFishReward to true>> 
    <<SetSpeaker Original>>
    <<GiveAchievement AllFish>> 
<<endif>> 

[[BuyFishEnd]]
===
title: BuyFishQuip
tags: 
colorID: 0
position: -165,-1495
---
<<if $QuipLine is 0>>
    the {{FishName}}, eh?
    i'll give you {{FishPrice}} for it
<<elseif $QuipLine is 1>>
    would you look at that!
    the {{FishName}}!
    i can give you {{FishPrice}} for it
<<elseif $QuipLine is 2>>
    nice catch
    the {{FishName}} is a\npersonal favourite of mine
    i'll trade you {{FishPrice}} coins for it
<<elseif $QuipLine is 3>>
    good one!
    the {{FishName}} has\nevaded me for a while
    but no longer!
    i'll give you {{FishPrice}} for it
<<elseif $QuipLine is 4>>
    the {{FishName}}...
    not a fan of this one
    bit me once!
    but i'm still looking for it!
    i'll give you {{FishPrice}} coin
<<elseif $QuipLine is 5>>
    ah the {{FishName}}
    this one is popular here at hawk peak
    i'll give you...
    ...{{FishPrice}} coins for it
<<else>>
    the {{FishName}}
    hmm
    i think {{FishPrice}} coins\nseems like a fair price
<<endif>>

<<set $QuipLine to $QuipLine + 1>>
<<if $QuipLine > 6>>
    <<set $QuipLine to 0>>
<<endif>>

[[BuyFishAction]]
===
title: BuyRareFishQuip
tags: 
colorID: 0
position: 64,-1497
---
<<set $FoundAndSoldRareFish to true>>
<<if $RareQuip is 0>>
    now hold on...
    what is THIS?!
    the rare {{FishName}}?!?
    that's a mighty catch right there!
    i'll pay ya {{FishPrice}} coins for it!
<<elseif $RareQuip is 1>>
    ...now, what is this?
    i don't recognize this one
    ...
    is that the {{FishName}}?
    that's a rare find!
    i'll give you {{FishPrice}} to add\nit to my collection!
<<elseif $RareQuip is 2>>
    you've GOT to be kidding me
    you netted the {{FishName}}!?
    gosh dang it!
    you've really got\na knack for fishing!
    <<SetSpeaker Player>>
    i dunno about that!
    <<SetSpeaker Original>>
    welp
    i gotta give you at\nleast {{FishPrice}} coins for this!
<<elseif $RareQuip is 3>>
    WHAT THE HECKY?!
    claire
    you're a fishing legend!
    the {{FishName}}\nis one TRICKY catch!
    i'll be givin' you\n{{FishPrice}} coin for that!
<<elseif $RareQuip is 4>>
    <<if $CrazyFishStory is false>>
        well i'll be!
        the {{FishName}} huh?
        we go way back
        the last time i saw this guy\nwas almost FIVE whole years ago!
        ya see,
        i had it on the hook
        it was givin' a real strong tug
        and i mean
        like this fish-
        has got muscles like\nyou wouldn't believe!
        it nearly pulled me\noff the boat!
        <<SetSpeaker Player>>
        no way!
        <<SetSpeaker Original>>
        yeah way!
        anyway...
        i gotta give you {{FishPrice}}\ncoin for this one!
        <<set $CrazyFishStory to true>>
    <<else>>
        the {{FishName}}!?
        now that's a spectacular find!
        i'll give you {{FishPrice}} for it!
    <<endif>>
<<else>>
    WHA-WHAT!?
    i've heard LEGENDS of the\n{{FishName}} at hawk peak
    but here it is...
    ...in front of MY VERY OWN EYES!!
    claire
    i'll be givin' ya {{FishPrice}}\ncoins for this catch
<<endif>>

<<set $RareQuip to $RareQuip + 1>>
<<if $RareQuip > 5>>
    <<set $RareQuip to 0>>
<<endif>>

[[BuyFishAction]]
===
title: FishingTutorialBegin
tags: 
colorID: 0
position: 931,-1383
---
<<SetSpeaker Original>>
<<set $DidFishTutorial to true>>
here
i'll lend ya my spare rod
<<Hide>>
<<GiveItem FishingRod 1>>
<<SendMessage FishingTutorial StartTutorial>>
===
title: AveryBuildingSignStart
tags: 
colorID: 0
position: 800,-2212
---
<<if visited() is false>> 
    Dear Sign Reader:
    Do you like reading signs?
    I assume so, since this\nis happening right now.
    Well...
    ...I can introduce you to\nsomething WAY better than reading!
    Two words:
    PARKOUR
    RACE
    PARKOUR RACE!!
    I'll be back right after I set up the\nfinish line at the <color=yellow>abandoned building.</color>
    -Avery
    <<set $ReadAverySignBuilding to true>>
<<else>>
    <<SetSpeaker Player>> 
    hmm
    maybe i better figure out where\nthis <color=red>abandoned building</color> is
<<endif>> 
===
title: AveryLighthouseSignStart
tags: 
colorID: 0
position: 1030,-2214
---
<<if visited() is false>> 
    Dear Sign Reader:
    Hi!
    I don't know you.
    But you're probably pretty cool.
    I'm cool too!
    We should race each other.
    It would be cool.
    BRB!
    I'm gonna setup a ribbon\nat the <color=yellow>lighthouse.</color>
    -Avery
    <<set $ReadAverySignLighthouse to true>>
<<else>>
    <<SetSpeaker Player>> 
    hmm
    i wonder where that <color=red>lighthouse</color> is...
<<endif>> 
===
title: AveryIntroduction
tags: 
colorID: 0
position: 1422,-3276
---
<<if $AgreedToAvery is true>> 
    [[BeforeRaceIntro]]
<<endif>> 

<<if visited() is false>>
    <<set $_A to $ReadAverySignLighthouse is true and $_RaceId is "LighthouseRace">>
    <<set $_B to $ReadAverySignBuilding is true and $_RaceId is "OldBuildingRace">>
    <<if $_A or $_B>> 
        <<SetSpeaker Player>> 
        hi!
        are you avery?
        <<SetSpeaker Original>> 
        YES!
        that's me!!
        did you read my ad?
        <<SetSpeaker Player>> 
        the sign?
        yeah
        <<SetSpeaker Original>> 
        GREAT!
        sooo
        you wanna race?
        [[AveryIntroQuestion]]
    <<elseif $ReadAverySignBuilding or $ReadAverySignLighthouse>> 
        HI!!
        my name is AVERY!!
        it's a pleasure to meet you!!
        <<SetSpeaker Player>>
        hi
        i'm claire
        i think i saw your sign
        <<SetSpeaker Original>>
        you DID!?
        EXCELLENT!
        so have you come to race me?
        [[AveryIntroQuestion]]
    <<else>>
        HI!!
        my name is AVERY!!
        i'm on a quest to become a\nPARKOUR RACING MASTER!!
        <<SetSpeaker Player>>
        neat!
        <<SetSpeaker Original>>
        <<SetPose RaiseArms>> 
        you BET YOUR BOTTOM\nDOLLAR it's neat!!
        <<ClearPose>> 
        however
        i'm trying to find someone\nto practice with!
        so...
        ...any chance you wanna\ngo for a race?
        [[AveryIntroQuestion]]
    <<endif>>
<<else>>
    <<SetEmotion Happy>>
    hello again!
    you wanna race me?
    <<ClearEmotion>>
    [[AveryIntroQuestion]]
<<endif>>
===
title: Node211
tags: 
colorID: 0
position: 2194,2528
---
i was going to run 

HI!
my name is avery
do you wanna race?
race?
yeah



you know
like just run

like
i was going to run in the marathon
but it turns out you need to sign up online!
i was 


so
i was supposed to join the marathon race today
turns out i missed registration
i was like
super bummed out
like majorly no good feels all around
if you know what i mean

then i watched parkour videos on the internet for like 2 hours
i think i found my new calling!

HI!
my name is AVERY!
now
i don't wanna alarm you but
i'm about to challenge you to a race
but i don't wanna startle you so
i just wanan make sure you're 
===
title: AveryStory
tags: 
colorID: 0
position: 1823,-2049
---
<<SetSpeaker Player>>
there's this big race happening today!
<<SetSpeaker Original>>
WELL...
um
it's a little embarrassing, but
i was SUPPOSED to run in\nthe marathon today
but it turns out i\nmissed registration!!
<<SetSpeaker Player>>
ohh
sorry, that sucks
<<SetSpeaker Original>>
yup
i gotta say
i was pretty bummed out
kinda felt like giving up on racing
...
but then i spent like 2 hours\nwatching parkour videos
and now my new passion\nis PARKOUR RACING!!
so whatever
<<SetEmotion Happy>> 
i'm glad you were willing\nto help me practice!
<<ClearEmotion>> 
<<set $AskedAboutAvery to true>>
===
title: AveryIntroQuestion
tags: 
colorID: 0
position: 1669,-3273
---
<<SetSpeaker Player>>
-> sure!
    <<SetSpeaker Original>>
    <<set $AgreedToAvery to true>>
    <<SetEmotion Happy>>
    <<SetPose RaiseArms>>
    FAAANTASTIC!
    <<ClearPose>>
    <<ClearEmotion>>
    <<set $JustMetAvery to true>>
    [[BeforeRaceIntro]]
-> later
    <<SetSpeaker Original>>
    oh well
    alright then
    
===
title: BuildingRaceIntro
tags: 
colorID: 0
position: 1802,-2400
---
<<set $BuildingRaceFound to true>>

<<if visited() is false>>
    <<set $RacesFound to $RacesFound + 1>>
    <<if $JustMetAvery is false>>
        hiya!
        ok ok so
        i've set up another route to race!
        you want in?
        <<SetSpeaker Player>>
        sure!
        where we racing to?
        <<SetSpeaker Original>>
    <<endif>>
    so there's this abandoned building\nto the east of here
    at least
    i think it's abandoned?
    anyway
    i'm thinking we should race\nto the top of it!
    whoever touches the post\nup there first WINS!
    and also
    we should only use <color=red>5 golden\nfeathers</color> during this race!
    that'll make\nit more interesting!
    you ready?
<<elseif $_LighthouseRace_Attempted is false>>
    so how about that race?
<<else>>
    hello again!
    you wanna rematch to the abandoned building?
<<endif>>

[[BeforeRaceMenu]]
===
title: BuildingRaceRetry
tags: 
colorID: 0
position: 2700,-2646
---
<<SetSpeaker Original>>
alright!
LET'S GET TO IT!
<<set $_RaceId to "OldBuildingRace">>
<<set $_ResetRace to true>>
===
title: CancelRaceShoes
tags: 
colorID: 0
position: 2339,-3130
---
okay
i'll wait 'till you've found a pair!
===
title: ConsiderFeathersRace
tags: 
colorID: 0
position: 2588,-2890
---
hmm!
you might want to find more feathers\nbefore trying this race
it might be hard to keep\nup without them!
<<SetSpeaker Player>>
-> i'll race anyway!
    <<SetSpeaker Original>>
    alrighty
    lets do this!
    <<set $_BeginRace to true>>
-> wait, nevermind
    <<SetSpeaker Original>>
    i'll wait until you find more!
===
title: WinRegular
tags: 
colorID: 0
position: 1866,-1671
---
<<set $BeatOneRace to true>>
<<if visitCount() % 3 is 0>> 
    ok
    i have to admit
    you are GOOD!
    but
    i feel like i was\njust getting warmed up!
<<elseif visitCount() % 3 is 1>> 
    dang you got me again!
    you've got some slick moves
    but
    i messed up a bit
    i think i could have\ndone much better
<<else>>
    you beat me!!
    i thought i had that one...
    well
    if you ever want another go,
    i think i could still improve!
<<endif>>
[[AfterRaceMenu]]
===
title: WinGhost
tags: 
colorID: 0
position: 2095,-1669
---
<<if visitCount() % 3 is 0>> 
    claire you are a MACHINE
    you got here in just {{WonRaceTime}} seconds!
    i bet i can learn\na lot from you
    welp
    i'll always be around\nif you want a rematch!
    i bet if i borrowed some\nof your strategies...
    ...i could beat ya next time!
<<elseif visitCount() % 3 is 1>> 
    woah
    you beat me again
    i can't believe it only\ntook you {{WonRaceTime}} seconds!
    you're definitely the better racer
    i oughta try your route next time!
<<else>>
    gosh dang it!
    you're too good!
    how am i supposed to\nbeat {{WonRaceTime}} seconds?
    hmm...
    next time,
    i'll try doing what you did!
<<endif>>
[[AfterRaceMenu]]
===
title: LoseRegular
tags: 
colorID: 0
position: 2593,-1497
---
<<if visitCount() % 3 is 0>> 
    i won!
    you put up a good race
    <<SetEmotion Happy>>
    that was really fun!
    <<ClearEmotion>>
    [[GiveWalkieTalkie]]
<<elseif visitCount() % 3 is 1>>
    good race!
    you had some smooth moves
    i'm always be down for a\nrematch if you want one!
<<else>>
    yes!!
    i love a good race!
    thanks for racing me
    <<SetEmotion Happy>>
    i hope i haven't tired you out!
    <<ClearEmotion>>
<<endif>>
[[AfterRaceMenu]]
===
title: LoseGhost
tags: 
colorID: 0
position: 2333,-1593
---
<<if visitCount() % 3 is 0>> 
    i did it!
    finally!
    although, if i'm being honest
    i kinda... copied your strategy from last time
    [[GiveWalkieTalkie]]
<<elseif visitCount() % 3 is 1>>
    i won!
    phew!
    that was tight
    i hope you don't mind but
    i borrowed some of your\nmoves for that run!
<<else>>
    ahh i won!
    claire...
    i've learned so much from racing you!
    <<SetEmotion Happy>>
    i really owe you one!!
    i'm glad i've got\nyou as my racing buddy!
    <<ClearEmotion>>
<<endif>>
[[AfterRaceMenu]]
===
title: GiveWalkieTalkie
tags: 
colorID: 0
position: 2596,-1790
---
<<if HasItem("WalkieTalkie", 1) is false>>
    <<Hide>>
    <<Wait 1>>
    OH!
    i nearly forgot
    i've got something for you!
    <<Hide>>
    <<GiveItem WalkieTalkie 1>>
    <<Face Original Player>>
    if you ever get lost during a race
    ring me up and we can start over!
<<endif>>
[[AfterRaceMenu]]
===
title: AuntDynamic
tags: 
colorID: 0
position: 4114,-1871
---
<<SetSpeaker Player>>
it's kinda been a busy day
<<SetSpeaker Original>>
oh yeah?
what have you been up to?
<<SetSpeaker Player>>
well...

<<set $TalkPoints to 0>>

<<if HasItem("Toast", 1) and $TalkPoints < 3>>
    <<SetSpeaker Player>>
    {{Transition}}
    i got a piece of toast
    <<SetSpeaker Original>>
    ...and strawberries?!
    <<SetSpeaker Player>>
    no, just the toast
    <<SetSpeaker Original>>
    aw
    <<set $TalkPoints to $TalkPoints + 1>>
<<elseif $AteToast and $TalkPoints < 3>>
    <<SetSpeaker Player>>
    {{Transition}}
    i ate some toast
    <<SetSpeaker Original>>
    ...and strawberries?!
    <<SetSpeaker Player>>
    no, just the toast
    <<SetSpeaker Original>>
    aw
    <<set $TalkPoints to $TalkPoints + 1>>
<<endif>>

<<if HasItem("GoldenFishingRod", 1) and $TalkPoints < 3>>
    <<SetSpeaker Player>>
    {{Transition}}
    i caught a lot of fish!
    and i mean like,
    a LOT of fish
    i filled out a whole journal of them!
    <<SetSpeaker Original>>
    wow!
    you HAVE been busy
    <<set $TalkPoints to $TalkPoints + 1>>
<<elseif $FoundAndSoldRareFish and $TalkPoints < 3>>
    <<SetSpeaker Player>>
    {{Transition}}
    i caught a really rare fish!
    <<SetSpeaker Original>>
    since when did you learn to fish?
    <<SetSpeaker Player>>
    just today!
    <<SetSpeaker Original>>
    wow!
    you HAVE been busy
    <<set $TalkPoints to $TalkPoints + 1>>
<<elseif $DidFishTutorial and $TalkPoints < 3>>
    <<SetSpeaker Player>>
    {{Transition}}
    i learned how to fish!
    <<SetSpeaker Original>>
    did you now?
    <<SetSpeaker Player>>
    yeah!
    bill lent me a fishing rod
    <<SetSpeaker Original>>
    of course it was bill!
    i don't think i've ever\nseen him NOT fishing!
    <<set $TalkPoints to $TalkPoints + 1>>
<<endif>>

<<if $GotVolleyPrize2 and $TalkPoints < 3>>
    <<SetSpeaker Player>>
    {{Transition}}
    i don't mean to brag but
    i think i became a beachstickball\nworld champion today
    <<SetSpeaker Original>>
    what's beachstickball?
    <<SetSpeaker Player>>
    it's gonna be the next big thing
    it's like volleyball\nbut with a stick
    <<SetSpeaker Original>>
    i'd play that!
    <<set $TalkPoints to $TalkPoints + 1>>
<<elseif $PlayedBeachStickBall and $TalkPoints < 3>>
    <<SetSpeaker Player>>
    {{Transition}}
    i played a game of beachstickball
    <<SetSpeaker Original>>
    what's beachstickball?
    <<SetSpeaker Player>>
    it's some game the kids play
    <<SetSpeaker Original>>
    they're always playing something new
    i can't keep up with\nthe youth these days
    <<set $TalkPoints to $TalkPoints + 1>>
<<endif>>

<<if $_ClimbedSecretIslandTop and $TalkPoints < 3>>
    <<SetSpeaker Player>>
    {{Transition}}
    i swam out to a mysterious island!
    <<SetSpeaker Original>>
    you mean pat's point?
    <<SetSpeaker Player>>
    is that it's name?
    that makes it sound like
    way less mysterious
    <<SetSpeaker Original>>
    sorry!
    <<set $TalkPoints to $TalkPoints + 1>>
<<endif>>

<<if $_HitMagicRock is true and $TalkPoints < 3>>
    <<SetSpeaker Player>>
    {{Transition}}
    i whacked my shovel against this rock...
    ...and MONEY came out!
    it was a MONEY ROCK!
    <<SetSpeaker Original>>
    well now you're\njust making stuff up!
    i highly doubt you\nfound this "money rock"
    ...in ontario
    they're more of a\nwest-coast thing
    <<set $TalkPoints to $TalkPoints + 1>>
<<endif>>

<<if $BeatOneRace and $TalkPoints < 3>>
    <<SetSpeaker Player>>
    {{Transition}}
    i won a parkour race today!
    <<SetSpeaker Original>>
    wow!
    uh
    what's parkour?
    <<SetSpeaker Player>>
    uh it's like...
    jumping around and climbing stuff?
    <<SetSpeaker Original>>
    that sounds fun
    <<set $TalkPoints to $TalkPoints + 1>>
<<endif>>

<<if $_Artist6Talk and $TalkPoints < 3>>
    <<SetSpeaker Player>>
    {{Transition}}
    i met an artist today!
    they were painting landscapes here
    their art was amazing
    <<SetSpeaker Original>>
    well hawk peak is beautiful
    <<SetEmotion Happy>>
    so i'm not surprised!
    <<ClearEmotion>>
    <<set $TalkPoints to $TalkPoints + 1>>
<<endif>>

<<if $MetHikerAtTop and $TalkPoints < 3>>
    <<SetSpeaker Player>>
    {{Transition}}
    i made a friend at outlook point!
    the firetower is really cool
    <<SetSpeaker Original>>
    yeah!
    it's a great place\nto watch the sunset
    <<set $TalkPoints to $TalkPoints + 1>>
<<endif>>

<<if $_Broke_BIG_ROCK and $TalkPoints < 3>>
    <<SetSpeaker Player>>
    {{Transition}}
    i found a secret shortcut\nthrough the mountain!
    <<SetSpeaker Original>>
    you mean...
    you got through the\nblocked tunnels!?
    that's very dangerous
    they were blocked off\nfor a reason!
    <<SetSpeaker Player>>
    <<SetEmotion Happy Player>>
    haha whooooops!
    <<SetSpeaker Original>>
    claire!
    <<ClearEmotion Player>>
    <<set $TalkPoints to $TalkPoints + 1>>
<<endif>>

<<if $ListenedToFrogSpeech and $TalkPoints < 3>>
    <<SetSpeaker Player>>
    {{Transition}}
    i think i met a future\nprime minister today...
    either that or a\nmaster architect!
    <<SetSpeaker Original>>
    you mean sandcastle kid?
    <<SetSpeaker Player>>
    yeah the sandcastle kid
    <<SetSpeaker Original>>
    i'm not surprised
    that's classic sandcastle kid
    <<set $TalkPoints to $TalkPoints + 1>>
<<endif>>

<<if $JoinedClimbClub and $TalkPoints < 3>>
    <<SetSpeaker Player>>
    {{Transition}}
    i joined a rock climbing club
    <<SetSpeaker Original>>
    very nice!
    that's good exercise
    is it a big club?
    <<SetSpeaker Player>>
    uh
    it...
    ...depends?
    <<SetSpeaker Original>>
    hmm ok
    <<set $TalkPoints to $TalkPoints + 1>>
<<endif>>

<<if $ToughBirdFriend and $TalkPoints < 3>>
    <<SetSpeaker Player>>
    {{Transition}}
    i paid a semester's tuition today!
    <<SetSpeaker Original>>
    you WHAT?!
    <<SetSpeaker Player>>
    yeah uh
    not mine but
    i did
    <<SetSpeaker Original>>
    uhh
    that's good i guess
    <<set $TalkPoints to $TalkPoints + 1>>
<<elseif $DeliveredNecklace and $TalkPoints < 3>>
    <<SetSpeaker Player>>
    {{Transition}}
    i think i joined\nthe gig economy
    <<SetSpeaker Original>>
    you what?
    <<SetSpeaker Player>>
    eh it's a long story
    <<SetSpeaker Original>>
    well
    i hope the pay was good!
    <<set $TalkPoints to $TalkPoints + 1>>
<<endif>>

<<if $SoldHat and $TalkPoints < 3>>
    <<SetSpeaker Player>>
    {{Transition}}
    <<if "ParkHat" is $_WornHat>>
        i bought this rangers hat!
        you wear yours so well,
        i thought i'd better get one too!
        <<SetPose RaiseArms>>
        <<SetEmotion Happy>>
        what do ya think?
    <<else>>
        i bought a rangers hat!
        you wear yours so well,
        i thought i'd better get one too!
        check it out
        <<SetPose RaiseArms>>
        <<Hide>>
        <<Wait 0.25>>
        <<set $_WornHat to "ParkHat">>
        <<Wait 0.25>>
    <<endif>>
    <<SetSpeaker Original>>
    nice buy!
    <<SetEmotion Happy>>
    it looks great on you!  
    <<ClearEmotion>>
    <<ClearEmotion Player>>
    <<ClearPose Player>>
    <<set $TalkPoints to $TalkPoints + 1>>
<<elseif HasItem("Headband", 1) and $TalkPoints < 3>>
    <<SetSpeaker Player>>
    {{Transition}}
    i got this cool headband!
    <<if "Headband" is $_WornHat>>
        <<SetEmotion Happy>>
    <<else>>
        check this out
        <<SetPose RaiseArms>>
        <<Hide>>
        <<Wait 0.25>>
        <<set $_WornHat to "Headband">>
        <<Wait 0.25>>        
        <<SetEmotion Happy>>
        eh?
    <<endif>>
    pretty cool huh?
    <<ClearEmotion>>
    <<SetSpeaker Original>>
    it IS pretty cool
    <<ClearPose Player>>
    <<set $TalkPoints to $TalkPoints + 1>>
<<endif>>

<<set $AnyTreasureMapFound to $_Unearthed_LighthouseShadow or $_Unearthed_SidBeachTreasure or $_Unearthed_KingsThrone>>
<<if $AnyTreasureMapFound and $TalkPoints < 3>>
    <<SetSpeaker Player>>
    {{Transition}}
    i found some buried treasure!
    i had to solve a tricky riddle
    <<SetSpeaker Original>>
    a riddle huh?
    that's cool
    <<set $TalkPoints to $TalkPoints + 1>>
<<endif>>

<<if HasItem("Coin", 100) and $TalkPoints < 3>>
    <<SetSpeaker Player>>
    {{Transition}}
    i filled up my pockets!
    <<SetEmotion Happy>>
    <<SetPose RaiseArms>>
    look at all these coins i found!
    <<Hide>>
    <<ShowCollection Coin>>
    <<Wait 1.5>>
    <<HideCollection Coin>>
    <<SetSpeaker Original>>
    you aren't kidding!
    that's some serious dough!
    <<ClearEmotion Player>>
    <<ClearPose Player>>
    <<set $TalkPoints to $TalkPoints + 1>>
<<endif>>

<<if $TalkPoints == 0>>
    <<SetSpeaker Player>>
    uh
    i don't actually remember!
    <<SetSpeaker Original>>
    you must be pretty tired!
<<elseif $TalkPoints < 3>>
    <<SetSpeaker Player>>
    and
    i probably did a bunch more!
    i just don't remember...
    it's been a long day!
    <<SetSpeaker Original>>
    fair enough!
<<endif>>


[[AuntHungry]]
===
title: AuntHungry
tags: 
colorID: 0
position: 4367,-1866
---
<<SetSpeaker Original>>
anywhoo
you hungry?
<<set $TalkedAboutDinner to true>>
i'm gonna break out the hotdogs soon
<<if $CurryDinner is true>>
    <<SetSpeaker Player>>
    i thought you were\ngonna make curry?
    <<SetSpeaker Original>>
    eh
    now i'm feeling lazy
<<endif>>
<<SetSpeaker Player>>
well...
i'm kinda tired out
i'm gonna go take a nap or something
<<SetSpeaker Original>>
alrighty then!
<<SetEmotion Happy>>
have a good one!
<<ClearEmotion>>

<<set $HadFinalAuntDialogue to true>>
===
title: FoundFishPermit
tags: 
colorID: 0
position: 4140,715
---
huh?
it has something\nin its mouth...
it looks like...
a camping permit!?
<<Hide>> 
<<GiveItem CampingPermit 1>>
===
title: CampCatFreakout
tags: 
colorID: 0
position: 4390,484
---
sorry for freaking out earlier
i've been on edge ever\nsince i lost my permit
the thought of...
...being accused of camping\nwithout a permit...
that's the ultimate nightmare!
<<set $CampCatSorry to true>> 
===
title: IceSquirrelStart
tags: 
colorID: 0
position: 6362,933
---
<<if $ClimberRecapGroupTalk is false>>
    <<SetSpeaker Player>>
    hey tim!
    how's it going?
    [[ClimbersRestTalk]]
<<elseif $DiscussedNovel is false>>
    sooooo
    did you know
    [[DiscussedNovel]]
<<elseif $IceSquirrelTalkCount is 0>>
    <<SetSpeaker Player>> 
    so how'd you get up that cliff?
    <<SetSpeaker Original>> 
    well...
    i had some really good advice
    <<SetSpeaker SitRhino>> 
    <<SetEmotion Happy>> 
    aw thanks
    i tried to my best to help
    <<ClearEmotion>> 
    <<if $HeardClimbingSecret is true>>
        <<SetSpeaker Original>> 
        well actually
        i just remembered\nthe secret of climbing
        <<SetSpeaker Player>> 
        climb climb all the time?
        <<SetSpeaker Original>> 
        bingo!
    <<endif>>
    <<set $IceSquirrelTalkCount to 1>> 
<<elseif $IceSquirrelTalkCount is 1>> 
    remember
    the rock climbers association\nisn't JUST about climbing
    <<SetSpeaker Player>> 
    it isn't?
    <<SetSpeaker Original>>
    you're also expected to be an ACTIVE\nmember of the online community!
    it's very important
    <<SetSpeaker Player>> 
    i see
    <<set $IceSquirrelTalkCount to 2>> 
<<else>> 
    i'm a moderator on the forums
    so
    i'll know if you're\nnot participating!
<<endif>>
===
title: IceRhinoStart
tags: 
colorID: 0
position: 6595,934
---
<<if $ClimberRecapGroupTalk is false>>
    <<SetSpeaker Player>>
    hey you guys!
    how's it going?
    [[ClimbersRestTalk]]
<<elseif $DiscussedNovel is false>>
    <<SetSpeaker Player>>
    so what have you been up to?
    <<SetSpeaker Original>>
    just chattin' with charlie
    he's got a lot of good stories\nfrom working up here
    <<SetSpeaker SitSquirrel>>
    speaking of stories...
    i wanna read his NOVEL!!
    <<SetSpeaker Original>>
    tim!
    [[DiscussedNovel]]
<<elseif $IceRhinoTalk is 0>>
    claire
    i've got to say
    you're a really impressive climber
    <<SetSpeaker Player>>
    <<SetEmotion Happy>> 
    well, it helps that\ni can also fly
    it makes it a lot less risky
    <<ClearEmotion>>
    <<SetSpeaker Original>>
    don't sell yourself short
    <<SetEmotion Happy>> 
    you're awesome!
    <<ClearEmotion>>
    <<set $IceRhinoTalk to 1>> 
<<else>> 
    let's hike up to\nthe top tomorrow!
    <<SetEmotion Happy>> 
    you can show me the way!
    <<ClearEmotion>>
<<endif>>
===
title: SquirrelClimbStart
tags: 
colorID: 0
position: 6475,-69
---
THIS AIN'T HELPING CLAIRE!
===
title: ClimbersRestTalk
tags: 
colorID: 0
position: 6593,1240
---
<<LookAt Player SitBear>>
<<SetSpeaker SitSquirrel>>
<<LookAt SitSquirrel Player>>
<<LookAt Player SitSquirrel>>
OOHH
YOU KNOW
PRETTY GOOD
<<SetEmotion EyesClosed>>
I JUST CONQUERED HAWK PEAK
LIKE
NO BIG DEAL
<<ClearEmotion>>
<<SetSpeaker Player>>
nice!
you made it the top too?
<<SetSpeaker SitSquirrel>>
...
<<LookAt SitBear SitSquirrel>>
WELL
ALMOST CONQUERED IT
<<SetPose RaiseArms>>
<<LookAt Player SitSquirrel>>
WE'RE BASICALLY THERE RIGHT?
<<ClearPose>>
<<LookAt SitBear SitSquirrel>>
<<SetSpeaker SitBear>>
<<LookAt SitBear Player>>
<<LookAt SitSquirrel SitBear>>
well technically
this whole area is\nconsidered hawk peak
<<SetSpeaker SitSquirrel>>
<<LookAt Player SitSquirrel>>
<<LookAt SitSquirrel Player>>
SEE?
NAILED IT
<<SetSpeaker SitRhino>>
<<LookAt SitRhino Player>>
<<LookAt SitRhino SitBear>>
we're gonna make it to\nhawk peak's peak another day
i think we're at our limit
<<LookAt SitBear SitRhino>>
it's better not to push\nyourself too hard, right?
<<SetSpeaker SitBear>>
<<LookAt SitRhino SitBear>>
<<LookAt SitBear Player>>
definitely
safety always comes first
<<SetSpeaker SitRhino>>
<<ClearLookAt SitRhino>>
<<LookAt SitRhino Player>>
you made it up, though?
how was it?
<<SetSpeaker Player>>
<<LookAt Player SitBear>>
<<SetEmotion Happy>>
it was beautiful!
the northern lights are\nreally bright up there
<<ClearEmotion>>
<<SetSpeaker SitRhino>>
<<SetEmotion Happy>>
i can't wait to see it myself!
<<ClearEmotion>>

<<ClearLookAt SitBear>>
<<ClearLookAt Player>>
<<ClearLookAt SitRhino>>
<<ClearLookAt SitSquirrel>>
<<set $ClimberRecapGroupTalk to true>>
===
title: PolarBearSitStart
tags: 
colorID: 0
position: 6114,941
---
<<if $DiscussedNovel is false>>
    it's nice to have\nsome company up here
    usually it's pretty quiet
    <<SetSpeaker Player>>
    does it get boring up here?
    <<SetSpeaker Original>>
    well
    i keep busy
    <<SetSpeaker SitSquirrel>>
    claire, did ya hear?
    [[DiscussedNovel]]
<<elseif $BearSitTalkCount is 0>>
    <<SetSpeaker Player>> 
    can i read your story\nwhen it's done?
    <<SetSpeaker Original>> 
    well...
    i suppose so
    <<SetSpeaker Player>>
    <<SetEmotion Happy>> 
    awesome!
    i bet it'll be great!
    <<ClearEmotion>> 
    <<set $BearSitTalkCount to 1>> 
<<else>>
    keep warm out there!
<<endif>>
===
title: DiscussedNovel
tags: 
colorID: 0
position: 6331,1243
---
<<set $DiscussedNovel to true>>

<<SetSpeaker SitSquirrel>>
<<Face SitSquirrel Player>>
<<LookAt SitSquirrel Player>>
charlie's SUPPOSEDLY writing a book
<<LookAt SitBear SitSquirrel>>
<<LookAt SitBear SitRhino>>
<<SetSpeaker SitBear>>
<<LookAt SitSquirrel>>
<<LookAt SitBear Player>>
hey now!
i am...
...almost done
and
<<LookAt Player>>
it's only a short story!
<<SetSpeaker SitSquirrel>>
<<LookAt SitSquirrel Player>>
then why can't i read it!
<<SetSpeaker SitBear>>
<<LookAt SitSquirrel>>
<<LookAt SitBear Player>>
it's ALMOST done
it's not ready yet...
i'm having trouble with the ending
<<SetSpeaker SitSquirrel>>
<<LookAt SitSquirrel Player>>
oh well that's easy
just make it super sad
everyone cries everyone dies
that's how you make\na work of ART
<<SetSpeaker SitRhino>>
<<LookAt SitRhino Player>>
<<LookAt SitRhino SitSquirrel>>
i dunno
i like happy endings
like
i don't really get it
why would you want to put down a\nbook and feel worse than you\ndid when you picked it up?
<<SetSpeaker SitSquirrel>>
<<LookAt SitRhino>>
<<LookAt SitSquirrel Player>>
you don't feel worse!
it's...
FULFILLING!
sometimes you need sad endings!
<<SetSpeaker SitBear>>
<<LookAt SitBear Player>>
ahem
it's okay guys
i'll figure out the ending myself
<<ClearLookAt SitBear>>
<<ClearLookAt Player>>
<<ClearLookAt SitRhino>>
<<ClearLookAt SitSquirrel>>



===
title: Node231
tags: 
colorID: 0
position: 5205,2009
---


so how'd you get out of that pickle






----

hey!
i made it up to the top!

woah!
that's awesome!


YEAH
I GUESS THAT IS PRETTY AWESOME

how are you guys?

---






i really am writing a novel
then let us read it!
it's not ready yet!



<<SetSpeaker Player>>
tim!
you made it up!
<<SetSpeaker SitSquirrel>>
yeah
but
<<SetPose RaiseArms>>
AT WHAT COST!?
<<ClearPose>>
<<SetSpeaker SitRhino>>
no cost at all!
you made it up fine!
<<SetSpeaker SitSquirrel>>
<<SetPose RaiseArms>>
i could have DIED
MY CLIMBING CAREER...
...OVER BEFORE IT EVEN BEGAN!
<<ClearPose>>
<<SetSpeaker SitRhino>>
but you didn't die! 



hey!
you guys made it!

yup
i think we're gonna call it a day

i think i'm at my limit for today
i'm ready to try again tomorrow!
i'm gonna take a break 
you going to the top?


yup




SOO
CLAIRE
do you like happy or sad endings more
SOME people think sad endings are better

hey!
i'm not talking about sad endings!

what i just want to know!

did you know Charlie is writing a novel!
he won't let us read it


<<SetSpeaker SitBear>> 
hey!
is writing a novel!

i haven't decided if my novel\nshould have a happy ending\nor a tragic one...
<<SetSpeaker Player>>
-> do a happy ending!
    like, who actually likes sad endings!
    life is already sad enough
    i wanna see everyone's dreams come true
    and feel good about things at the end of the movie
    ...i mean book
    i don't read that much these days
    ...um
    no offense
-> make it super sad
    like, i mean REALLY sad
    everyone cries and everyone dies
<<SetSpeaker Original>>
hmm ok
===
title: FireOutRangerStart
tags: 
colorID: 0
position: 133,-219
---
<<Wait 1>> 
<<Face Original Player>>
<<if visited() is false>> 
    uh
    i guess
    it's good to be\nconcerned about fire safety...
    ...but i've got this\none under control
<<elseif visitCount() is 1>>
    so uhhhh
    i just relit that
    i'd like to keep it lit,\nif you don't mind
<<elseif visitCount() is 2>> 
    hey uh
    can you leave that alone?
    <<SetSpeaker Player>> 
    but there's a fire ban!
    <<SetSpeaker Original>> 
    it's actually just a fire caution
    <<SetSpeaker Player>> 
    oh
    alright
<<elseif visitCount() is 3>> 
    claire!
    <<SetSpeaker Player>> 
    i'm sorry!
    it looked like it was\nstarting to get too big
    better safe than sorry
    <<SetSpeaker Original>> 
    well
    you just said you WERE sorry
    <<SetSpeaker Player>>
    oops!
    yeah i guess i did
<<else>> 
    claire!!
    <<SetSpeaker Player>> 
    sorry!!
<<endif>> 
===
title: FireOutAuntStart
tags: 
colorID: 0
position: 129,24
---
<<Wait 1>> 
<<Face Original Player>>
<<if visited() is false>> 
    it is pretty hot out
    i guess we don't really\nneed a fire right now
<<elseif visitCount() is 1>>
    too hot for you?
    <<SetSpeaker Player>> 
    i dunno
    i was just curious what you'd do
    <<SetSpeaker Original>> 
    huh
<<else>>
    welp
    i'm sure getting a lot\nof fire starting practice
<<endif>> 
===
title: FireOutBearStart
tags: 
colorID: 0
position: 137,257
---
<<if $_CampfireFriendsBegin is true>> 
    <<Wait 1>> 
    <<Face Original Player>>
    <<if visited() is false>>
        claire
        i like your initiative!
        fire safety is no joke!
    <<elseif visitCount() is 1>>
        nice work claire!
        the fire's nice to look at
        but it's not worth\ncompromising our safety
    <<elseif visitCount() is 2>> 
        who needs fire anyway
        i'm comfortable with or without
        <<SetSpeaker SitRhino>> 
        well
        i like the fire...
    <<else>> 
        <<SetSpeaker SitRhino>> 
        maybe it's just me
        but i'd like to keep the fire going
        <<SetSpeaker SitSquirrel>> 
        seconded
    <<endif>> 
<<endif>> 
===
title: IceHikerPeak
tags: 
colorID: 0
position: 5098,702
---
<<if visited() is false>>
    <<SendMessage Original CameraShift1>>
    woah
    claire
    <<Face Player>>
    this is better than i\ncould have imagined!
    <<SetEmotion Happy>>
    thanks for helping\nme get up here!
    <<ClearEmotion>>
    <<SetSpeaker Player>>
    <<SetEmotion Happy>>
    it was no problem!
    really!
    <<ClearEmotion>>

    <<if $RequestFoxPhoto is true>>
        <<SetSpeaker Original>>
        so
        <<SetEmotion Happy>>
        you still want that photo?
        <<ClearEmotion>>
        <<SetSpeaker Player>>
        oh yeah!
        let's do it!
        <<SetSpeaker Original>>
        ok!
        hold on
    <<else>> 
        <<SetSpeaker Original>>
        these lights are so incredible
        i hope they turn out on film!
        i lugged this fancy camera\nall the way up here...
        it better be worth it!
        <<SetSpeaker Player>>
        well
        the camera on my phone sucks
        so at least it's\nbetter than that!
        <<SetSpeaker Original>>
        hmm
        want me to take your photo?
        this would make a\nsweet profile pic!
        <<SetSpeaker Player>>
        aww yeah!
        that'd be great!
        <<SetSpeaker Original>>
        ok!
        hold on
    <<endif>> 

    <<Hide>>
    <<SetPose TakePhoto>>
    <<Wait 0.25>>
    <<SendMessage Original ShowPropCamera>>
    <<SendMessage Original CameraShift2>>
    <<SendMessageCoroutine Original WalkPlayer>>
    <<Wait 1>>
    <<Face Player Original>>

    strike a pose!
    <<SetSpeaker Player>>
    oh
    uhhhhh
    <<Hide>>
    <<SetPose CoolPose Player>>
    <<Wait 1.5>>

    <<SetSpeaker Original>>
    amazing
    
    <<Hide>>
    <<Wait 0.5>>
    <<SetEmotion Happy Player>>
    <<Wait 0.5>>
    <<PlaySound Camera>>
    <<Wait 1.5>>
    <<ClearEmotion Player>>
    <<Wait 0.5>>

    <<ClearPose Player>>
    <<Wait 0.5>>
    <<ClearPose Original>>
    <<Wait 0.1>>
    <<SendMessage Original CameraShift1>>
    <<Wait 0.1>>
    <<SendMessage Original HidePropCamera>>

    <<Wait 1.2>>
    <<SetSpeaker Original>>
    the northern lights\nturned out great!
    <<SetEmotion Happy>>
    what a nice photo!
    <<ClearEmotion>>
    oh, before i forget
    here's the feathers\nyou lent me
    <<Hide>>
    <<GiveItem GoldenFeather 6>>
    <<Wait 1>>
    <<SetSpeaker Player>>
    <<Face Original>>
    thanks!
    <<SetEmotion Happy>>
    make sure you email\nme that photo!
    <<ClearEmotion>>
    <<SetSpeaker Original>>
    <<SendMessage Original CameraShift3>>
    i will!
<<else>>
    i'm gonna chill here for a while
    i don't know when\ni'll be back so...
    i wanna soak it all\nup while i can
<<endif>>
===
title: IceHikerAfterPeak
tags: 
colorID: 0
position: 4850,953
---
<<if visited() is false>>
    any luck getting to the top?
    <<SetSpeaker Player>>
    yeah!
    i was just up there
    <<SetSpeaker Original>>
    !!
    can you show me the route?
    <<SetSpeaker Player>>
    well
    you kinda need to...
    ...be able to fly
    <<SetSpeaker Original>>
    oh
    right...
    <<SetSpeaker Player>>
    maybe you could climb up?
    <<SetSpeaker Original>>
    i don't have enough\ngolden feathers...
    <<SetSpeaker Player>>
    i could lend you some!
    <<SetSpeaker Original>>
    you would?
    <<SetSpeaker Player>>
    tots!
    how many do you need?
    <<SetSpeaker Original>>
    uhh
    it's kinda embarrassing
    but i'd need a lot
    like, 6
    <<SetSpeaker Player>>
    <<if HasItem("GoldenFeather", 6) is true>>
        -> oh, sorry
            <<SetSpeaker Original>>
            don't worry about it!
            i know it's a lot to ask
        -> i can lend you 6 feathers!
            [[IceHikerLend]]
    <<else>>
        oh
        sorry...
        i don't enough to help you
        <<SetSpeaker Original>>
        really,
        don't worry about it!
    <<endif>>
<<else>>
    i'm so close yet so far!
    <<if HasItem("GoldenFeather", 6) is true>>
        <<SetSpeaker Player>>
        -> i can lend you 6 feathers!
            [[IceHikerLend]]
        -> good luck!
            <<SetSpeaker Original>>
            thanks!
            i'll figure something out
    <<endif>>
<<endif>>



===
title: IceHikerLend
tags: 
colorID: 0
position: 4853,1202
---
<<SetSpeaker Original>>
<<if HasItem("GoldenFeather", 12) is true>>
    really!?
    <<SetSpeaker Player>>
    of course!
    <<Hide>>
    <<GiveItem GoldenFeather -6>>
    <<Wait 1>>
    <<SetSpeaker Original>>
    you're amazing, claire!
    i'll see you up there!
    <<SendMessage Original StartClimbing>>
<<else>>
    but
    you'd be giving me more\nthan half of your feathers!
    i don't wanna do that to you
<<endif>>

===
title: WinMedal
tags: 
colorID: 0
position: 1628,-1670
---
<<set $BeatOneRace to true>>
<<if visitCount() % 3 is 0>> 
    <<SetPose LookingDown>> 
    <<SetEmotion EyesClosed>> 
    oof
    hold on
    i gotta...
    ...catch my breath
    <<Hide>> 
    <<Wait 2>>
    okay!
    <<ClearEmotion>> 
    <<ClearPose>> 
    *ahem*
    claire
    you got me beat!
    i think i owe you this
    <<Hide>> 
    <<GiveItem Medal 1>> 
    <<Face Original Player>> 
    <<SetSpeaker Player>>
    woah!
    where'd ya get this?
    <<SetSpeaker Original>> 
    <<SetEmotion EyesClosed>>
    i have my ways!
    <<ClearEmotion>> 
    ...anyway
    next time we race,
    i'll be sure to use what\ni learned from you!
<<elseif visitCount() % 3 is 1>> 
    claire!
    you are a SPEED DEMON
    and you know what speed demons get?
    <<SetSpeaker Player>>
    uhhhhhh?
    <<SetSpeaker Original>>
    they get AWARDS!!
    <<Hide>> 
    <<GiveItem Medal 1>> 
    <<Face Original Player>>
    don't think i'm done yet!
    i'm still improving, so
    i'll get you next time!
<<else>>
    that was one intense race
    welp
    i'd better hand this over
    <<Hide>> 
    <<GiveItem Medal 1>> 
    <<Face Original Player>>
    that's all the medals i got
    but if you ever want a rematch,
    i'll do better next time!
<<endif>>

[[AfterRaceMenu]]
===
title: InspectMedalStart
tags: 
colorID: 0
position: 927,-1991
---
<<if visited() is false>> 
    it says
    "certified better than Avery"
    huh?
    it looks like...
    ...it's just a gold coin glued a ribbon?
<<else>> 
    it's a gold coin glued to a ribbon
<<endif>>

-> extract the coin
    [[ExtractCoin]] 
-> leave it be
===
title: ExtractCoin
tags: 
colorID: 0
position: 926,-1741
---
<<if IsVisible("RaceOpponent") is true>>
    [[NotExtractCoin]] 
<<else>>
    <<if $BrokenMedal is false>> 
        i feel a little bad about this...
        but
        eh
    <<endif>>
    <<Hide>>
    <<SetPose LookingDown>> 
    <<PlaySound RipCoin>>
    <<Wait 1.2>>
    <<ClearPose>> 
    <<GiveItem Coin 7>> 
    <<GiveItem Medal -1>> 
    <<set $BrokenMedal to true>> 
<<endif>>
===
title: NotExtractCoin
tags: 
colorID: 0
position: 680,-1741
---
<<if visited() is false>> 
    uhh
    ...
    maybe i shouldn't do this\nwhere they can see me
<<else>> 
    i'm too close!
    i don't wanna hurt\ntheir feelings...
<<endif>> 
===
title: JimStart
tags: 
colorID: 0
position: 4259,1870
---
<<if visited() is false>> 
    <<SetSpeaker Player>> 
    woah!
    is that a metal detector?
    <<SetSpeaker Original>> 
    yeah baby
    this lil' thing is going\nto make me rich
<<elseif visitCount() is 1>> 
    could i try it out?
    <<SetSpeaker Original>> 
    excuse me?
    this is MY metal detector
    it was an INVESTMENT!
    i plan to maximize my return,\nthank you very much!
<<elseif visitCount() is 2>>
    have you found anything cool?
    <<SetSpeaker Original>> 
    well, yeah
    obviously
    i didn't buy this for nothing!
    ...
    it IS a little sensitive, though
    apparently there's something nearby
    but i can't...pinpoint just where...
    well
    if even i can't find it,\ni DOUBT anyone else can!
<<elseif visitCount() is 3>>
    i wonder where it could be...
<<endif>>
===
title: HatSellerStart
tags: 
colorID: 0
position: 4289,3914
---
<<if $SunhatPrice != 0>>
    [[HatSell]]
<<elseif $BoughtSunhat is false>>
    <<SetEmotion Happy>>
    hellooooooooo <sprite name="Music" tint=1>
    <<ClearEmotion>> 
    would you like to buy a sunhat?
    they're handmade!
    <<SetSpeaker Player>> 
    they look nice!
    how much are they?
    <<SetSpeaker Original>> 
    <<SetEmotion EyesClosed>> 
    hmm
    <<ClearEmotion>> 
    i don't know!
    pricing things is hard
    what do you think is a fair price?
    <<SetSpeaker Player>> 
    oh uhhh
    [[HatPriceMenu]]
<<else>> 
    <<SetSpeaker Player>> 
    why is your shop all\nthe way out here?
    <<SetSpeaker Original>> 
    i like it here!
    <<SetSpeaker Player>> 
    fair enough!
<<endif>> 
===
title: HatPriceMenu
tags: 
colorID: 0
position: 4537,3915
---
<<if $HatPrice1Tried and $HatPrice25Tried and $HatPrice50Tried and $HatPrice100Tried and $HatPrice200Tried>>
    [[HatPriceUnknown]]
<<endif>>

<<SetSpeaker Player>> 
<<ShowCollection Coin>>
<<if $HatPrice1Tried is false>> 
    [[1 coin|HatPrice1]]
<<endif>> 
<<if $HatPrice25Tried is false>> 
    [[25 coins|HatPrice25]]
<<endif>> 
<<if $HatPrice50Tried is false>> 
    [[50 coins|HatPrice50]]
<<endif>> 
<<if $HatPrice100Tried is false>> 
    [[100 coins|HatPrice100]]  
<<endif>> 
<<if $HatPrice200Tried is false>> 
    [[200 coins|HatPrice200]]  
<<endif>> 
<<if $HatPriceUnknownTried is false>> 
    [[i have no idea!|HatPriceUnknown]] 
<<endif>> 
===
title: HatSell
tags: 
colorID: 0
position: 4625,3307
---
okay! sale time
===
title: HatPrice1
tags: 
colorID: 0
position: 4052,3542
---
<<SetSpeaker Original>>
<<HideCollection Coin>>
...
oh, um
you don't really mean\nthat do you?
<<SetSpeaker Player>> 
uh
i was just kidding!
sorry
that was a bad joke...
<<SetSpeaker Original>> 
ah, okay...
<<set $HatPrice1Tried to true>>
[[HatPriceMenu]] 
===
title: HatPrice25
tags: 
colorID: 0
position: 4295,3539
---
<<SetSpeaker Original>>
<<HideCollection Coin>>
oh... um
that's pretty low...
it took me a while\nto make each hat
<<SetSpeaker Player>>
oh! sorry
i really have no\nidea what to charge!
<<SetSpeaker Original>>
well
if it's all you can afford...
i guess i could give\nyou a first sale discount?
<<SetSpeaker Player>>
-> 25 is a good price!
    <<set $SunhatPrice to 25>>
    [[HatSell]]
-> actually, that's not right
    <<set $HatPrice25Tried to true>>
    [[HatPriceMenu]] 
===
title: HatPrice50
tags: 
colorID: 0
position: 4525,3542
---
<<SetSpeaker Original>>
<<HideCollection Coin>>
hmm
i guess 50 coins is alright
<<SetSpeaker Player>>
how do you feel about that price?
<<SetSpeaker Original>>
um
well
i think the craftsmanship is really nice
i thought i might be able to charge more
but, i don't really know!
<<SetSpeaker Player>>
-> 50 coins sounds good!
    <<set $SunhatPrice to 50>>
    [[HatSell]]
-> wait, maybe another price
    <<set $HatPrice50Tried to true>>
    [[HatPriceMenu]] 
===
title: HatPrice100
tags: 
colorID: 0
position: 4750,3542
---
<<SetSpeaker Original>>
<<HideCollection Coin>>
i think that's a good price!
<<SetSpeaker Player>>
yeah?
<<SetSpeaker Original>>
well
i've seen some other\nhats sell for that...
and
i think mine is\nas good as theirs?
<<SetSpeaker Player>>
-> 100 coins sounds good!
    <<set $SunhatPrice to 100>>
    [[HatSell]]
-> wait, maybe not 100 coins...
    <<set $HatPrice100Tried to true>>
    [[HatPriceMenu]] 
===
title: HatPrice200
tags: 
colorID: 0
position: 4977,3540
---
<<SetSpeaker Original>>
<<HideCollection Coin>>
woah!
i think that might\nbe a bit much
but... i'd really appreciate it!
<<SetSpeaker Player>>
-> i'd buy it for 200 coins!
    <<set $SunhatPrice to 200>>
    [[HatSell]]
-> wait, that might be a bit high...
    <<set $HatPrice200Tried to true>>
    [[HatPriceMenu]] 
===
title: HatPriceUnknown
tags: 
colorID: 0
position: 5200,3535
---
<<SetSpeaker Original>>
<<HideCollection Coin>>
that's ok
<<SetSpeaker Player>>
oh! sorry
i really have no\nidea what to charge!
<<SetSpeaker Original>>
well
if it's all you can afford...
i guess i could give\nyou a first sale discount?
<<SetSpeaker Player>>
-> that price is good for me!
    <<set $SunhatPrice to 90>>
    [[HatSell]]
-> nevermind, that's not a good price...
    <<set $HatPriceUnknownTried to true>>
    [[HatPriceMenu]] 
===
title: SunhatSellerStart
tags: 
colorID: 0
position: 5615,3000
---
<<if $BoughtSunhat is true>>
    <<SetSpeaker Player>> 
    so...
    why are you all\nthe way out here?
    <<SetSpeaker Original>> 
    i like it here!
    <<SetSpeaker Player>> 
    fair enough!
<<elseif $SunhatPrice > 0>>
    <<SetSpeaker Player>> 
    hello!
    how much was the sunhat, again?
    <<SetSpeaker Original>>
    i think we decided on...
    {{$SunhatPrice}} coins?
    [[HatSellNew]]
<<else>>
    <<SetSpeaker Player>> 
    i like your sunhat!
    <<SetSpeaker Original>> 
    <<SetEmotion Happy>>
    thank you!
    i made it myself
    <<ClearEmotion>> 
    <<SetSpeaker Player>> 
    woah!
    that's awesome
    <<SetSpeaker Original>>
    i'm making a bunch for\nthe upcoming craft fair
    if you're interested,
    i could sell you one now!
    <<SetSpeaker Player>> 
    oh!
    how much are they?
    <<SetSpeaker Original>> 
    <<SetEmotion EyesClosed>> 
    hmm
    <<ClearEmotion>> 
    i don't know!
    pricing things is hard
    um...
    do you think 100 coins seems fair?
    <<ShowCollection Coin>>
    <<SetSpeaker Player>> 
    -> sounds good to me!
        <<HideCollection Coin>>
        <<SetSpeaker Original>>
        great!
        i think the craftsmanship\nreally shows...
        but i was a little afraid\nto ask that much!
        <<set $SunhatPrice to 100>>
        [[HatSellNew]]
    -> it's a bit pricey
        <<HideCollection Coin>>
        <<SetSpeaker Original>>
        oh!
        umm...
        ...
        sorry...
        ...i'm just a\nlittle embarrassed!
        <<SetSpeaker Player>> 
        oh, no!
        i'm sorry!
        i really have no idea\nwhat a good price is...
        <<SetSpeaker Original>>
        umm...
        what about 75 coins?
        <<SetSpeaker Player>> 
        i think that's pretty good!
        <<set $SunhatPrice to 75>>
        [[HatSellNew]]
    -> i'd pay more!
        <<HideCollection Coin>>
        i REALLY like the hat!
        you can tell a lot\nof love went into it!
        i wouldn't want to\nsell you short
        <<SetSpeaker Original>>
        aw
        <<SetEmotion Happy>> 
        thank you!
        <<ClearEmotion>>
        do you think i could\ncharge 125 coins?
        <<SetSpeaker Player>> 
        yeah, easily!
        <<set $SunhatPrice to 125>>
        [[HatSellNew]]
<<endif>> 
===
title: HatSellNew
tags: 
colorID: 0
position: 5866,2999
---
<<SetSpeaker Player>>
<<ShowCollection Coin>>
-> i'll take a hat!
    <<HideCollection Coin>>
    [[BuySunhat]]
-> see ya later!
    <<HideCollection Coin>>
    <<SetSpeaker Original>>
    <<if $SunhatPrice < 100>>
        okay...
        i'll see you around
    <<else>>
        <<SetEmotion Happy>>
        'till next time!
        <<ClearEmotion>>
    <<endif>>
    
===
title: BuySunhat
tags: 
colorID: 0
position: 5870,3253
---
<<if HasItem("Coin", $SunhatPrice) is true>>
    <<SetSpeaker Original>>
    <<if $SunhatPrice < 100>>
        okay!
        um
        here you go
    <<else>>
        <<SetEmotion Happy>>
        great!
        here you go!
        <<ClearEmotion>>
    <<endif>>

    <<set $SunhatCost to $SunhatPrice * -1>>
    <<Hide>>
    <<GiveItem Coin $SunhatCost>>
    <<PlaySound BuySound>>
    <<Hide>>
    <<Wait 1>>
    <<GiveItem Sunhat 1>>
    <<set $BoughtSunhat to true>>

    <<if $SunhatPrice < 100>>
        i hope you like it
        <<SetSpeaker Player>>
        i do!
        it's really nice
        <<SetSpeaker Original>>
        aw
        thank you!
    <<elseif $SunhatPrice > 100>>
        <<SetEmotion Happy>>
        thanks so much!!
        i'm so happy you like it!
        <<ClearEmotion>>
        <<SetSpeaker Player>>
        <<SetEmotion Happy>>
        thank YOU
        it's so pretty!
        i love it!
        <<ClearEmotion>>
    <<else>>
        thanks!
        <<SetEmotion Happy>>
        you're my first customer!
        <<ClearEmotion>>
        <<SetSpeaker Player>>
        <<SetEmotion Happy>>
        the first of many!
        it's a great hat!
        <<ClearEmotion>>
    <<endif>>
<<else>>
    <<SetSpeaker Original>>
    oh, um
    i think you're short\nby a few coins...
<<endif>>
===
title: GIFStart
tags: 
colorID: 0
position: 5235,3171
---
<<SetSpeaker Original>>
have you seen any bugs lately?
<<SetSpeaker Player>>
no...
i don't think so?
<<SetSpeaker Original>>
phew!
cuz the game comes out TOMORROW\n on PC, Mac, and Linux!
it would be a bad time\nto find a new bug!!
<<SetSpeaker Player>>
wait
what are you talking about?
<<SetSpeaker Original>> 
this game, A Short Hike!
<<SetEmotion Happy>> 
available July 30th on\nboth Itch.io and Steam!
<<ClearEmotion>> 
<<SetSpeaker Player>>
uh...
<<SetPose LookingDown>>
<<SetEmotion EyesClosed>> 
i'm so confused
<<ClearEmotion>> 
<<ClearPose>> 
===
title: DeerStart
tags: 
colorID: 0
position: 3699,1290
---
<<if $PickedUpTrash is true>> 
    [[Deer2]] 
<<elseif visited() is false>>
    *sigh*
    i just spent the past\nhour picking up garbage...
    it looks like someone threw\na party here and left\nhalf of it behind!
    <<SetSpeaker Player>>
    what!?
    that's NOT cool
    <<SetSpeaker Original>>
    right!?
    ugh
    <<SetSpeaker Player>>
    i could help you with the rest!
    it looks like you've\nalready done most of it!
    <<SetSpeaker Original>>
    <<SetEmotion Happy>> 
    really?
    that would be fantastic
    thank you!
    <<ClearEmotion>> 
<<elseif HasItem("Trash", 9)>> 
    <<SetSpeaker Player>>
    i think i got it all!
    <<Hide>>
    <<GiveItem Trash -9>>
    <<Wait 1>>
    <<SetSpeaker Original>>
    <<SetEmotion Happy>> 
    that looks like everything!
    <<ClearEmotion>> 
    this is SO much better!
    <<SetEmotion Happy>> 
    thanks for your help!
    <<ClearEmotion>> 
    <<SetSpeaker Player>>
    <<SetEmotion Happy>> 
    no problem!
    <<set $PickedUpTrash to true>> 
    <<ClearEmotion>> 
<<elseif HasItem("Trash", 5)>> 
    <<SetSpeaker Player>>
    is this all of it?
    <<SetSpeaker Original>>
    i think there's a little more
<<else>>
    <<SetSpeaker Player>>
    they really left behind a mess huh?
    <<SetSpeaker Original>>
    yeah...
    can you believe the\nnerve of some people?
<<endif>> 


===
title: Node257
tags: 
colorID: 0
position: 3976,1254
---

===
title: Deer2
tags: 
colorID: 0
position: 3708,1543
---
<<if $BoughtSunhat is true>>
    [[Deer3]] 
<<elseif visited() is false>> 
    <<SetSpeaker Player>> 
    i like your sunhat!
    <<SetSpeaker Original>> 
    <<SetEmotion Happy>>
    thank you!
    i made it myself
    <<ClearEmotion>> 
    <<SetSpeaker Player>> 
    really?!
    that's, like, intense!
    i didn't know you could\njust like... MAKE a hat!
    <<SetSpeaker Original>>
    yup it's true
    but it takes a while
    i'm making a bunch for\nthe upcoming craft fair
    if you're interested,
    i could sell you one now!
    <<SetSpeaker Player>> 
    oh yeah!?
    how much are they?
    <<SetSpeaker Original>> 
    <<SetEmotion EyesClosed>> 
    hmm
    <<ClearEmotion>> 
    pricing things is hard...
    how does 100 coins sound?
    <<SetSpeaker Player>>
    -> sounds good to me!
        <<SetSpeaker Original>> 
        great!
        i think it's a fair price!
        <<set $SunhatPrice to 100>>
        [[DeerHatSell]]
    -> i'd pay more!
        i REALLY like the hat!
        you can tell a lot\nof love went into it!
        <<if $KnowsAboutMerchHat is true>>
            the manufactured ranger\nhats cost 100 coins...
            ...but you can tell\nyours is handcrafted!
        <<endif>> 
        i wouldn't want to\nsell you short
        <<SetSpeaker Original>>
        aw
        <<SetEmotion Happy>> 
        thank you!
        <<ClearEmotion>>
        do you think i could\ncharge 125 coins?
        <<SetSpeaker Player>> 
        yeah, easily!
        <<set $SunhatPrice to 125>>
        [[DeerHatSell]]
<<else>>
    <<SetSpeaker Player>> 
    hello!
    how much was the sunhat, again?
    <<SetSpeaker Original>>
    {{$SunhatPrice}} coins!
    [[DeerHatSell]]
<<endif>>
===
title: DeerHatSell
tags: 
colorID: 0
position: 3982,1548
---
<<SetSpeaker Player>>
<<ShowCollection Coin>>
-> i'll take a hat!
    <<HideCollection Coin>>
    [[BuyDeerHat]]
-> see ya later!
    <<HideCollection Coin>>
    <<SetSpeaker Original>>
    <<SetEmotion Happy>>
    'till next time!
    <<ClearEmotion>>
    
===
title: BuyDeerHat
tags: 
colorID: 0
position: 4238,1545
---
<<if HasItem("Coin", $SunhatPrice) is true>>
    <<SetSpeaker Original>>
    <<SetEmotion Happy>>
    great!
    <<ClearEmotion>>
    <<if $SunhatPrice > 100>>
        do you want to choose\nthe ribbon color?
        i don't mind changing\nit for you!
        <<SetSpeaker Player>>
        oh! hmm
        do you have...
        -> pink?
            <<set $_SunhatRibbon to 0>>
        -> red?
            <<set $_SunhatRibbon to 1>>
        -> blue?
            <<set $_SunhatRibbon to 2>>
        -> purple?
            <<set $_SunhatRibbon to 3>>
        <<SetSpeaker Original>>
        yup!
    <<endif>> 
    here you go!

    <<Hide>>
    <<set $SunhatCost to $SunhatPrice * -1>>
    <<GiveItem Coin $SunhatCost>>
    <<PlaySound BuySound>>
    <<Wait 1>>
    <<GiveItem Sunhat 1>>
    <<set $BoughtSunhat to true>>
    <<Face Original Player>> 

    <<if $SunhatPrice > 100>>
        <<SetEmotion Happy>>
        thanks so much!!
        i'm so happy you like it!
        <<ClearEmotion>>
        <<SetSpeaker Player>>
        <<SetEmotion Happy>>
        thank YOU
        it's so pretty!
        i love it!
        <<ClearEmotion>>
    <<else>>
        thanks!
        <<SetEmotion Happy>>
        you're my first customer!
        <<ClearEmotion>>
        <<SetSpeaker Player>>
        <<SetEmotion Happy>>
        the first of many!
        it's a great hat!
        <<ClearEmotion>>
    <<endif>>
<<else>>
    <<SetSpeaker Original>>
    oh, um
    i think you're short\nby a few coins
    <<SetSpeaker Player>>
    oops
    i'll be back i guess!
<<endif>>
===
title: Deer3
tags: 
colorID: 0
position: 3704,1789
---
<<if visited() is true and $SunhatPrice > 100 and "Sunhat" is $_WornHat>> 
    <<SetEmotion Happy>>
    so how do you like the color?
    <<ClearEmotion>> 
    <<SetSpeaker Player>>
    -> it's nice!
        <<SetSpeaker Original>>
        cool
    -> can i try a different one?
        <<SetSpeaker Original>> 
        sure!
        whatcha thinking?
        <<SetSpeaker Player>>
        -> pink
            <<SetSpeaker Original>>
            the classic
            <<set $_SunhatRibbon to 0>>
        -> red
            <<SetSpeaker Original>>
            on it
            <<set $_SunhatRibbon to 1>>
        -> blue
            <<SetSpeaker Original>>
            totally
            <<set $_SunhatRibbon to 2>>
        -> purple
            <<SetSpeaker Original>>
            no problem!
            <<set $_SunhatRibbon to 3>>
<<else>> 															 
this is one of my favourite\nspots to come unwind
it's relaxing just to sit\nhere and work on my hats!
<<endif>>  
===
